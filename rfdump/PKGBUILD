# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=rfdump
pkgver=1.6
pkgrel=5
pkgdesc='Tool to detect RFID-Tags and show their meta information'
url='http://www.rfdump.org'
arch=('x86_64')
license=('GPL2')
depends=('gtk3' 'expat')
source=(http://www.rfdump.org/dl/${pkgname}-${pkgver}.tar.bz2
        drop-libglade.patch
        format-security.patch)
sha512sums=('3402b9147b3e255b3c957d92f1fa4d7610fb6fde0aef2e3205343f64b252635bcf6345fb904b18ded063df775f15379a726b7b3b166b13d94efc9712373c7df7'
            '978ad544513535e5177c579718ac2f81e9ec33c66ddd1713264b20fc3ee6d87c0b23e96d17e803c18dfaeaf2602ce9f4bf1022f59a44e89752d8463c5271bc3e'
            '8204f064a8a6e20a84abaccc175b847cbd26516bca67a7f7eac9fb182a878c7e8b9afd07f41be3634a982f3670f7f9e02a03390d8965d09e3dcf92ffc831cb44')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../drop-libglade.patch
  patch -p0 -i ../format-security.patch
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  CFLAGS+=' -fcommon'
  LIBS=-lexpat ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
