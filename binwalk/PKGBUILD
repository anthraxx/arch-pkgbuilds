# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Frederik Schwan <freswa at archlinux dot org>
# Contributor: Peter Wu <peter@lekensteyn.nl>
# Contributor: Miguel Revilla <yo@miguelrevilla.com>
# Contributor: s1gma <s1gma@mindslicer.com>

pkgname=binwalk
pkgver=2.3.2
pkgrel=1
pkgdesc='Tool for searching a given binary image for embedded files'
url='https://github.com/ReFirmLabs/binwalk'
arch=('any')
license=('MIT')
depends=('python')
optdepends=(
  'python-opengl: binviz module support'
  'python-capstone: disassembly support'
  'arj: ARJ decompression support'
  'cabextract: CAB archive support'
  'cpio: CPIO archvie support'
  'gzip: GZIP decompression support'
  'mtd-utils: JFFS filesystem support'
  'p7zip: ZIP, LZMA and ISO decompression support'
  'squashfs-tools: squashfs support'
  'tar: TAR archive support'
  'bzip2: BZIP2 archive support'
  'unrar: RAR decompression support'
  'xz: XZ decompression support'
  'lhasa: LHA support'
  'sleuthkit: forensic analysis support'
)
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('7107e322bdbd2464c950b611d85fa20db59aefab5dbb134a341be72f683504683e7e43ab3cc78310d132f86d6289222cf991fb08923da9c33f13adc523456d3d')
b2sums=('9ae21e570c6d7953eb025da9d0cc7c73953b948ef25e82d1948d265da9520618244763fefe4576e604806f56bd70306c4dd271259254a13935eaf11fcb199354')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install -O1 --prefix="${pkgdir}/usr"
  install -Dm 644 *.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
