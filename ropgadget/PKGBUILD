# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: s1gma <s1gma@mindslicer.com>

pkgname=ropgadget
_pkgname=ROPgadget
pkgver=6.8
pkgrel=1
pkgdesc='Search gadgets in binaries to facilitate ROP exploitation for several file formats and architectures'
url='http://www.shell-storm.org/project/ROPgadget'
arch=('any')
license=('BSD')
depends=('python' 'python-capstone')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel')
source=(https://github.com/JonathanSalwan/${_pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('9e14d5a180a2d2d974bf69fb6456d9a68f197d4d4dda1de16374a2c81b0b2780b220d6a73d155be65d5eee03273fdc94f76235fe645511646bc3b29c07e6e828')
b2sums=('f7278ea0934e61f6afcd95f3c3047a34cf07bfaa52b67514ad2c63aa06104ee1c3c13bf77275ae89a406e2db5f238b95353d2cef7b696b3b53769434775183f3')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed 's|python2|python|g' -i ropgadget/**/*.py ropgadget/*/*/*.py
}

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pkgname}-${pkgver}
  ./ROPgadget.py --binary ./test-suite-binaries/elf-Linux-x86 --string "main"
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE_BSD.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
