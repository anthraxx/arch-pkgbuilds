# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-libarchive-c
pkgver=3.2
pkgrel=3
pkgdesc='Python interface to libarchive'
url='https://github.com/Changaco/python-libarchive-c'
arch=('any')
license=('CC0')
depends=('python' 'libarchive')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-mock')
source=(https://github.com/Changaco/python-libarchive-c/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('d11a7da0e592b7fce5044b9ff2930e50004c15b166a76f7871b9d0d584cb6da5989c4ec79969439b00baffa564738af607cdf5e37141c6e879b8a76ae68c325c')
b2sums=('9e984c8e23422ceb552d34d4560b2c8279e1c2d52ffedcc893af3a1a8420eb03b735058d2f8dbe542f3920d0f0bf53777f0679a238cc0368e8e680c44358a72c')

prepare() {
  sed '/os.umask(2)/d' -i ${pkgbase}-${pkgver}/setup.py
}

check() {
  cd ${pkgbase}-${pkgver}
  LC_CTYPE=en_US.UTF-8 py.test
}

build() {
  cd ${pkgbase}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
