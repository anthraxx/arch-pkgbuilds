# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-login
pkgver=0.5.0
pkgrel=5
pkgdesc="User session management for Flask."
arch=('any')
url="https://pythonhosted.org/Flask-Login/"
license=('MIT')
depends=('python-flask')
makedepends=('python-setuptools')
checkdepends=('python-nose' 'python-mock' 'python-blinker' 'python-semantic-version')
source=("$pkgname-$pkgver.tar.gz::https://github.com/maxcountryman/flask-login/archive/$pkgver.tar.gz"
        $pkgname-python310.patch::https://github.com/maxcountryman/flask-login/commit/3d316b615f91.patch)
sha512sums=('8ffecd2a512738f81ec3901ad30d356b1d63355268f7cf1b427e463d0a271a4623723255e139bbd0f88845d73fa4aab051418b5f68aef0b4576c30294f6207b3'
            '0739db32782fac6906d1c5fa871dabc4728a200489566a9e9c2f302a9af877d0a6e79e11f36e865c468aa4dbadf799e2022ea70d9e579164970730e795aca8dc')

prepare() {
  cd "$srcdir"/flask-login-$pkgver
  patch -Np1 -i ../$pkgname-python310.patch
}

check() {
  cd "$srcdir"/flask-login-$pkgver
  nosetests3
}

package() {
  cd flask-login-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1

  mkdir -p "$pkgdir"/usr/share/licenses/$pkgname
  install -m644 LICENSE "$pkgdir"/usr/share/licenses/$pkgname
}
