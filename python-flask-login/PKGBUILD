# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-flask-login
pkgver=0.6.1
pkgrel=1
pkgdesc="User session management for Flask"
arch=('any')
url="https://pythonhosted.org/Flask-Login/"
license=('MIT')
depends=('python' 'python-flask' 'python-werkzeug')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-nose' 'python-mock' 'python-blinker' 'python-semantic-version' 'python-asgiref')
source=("https://github.com/maxcountryman/flask-login/archive/$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('f220e24079923c669d1ce27fdbc7ff8e07e92bc947e2708034cb349176e33ae0adf36ef6c30e8328f592d9a3eb6d0796dc577033f07c33ef0bc48c13a11a942e')

build() {
  cd flask-login-$pkgver
  python -m build --wheel --no-isolation
}

check() {
  cd flask-login-$pkgver
  nosetests3
}

package() {
  cd flask-login-$pkgver
  python -m installer --destdir="$pkgdir" dist/*.whl

  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/$pkgname
  install -Dm 644 README.md -t "$pkgdir"/usr/share/docs/$pkgname
}
