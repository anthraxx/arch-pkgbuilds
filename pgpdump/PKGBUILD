# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=pgpdump
pkgver=0.34
pkgrel=1
pkgdesc='PGP packet visualizer which displays the packet format'
url='https://www.mew.org/~kazu/proj/pgpdump'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'zlib' 'bzip2')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/kazu-yamamoto/${pkgname}/archive/v${pkgver}.tar.gz)
sha256sums=('d5bb5c5a1cbbc5a536baa64c12820a184211e8855b8d3e16ae832d1eaa314926')
sha512sums=('9efa63f7609fda764b9b21923135a0c90f25a3e22e924810250ea8f3440fb9c894c133bc8e77c746ac797be8816c50bb9972cabee9898e910736a57fc1310e7e')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
