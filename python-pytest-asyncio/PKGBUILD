# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-asyncio
pkgver=0.16.0
pkgrel=4
pkgdesc="Pytest support for asyncio."
arch=('any')
license=('Apache')
url="https://github.com/pytest-dev/pytest-asyncio/"
depends=('python-pytest')
makedepends=('python-setuptools')
checkdepends=('python-coverage' 'python-async_generator' 'python-hypothesis')
source=("$pkgname-$pkgver.tar.gz::https://github.com/pytest-dev/pytest-asyncio/archive/v$pkgver.tar.gz")
sha512sums=('f0059a6d05bf0c4e208f0058e30d5baf8284e630b8b91fa217f52eec18abe6b057dabfdadce2c5282e02bd4ea4cdb27f9ef62c4c308375cce1795d75a036db22')

prepare() {
  cd pytest-asyncio-$pkgver
  # Don't treat DeprecationWarnings as errors
  sed -i '/filterwarnings = error/d' setup.cfg
}

build() {
  cd pytest-asyncio-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-asyncio-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH:$PWD/tests" py.test
}

package() {
  cd pytest-asyncio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
