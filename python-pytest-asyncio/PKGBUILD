# Maintainer: Felix Yan <felixonmars@archlinux.org>

pkgname=python-pytest-asyncio
pkgver=0.18.2
pkgrel=1
pkgdesc="Pytest support for asyncio."
arch=('any')
license=('Apache')
url="https://github.com/pytest-dev/pytest-asyncio/"
depends=('python-pytest')
makedepends=('python-setuptools-scm')
checkdepends=('python-flaky' 'python-hypothesis')
source=("https://github.com/pytest-dev/pytest-asyncio/archive/v$pkgver/$pkgname-$pkgver.tar.gz")
sha512sums=('377d219e731ac97a69e143c2de2e327f7293baf4af4bf370cb84dde4f94f705e315bf6deaea4b7d8c1d6cbc54ed753e15ccc0b21a0cef6c3397a560aae684f9e')

export SETUPTOOLS_SCM_PRETEND_VERSION=$pkgver

build() {
  cd pytest-asyncio-$pkgver
  python setup.py build
}

check() {
  # Hack entry points by installing it

  cd pytest-asyncio-$pkgver
  python setup.py install --root="$PWD/tmp_install" --optimize=1
  PYTHONPATH="$PWD/tmp_install/usr/lib/python3.10/site-packages:$PYTHONPATH:$PWD/tests" py.test
}

package() {
  cd pytest-asyncio-$pkgver
  python setup.py install --root="$pkgdir" --optimize=1
}
