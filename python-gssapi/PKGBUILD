# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Samuel Laur√©n <samuel.lauren@iki.fi>

pkgname=python-gssapi
pkgver=1.8.0
pkgrel=1
pkgdesc='Provides both low-level and high level wrappers around the GSSAPI C libraries'
url='https://github.com/pythongssapi/python-gssapi'
arch=('x86_64')
license=('ISC')
depends=('python' 'python-decorator' 'krb5')
makedepends=('python-setuptools' 'cython')
checkdepends=('python-parameterized' 'python-k5test')
source=("https://github.com/pythongssapi/python-gssapi/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('8a9d6d524678511fe37b97184484531a9d68dc8b363466c3a25097d294907136441a91e5ad8e58753485d84cff69d568d32ab2294b2f848813b2d0a356219b7e')
b2sums=('34ac318d93227d91eb0e1037aaca0591dd730594983597bfe660520016bac69ef377a6882a4fe3b5f1405bd870b36d451b40ea1185f716cf68b7c80ce3a8d698')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  python setup.py test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
