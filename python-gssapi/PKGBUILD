# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Samuel Laur√©n <samuel.lauren@iki.fi>

pkgname=python-gssapi
pkgver=1.6.14
pkgrel=3
pkgdesc='Provides both low-level and high level wrappers around the GSSAPI C libraries'
url='https://github.com/pythongssapi/python-gssapi'
arch=('x86_64')
license=('ISC')
depends=('python' 'python-decorator' 'krb5')
makedepends=('python-setuptools' 'cython')
checkdepends=('python-parameterized' 'python-k5test')
source=("https://github.com/pythongssapi/python-gssapi/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('841e0ea803aa29c9034d7dc064a3d440b45521904284529ba201d12d462b9f879df84ba2f8ff29246b382a1495b3b710816c2b0864a3a7eec32338a97b44a51c')
b2sums=('c72819bfd93c4a1a027abbb3902a549b5bea8a8d17f9d1bbb3e593d17539b0fe4a3bec8f52aee6efe0b19647ecf9a2ffc8d99b6e79ab0f3bc4e066b8e264847a')

build() {
  cd "${pkgname}-${pkgver}"
  python setup.py build
}

check() {
  cd "${pkgname}-${pkgver}"
  python setup.py test
}

package() {
  cd "${pkgname}-${pkgver}"
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
