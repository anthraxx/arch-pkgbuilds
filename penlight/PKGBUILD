# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer
# Contributor: SpepS <dreamspepser at yahoo dot it>
# Contributor: Laszlo Papp <djszapi at archlinux us>
# Contributor: Donald Ephraim Curtis <dcurtis@gmail.com>

pkgbase=penlight
pkgname=('lua-penlight' 'lua53-penlight')
pkgver=1.6.0
pkgrel=2
pkgdesc='Lua libraries focusing on input data handling'
url='https://github.com/stevedonovan/penlight'
arch=('any')
license=('custom')
makedepends=('lua' 'lua-filesystem' 'lua53' 'lua53-filesystem')
source=(https://github.com/stevedonovan/penlight/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        penlight-lua-5.4.patch)
sha256sums=('a552d0a314f7aa94c9579746996a7aad4ed59f3187f33b4735d3e323e27354b0'
            'bdb1fb023240b717b3fd800ebd9cf9ee51e94a073769e5c4b6e19c9dc4713b6c')
sha512sums=('4f9988e9e5d66aad1c7137a5b8a36e4eaa2dba5408c90ee0253ce3db69980b2e0be8c9ba38f1e88f269da96d09a908115e0e9c2bd0667393f950fdfc1e232ff0'
            'eec7d1c61a4ca2cbea7760f18e15d4634ca9eca451bc4a101ace7efd672ffe061141f0d028c459b72a4834cf8dbe0ca29942b5c871885636dfec18dc10515c67')

prepare() {
  cd Penlight-${pkgver}
  patch -p1 -i ../penlight-lua-5.4.patch
}

check() {
  cd Penlight-${pkgver}
  export LUA_PATH="${PWD}/lua/?/init.lua;${PWD}/lua/?.lua;$(lua -e 'print(package.path)')"
  lua run.lua
  export LUA_PATH="${PWD}/lua/?/init.lua;${PWD}/lua/?.lua;$(lua5.3 -e 'print(package.path)')"
  lua5.3 run.lua
}

package_lua-penlight() {
  depends=('lua' 'lua-filesystem')
  cd Penlight-${pkgver}
  install -Dm 644 lua/pl/* -t "${pkgdir}/usr/share/lua/5.4/pl"
  install -Dm 644 CONTRIBUTING.md CHANGELOG.md README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/manual/* -t "${pkgdir}/usr/share/doc/${pkgname}/manual"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_lua53-penlight() {
  depends=('lua53' 'lua53-filesystem')
  cd Penlight-${pkgver}
  install -Dm 644 lua/pl/* -t "${pkgdir}/usr/share/lua/5.3/pl"
  install -Dm 644 CONTRIBUTING.md CHANGELOG.md README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/manual/* -t "${pkgdir}/usr/share/doc/${pkgname}/manual"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
