# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jonathan Steel <jsteel at aur.archlinux.org>
# Contributor: Alan Young <harleypig@gmail.com>

pkgname=perl-module-find
_cpanname=Module-Find
pkgver=0.15
pkgrel=1
pkgdesc="Find and use installed perl modules in a (sub)category"
url='https://search.cpan.org/dist/Module-Find'
arch=('any')
license=('PerlArtistic' 'GPL')
depends=('perl')
checkdepends=('perl-test-pod' 'perl-test-pod-coverage')
options=('!emptydirs')
source=(${pkgname}-${pkgver}.tar.gz::https://search.cpan.org/CPAN/authors/id/C/CR/CRENZ/${_cpanname}-${pkgver}.tar.gz)
sha512sums=('769f1ee8836a127cb3c08edc858240c88e6481243d15b79b348c66d960213a98963bd9c453756645982c730a3c895a7dc35b06a970a0d6300690a605f4487dac')

build() {
  cd ${_cpanname}-${pkgver}
  PERL_MM_USE_DEFAULT=1 perl Makefile.PL INSTALLDIRS=vendor
  make
}

check() {
  cd ${_cpanname}-${pkgver}
  make test
}

package() {
  cd ${_cpanname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README "${pkgdir}/usr/share/doc/${pkgname}/README"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: ts=2 sw=2 et:
