# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-socketio
pkgver=5.6.0
pkgrel=1
pkgdesc='Python implementation of the Socket.IO realtime server'
url='https://github.com/miguelgrinberg/python-socketio'
arch=('any')
license=('MIT')
depends=('python' 'python-six' 'python-engineio' 'python-bidict' 'python-msgpack')
makedepends=('python-setuptools' 'python-sphinx' 'python-tornado')
optdepends=('python-tornado: tornado asyncio webserver')
checkdepends=('python-pytest' 'python-mock')
options=('!makeflags')
source=(https://github.com/miguelgrinberg/python-socketio/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('65512b5d81013b1af636dbf61ed27154796ce86d4a470ecb9a749e722aaf442030a88172cd9ce6600e5c1ebd7d838be8aa26fa5887f4252174020fc926d3a447')
b2sums=('1642053479c527d3019be67113eb57a6237aec9b6e3e3b58d5d0a5d0c5f2a20090fa2cc94b98b6036a3c96b744ab05ab3abf133e88ab629ceb7ecf2f7fc53794')

build() {
  cd ${pkgbase}-${pkgver}
  python setup.py build
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${pkgbase}-${pkgver}
  PYTHONPATH=build/lib python setup.py test
}

package() {
  cd ${pkgbase}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/python-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
