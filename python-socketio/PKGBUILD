# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-socketio
pkgver=5.7.1
pkgrel=1
pkgdesc='Python implementation of the Socket.IO realtime server'
url='https://github.com/miguelgrinberg/python-socketio'
arch=('any')
license=('MIT')
depends=('python' 'python-six' 'python-engineio' 'python-bidict' 'python-msgpack')
makedepends=('python-setuptools' 'python-build' 'python-installer' 'python-wheel'
             'python-sphinx' 'python-tornado')
optdepends=('python-tornado: tornado asyncio webserver')
checkdepends=('python-pytest' 'python-mock')
options=('!makeflags')
source=(https://github.com/miguelgrinberg/python-socketio/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('0d2962aa10f1b2cef5a18f97aba32c015960d5aa70ada9ca4d63db18b24a68d08033036ed5f2b93f0d618bb946e7a792fdfcabca655b21b6aa479042bb07f43a')
b2sums=('aea56b52ef7f9bea12497e419818936d2fcc1b6feaaaa3eb289555d3e5219fffa7011ecd05603fbdd7c104b2795aa0b0f4941069e97f80c91f6978327aa823bb')

build() {
  cd ${pkgbase}-${pkgver}
  python -m build --wheel --no-isolation
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${pkgbase}-${pkgver}
  PYTHONPATH=build/lib python setup.py test
}

package() {
  cd ${pkgbase}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt "${pkgdir}/usr/share/doc/${pkgname}/DOCUMENTATION"
  install -Dm 644 docs/_build/man/python-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
