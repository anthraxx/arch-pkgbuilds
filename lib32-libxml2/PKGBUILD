# Maintainer: Jan Alexander Steffens (heftig) <heftig@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=lib32-libxml2
pkgver=2.9.14
pkgrel=1
pkgdesc='XML parsing library, version 2 (32-bit)'
url='http://www.xmlsoft.org/'
arch=(x86_64)
license=(MIT)
depends=(lib32-zlib lib32-readline lib32-ncurses lib32-xz lib32-icu libxml2)
makedepends=(git)
provides=(libxml2.so)
options=(debug)
_commit=7846b0a677f8d3ce72486125fa281e92ac9970e8  # tags/v2.9.14^0
source=("git+https://gitlab.gnome.org/GNOME/libxml2.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd libxml2
  git describe --tags | sed 's/-rc/rc/;s/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  mkdir build

  cd libxml2

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG="i686-pc-linux-gnu-pkg-config"

  cd build

  ../libxml2/configure \
    --prefix=/usr \
    --libdir=/usr/lib32 \
    --with-threads \
    --with-history \
    --without-python \
    --with-icu
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  make
}

check() {
  make -C build check
}

package() {
  make -C build DESTDIR="$pkgdir" install

  rm -r "$pkgdir"/usr/{include,share,bin}
  rm "$pkgdir/usr/lib32/xml2Conf.sh"

  install -Dm 644 build/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
}

# vim:set sw=2 et:
