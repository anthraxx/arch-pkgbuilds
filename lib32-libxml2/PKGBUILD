# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jan de Groot <jgc@archlinux.org>
# Contributor: Tom Gundersen <teg@jklm.no>
# Contributor: John Proctor <jproctor@prium.net>

_pkgbasename=libxml2
pkgname=lib32-$_pkgbasename
pkgver=2.9.12
pkgrel=2
pkgdesc='XML parsing library, version 2 (32-bit)'
url='http://www.xmlsoft.org/'
arch=(x86_64)
license=(MIT)
depends=(lib32-zlib lib32-readline lib32-ncurses lib32-xz lib32-icu libxml2)
makedepends=(git)
_commit=b48e77cf4f6fa0792c5f4b639707a2b0675e461b  # tags/v2.9.12^0
source=("git+https://gitlab.gnome.org/GNOME/libxml2.git#commit=$_commit")
sha256sums=('SKIP')

pkgver() {
  cd ${_pkgbasename}
  git describe --always --tags | sed 's/-rc/rc/;s/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd ${_pkgbasename}

  # Work around lxml API abuse
  git cherry-pick -n 85b1792e37b131e7a51af98a37f92472e8de5f3f
  # Fix regression in xmlNodeDumpOutputInternal
  git cherry-pick -n 13ad8736d294536da4cbcd70a96b0a2fbf47070c
  # Fix XPath recursion limit
  git cherry-pick -n 3e1aad4fe584747fd7d17cc7b2863a78e2d21a77
  # Fix whitespace when serializing empty HTML documents
  git cherry-pick -n 92d9ab4c28842a09ca2b76d3ff2f933e01b6cd6f

  NOCONFIGURE=1 ./autogen.sh
}

build() {
  export CC="gcc -m32"
  export CXX="g++ -m32"
  export PKG_CONFIG_PATH="/usr/lib32/pkgconfig"

  cd ${_pkgbasename}

  ./configure \
    --prefix=/usr \
    --with-threads \
    --with-history \
    --with-icu \
    --libdir=/usr/lib32
  sed -i -e 's/ -shared / -Wl,-O1,--as-needed\0 /g' libtool
  make
}

package() {
  cd ${_pkgbasename}
  make DESTDIR="${pkgdir}" install

  rm -rf "${pkgdir}"/usr/{include,share,bin} "$pkgdir/usr/lib32/xml2Conf.sh"
  mkdir -p "$pkgdir/usr/share/licenses"
  ln -s $_pkgbasename "$pkgdir/usr/share/licenses/$pkgname"
}

# vim: ts=2 sw=2 et:
