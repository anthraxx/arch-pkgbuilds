# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Vesa Kaihlavirta <vesa@archlinux.org>
# Contributor: Roman Cheplyaka <roma@ro-che.info>

pkgname=libedit
_pkgver=20210714-3.1
pkgver=${_pkgver/-/_}
pkgrel=1
pkgdesc='Command line editor library providing generic line editing, history, and tokenization functions'
url='https://thrysoee.dk/editline/'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'ncurses' 'libncursesw.so')
provides=('libedit.so')
source=(${url}/${pkgname}-${_pkgver}.tar.gz)
sha256sums=('3023b498ad593fd7745ae3b20abad546de506b67b8fbb5579637ca69ab82dbc9')
b2sums=('e2559e85151ce21e97caf008b96d07a6432d3b1b243cd9b08636bb3a44db68b2aa406f33c165d279bd07745d1e69531f6daa4ac362f3299c035b0dbe4724be8a')

build() {
  cd ${pkgname}-${_pkgver}
  ./configure --prefix=/usr
  make V=1
}

package() {
  cd ${pkgname}-${_pkgver}
  make DESTDIR="${pkgdir}" install

  rm "${pkgdir}"/usr/share/man/man3/history.3 # conflicts with readline
  install -Dm 644 "${pkgdir}"/usr/share/man/man3/editline.3 "${pkgdir}"/usr/share/man/man3/el.3

  install -Dm 644 ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
