# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Stas Rudakou <stas at garage22 dot net>

pkgname=cobalt
pkgver=0.16.2
pkgrel=1
pkgdesc='Static site generator written in Rust'
url='https://cobalt-org.github.io/'
arch=('x86_64')
license=('MIT')
depends=('openssl' 'libsass')
makedepends=('rust' 'cargo')
source=(https://github.com/cobalt-org/cobalt.rs/archive/v${pkgver}/${pkgname}.rs-v${pkgver}.tar.gz)
sha512sums=('9cf68907e68ccbe7d580864426e642b1a0eb15f7794f05a74cb3addb64482d494a010f71d137940dd1cc66a31b0b58298e2e485c03978f36ed7f65f87372ce0c')
b2sums=('d4c292bb3538ec3ffdbabbe5263e69d177d735555b032fb314b3c86eae04651b8ddfa284451f2782072798d125b6be1f8f76e7f0a46229a147cc6e0a81ac6c82')

build() {
  cd ${pkgname}.rs-${pkgver}
  cargo build --release --locked --features 'syntax-highlight sass'
}

check() {
  cd ${pkgname}.rs-${pkgver}
  cargo test --release --locked
}

package() {
  cd ${pkgname}.rs-${pkgver}
  install -Dm 755 target/release/cobalt -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
