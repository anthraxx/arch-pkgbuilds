# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=ropper
pkgname=('ropper' 'python-ropper')
_pkgname=Ropper
pkgver=1.13.6
pkgrel=2
pkgdesc='Show information about binary files and find gadgets to build rop chains for different architectures'
url='https://github.com/sashs/Ropper'
arch=('any')
license=('GPL2')
makedepends=('python-setuptools' 'python-capstone' 'python-filebytes')
source=(https://github.com/sashs/${_pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('c3e20bc7182a94984f50c42334a7af1e64bd564615a3aaaa08f432b0641a9557d27fd19bdb6c33a276d66c4b512534c625751e20531c324bde96a58d9c89b6d4')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python test.py
}

package_ropper() {
  depends=('python' "python-ropper>=${pkgver}")
  optdepends=('python-keystone: assemble command support')
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/lib"
}

package_python-ropper() {
  depends=('python-capstone' 'python-filebytes')
  optdepends=('python-keystone: assemble command support')
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 README.md sample.py -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
