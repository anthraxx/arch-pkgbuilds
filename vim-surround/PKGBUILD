# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Aaron Schaefer <aaron@elasticdog.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=vim-surround
pkgver=2.2
pkgrel=1
pkgdesc='Provides mappings to easily delete, change, and add paired surroundings'
url='https://github.com/tpope/vim-surround'
arch=('any')
license=('custom:vim')
depends=('vim')
groups=('vim-plugins')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/tpope/vim-surround/archive/v${pkgver}.tar.gz
        license.txt)
sha512sums=('ef0bac868ca30cc5606f12898f131debaff3b42ad178ab83b363e46210edb8e084658505f28ba46e24012dfc1843e22603bb2c4f40b5010db13aacf07b0bd970'
            'a50e91b1896b0d952008ba2f641a87af2d1a01e4f280f6c914edcd51ae5d1586d5ade71c3609866b501569007bcb7f2494f08280afec170884b90fab36332fac')

package() {
  cd ${pkgname}-${pkgver}
  local installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm 644 doc/* -t "${installpath}/doc"
  install -Dm 644 plugin/* -t "${installpath}/plugin"
  install -Dm 644 ../license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.markdown -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
