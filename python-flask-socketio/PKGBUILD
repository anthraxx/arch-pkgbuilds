# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-flask-socketio
_pkgname=Flask-SocketIO
pkgver=5.1.1
pkgrel=1
pkgdesc='Socket.IO integration for Flask applications'
url='https://flask-socketio.readthedocs.org/'
arch=('any')
license=('MIT')
depends=('python' 'python-flask' 'python-eventlet' 'python-socketio')
makedepends=('python-setuptools' 'python-sphinx' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-coverage' 'python-pytest' 'python-tox-current-env' 'flake8')
options=('!makeflags')
source=(https://github.com/miguelgrinberg/Flask-SocketIO/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('12510b0d853a03d613782a7ed23cf791a3c6c5f8c55e78308c3e635be22384ba7bb207811846d14252fcf4ff21ab42c27f6390382baabf393f1eba24b6a80861')
b2sums=('4a6a8fffcf27b350616ee4017569b2d55cb352d4b3d71739aee4e872e016e6adc349bf3e79f3b3fcc20e63ab1bec4ebf08776fa32a8972a0d2769672cc448e5f')

build() {
  cd ${_pkgname}-${pkgver}
  python -m build --wheel --no-isolation
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${_pkgname}-${pkgver}
  PYTHONPATH="build/lib" pytest test_socketio.py
}

package() {
  cd ${_pkgname}-${pkgver}
  python -m installer --destdir="${pkgdir}" dist/*.whl

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
