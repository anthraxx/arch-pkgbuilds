# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-flask-socketio
_pkgname=Flask-SocketIO
pkgver=4.3.1
pkgrel=1
pkgdesc='Socket.IO integration for Flask applications'
url='https://flask-socketio.readthedocs.org/'
arch=('any')
license=('MIT')
depends=('python-flask' 'python-eventlet' 'python-socketio')
makedepends=('python-setuptools' 'python-sphinx')
checkdepends=('python-coverage' 'python-pytest')
options=('!makeflags')
source=(https://github.com/miguelgrinberg/Flask-SocketIO/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('06caaae18933f78ebecb272065267af5260ff7657aec9241a5a956fad97f0e36bb47959529bf9041f8d72c954d4f90763f09c3bb1b170042d930501ec02f6550')
b2sums=('974a47730a70a6efa413c34ab02123d5d6adb1a66bc5ece256670ff033edaca076eeb5a4227e3ebd7afa9010c3170433c0182475822df6ce13149f8092c48c3e')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${_pkgname}-${pkgver}
  py.test test_socketio.py
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/text/index.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-socketio.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
