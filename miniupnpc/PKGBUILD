# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Florian Loitsch <archlinux@florian.loitsch.com>

pkgbase=miniupnpc
pkgname=(miniupnpc python-miniupnpc)
pkgver=2.2.3
pkgrel=1
pkgdesc='Small UPnP client library/tool to access Internet Gateway Devices'
url='https://miniupnp.tuxfamily.org/'
arch=('x86_64')
license=('BSD')
makedepends=('sh' 'glibc' 'lsb-release' 'python' 'python-setuptools')
source=(https://miniupnp.tuxfamily.org/files/${pkgname}-${pkgver}.tar.gz{,.sig}
        https://github.com/miniupnp/miniupnp/commit/ed1dc4bb.patch)
sha512sums=('8b9555d336d5dfe8017a16727f964cddacb3f2bf3528fe3dd2416480da5763b24e1740345e470f2123c170d79d88444e41dbbce4536e6069590925e4805bc5ee'
            'SKIP'
            'a1ade8a50d65dcf9da0127893c915d931154c3398063a3d49973647f0c39fb7326c2bf7c49dbc19db254b85093d1ab2b2bc9e5ad53d6999fc0d74b3192d015a2')
b2sums=('d2a7f64d56be524c5c4aadfecd40da4e1ad040fd7eab64be5b5716cb06047e86e3ed6a9c17f697fd3210af0fb0a6ae032b6ef0a5c1f0d96a2e185db97b0d06e8'
        'SKIP'
        '5c740007baf1deaf80b93ff882f9b6c972a8a74c05c388131c85b12914379ffe21b22ce7450b312bf662c020a090a96e504e85b3aca1fcd21bdc9c208da4290e')
validpgpkeys=(
  '751E9FF6944A3B36A5432216DB511043A31ACAAF' # miniupnp (miniupnp !) <miniupnp@free.fr>
)

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p2 -i ../ed1dc4bb.patch # Fix make install
}

build() {
  cd ${pkgname}-${pkgver}
  make
  python setup.py build
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package_miniupnpc() {
  depends=(sh glibc)
  provides=(libminiupnpc.so)

  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 man3/miniupnpc.3 -t "${pkgdir}/usr/share/man/man3"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

package_python-miniupnpc() {
  pkgdesc='Python bindings for miniupnpc'
  depends=(python glibc)

  cd ${pkgbase}-${pkgver}
  python setup.py install --optimize=1 --skip-build --root="$pkgdir"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
