# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=glad
pkgver=2.0.2
pkgrel=1
pkgdesc='Multi-Language Vulkan/GL/GLES/EGL/GLX/WGL Loader-Generator based on the official specs'
url='https://github.com/Dav1dde/glad'
arch=('any')
license=('MIT')
depends=('python' 'python-jinja' 'python-lxml' 'python-setuptools')
makedepends=('python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=(${url}/archive/refs/tags/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('c719bcd7a0aaa56d89158c9f347410b9e66466fb957a5a5f201b4cce21a0c1589d6d2a866e12134ed43435a5567d842fbfd134559dea1ca59a34b079ff6f5d37')
b2sums=('ce7f63ac1f9a6263d3de3f9c56f4a47b8bbdd1ac7eba875c7ad807ae923c8a82d7f323725af8b64adbbc30c2e014c6a5ed8b3325b7f15984222075874a68c0cf')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's/import mock/from unittest import mock/' -i tests/test____main__.py
}

build() {
  cd ${pkgname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${pkgname}-${pkgver}/tests
  PYTHONPATH="${PWD}/../build/lib" pytest
}

package() {
  cd ${pkgname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 README.md -t "$pkgdir"/usr/share/doc/"$pkgname"
  install -Dm 644 LICENSE -t "$pkgdir"/usr/share/licenses/"$pkgname"
}

# vim: ts=2 sw=2 et:
