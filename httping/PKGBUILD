# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: brent s. <bts[at]square-r00t[dot]net>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: Ondrej Kucera <ondrej.kucera@centrum.cz>

pkgname=httping
pkgver=2.5
pkgrel=5
pkgdesc='A ping-like tool for http-requests'
url='https://www.vanheusden.com/httping/'
arch=('x86_64')
license=('GPL2')
depends=('openssl' 'ncurses' 'fftw')
source=(https://sources.archlinux.org/other/community/httping/${pkgname}-${pkgver}.tgz
        theoretical-security-problem-fix.patch)
sha512sums=('397ce2eac91c9aede0eb19823bfca4e7f98c263937f5954c8a48d5248a251d2030e31f18decb7f7f8b0118ec0e16ec0a84ea4c943ccde7056b80de162954c355'
            '136c31c9c84ddc237a70b6a78e24160b81b258d7fdaf2f6ade7070723c1eccac5c5e84b24534ef2998abc91ec31ed41ae989eb55ce402cc8cc90259588695424')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 -i ../theoretical-security-problem-fix.patch
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --with-tfo
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
