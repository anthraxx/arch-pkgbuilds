# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Nicolas Hureau <archlinux@kalenz.net>

pkgname=jadx
pkgver=1.3.5
pkgrel=1
pkgdesc='Command line and GUI tools to produce Java source code from Android Dex and APK files'
url='https://github.com/skylot/jadx'
arch=('any')
license=('Apache')
depends=('java-runtime=11' 'bash')
makedepends=('java-environment=11' 'gradle')
source=(https://github.com/skylot/${pkgname}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('6b5e11c60122eefa1cf95dd45ba68622725acea7288c517ec32ed6c5fab40af238f5b7966264b9209402b5f9ee5f011873a1eea856764115418ff5d9b1025cc3')
b2sums=('af1629083c2345c2531f7f6a2f35f510e7f8cdb8c7422881feb0bde2d7acf405a0e3562cd48cb049dadbf6037b9da469168250514ca3c5295d78968af6107cc6')

build() {
  cd ${pkgname}-${pkgver}
  gradle dist
}

check() {
  cd ${pkgname}-${pkgver}
  gradle test
}

package() {
  cd ${pkgname}-${pkgver}/build/jadx

  install -Dm 755 bin/{jadx,jadx-gui} -t "${pkgdir}/usr/share/java/${pkgname}/bin"
  install -Dm 644 lib/* -t "${pkgdir}/usr/share/java/${pkgname}/lib"

  install -d "${pkgdir}/usr/bin"
  ln -s /usr/share/java/${pkgname}/bin/jadx "${pkgdir}/usr/bin/jadx"
  ln -s /usr/share/java/${pkgname}/bin/jadx-gui "${pkgdir}/usr/bin/jadx-gui"

  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 NOTICE README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
