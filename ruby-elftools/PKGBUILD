# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bet4 <0xbet4@gmail.com>

_gemname=elftools
pkgname=ruby-${_gemname}
pkgver=1.1.3
pkgrel=2
pkgdesc='ELF parser library implemented in pure Ruby'
url='https://github.com/david942j/rbelftools/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-bindata')
makedepends=('ruby-rake' 'ruby-rdoc' 'ruby-bundler')
options=('!emptydirs')
source=(${url}/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz)
sha512sums=('080de523d651c9f29ce0ba4e5d67c4117749306fd02cf2f6363c611e520ad806391e005e4d56d238ac0f2160847f4eb7b5b3cd212ae5728865e1aaaeb88a2507')
b2sums=('ae6620f1f4f333b4a3d7f01ad2a580effd3a108873e3e54a0df82a12fc605821fb0c7fa18cac4640d426fa586f6c8794119a4ee797264caedadbfdc45a59812c')

prepare() {
  cd rbelftools-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
}

build() {
  cd rbelftools-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd rbelftools-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install \
    --local \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "${pkgdir}${_gemdir}" \
    --bindir "${pkgdir}/usr/bin" \
    ${_gemname}-${pkgver}.gem

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -rf cache gems/${_gemname}-${pkgver}/{ext,lib/*/*.so,man} \
    extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
