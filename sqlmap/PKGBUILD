# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Evan Teitelman <teitelmanevan at gmail dot com>
# Contributor: fxbru <frxbru[at]gmail>

pkgname=sqlmap
pkgver=1.4.8
pkgrel=1
pkgdesc='Automatic SQL injection and database takeover tool'
url='http://sqlmap.org'
arch=('any')
license=('GPL2')
depends=('python' 'sh')
backup=('etc/sqlmap.conf')
source=(https://github.com/sqlmapproject/sqlmap/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('43e52c1331857d63df469892b24b1e47158951bf0a7a0b9c9b0917571abeac6e56ea09e9c2fd52f98397ff5b68481e3ffa8a8aa53f0d82187065639f2cccacd2')
b2sums=('e60c4656a7a18af979bd4b10cb534448265ac096a953faa26441a55a2181b48a2c8c9564b36fdb86cc60abadbbf75169d69a918d1b1b668d43cb45dd49ba7977')

build() {
  cd ${pkgname}-${pkgver}
  python -m compileall .
  python -O -m compileall .
}

package() {
  cd ${pkgname}-${pkgver}

  install -d "${pkgdir}/etc"
  install -d "${pkgdir}/usr/bin"
  install -d "${pkgdir}/opt/${pkgname}"
  install -d "${pkgdir}/usr/share/doc"

  mv doc "${pkgdir}/usr/share/doc/${pkgname}"
  mv sqlmap.conf "${pkgdir}/etc/sqlmap.conf"
  ln -sf /etc/sqlmap.conf "${pkgdir}/opt/${pkgname}/sqlmap.conf"
  cp -a --no-preserve=ownership * "${pkgdir}/opt/${pkgname}"

  cat > "${pkgdir}/usr/bin/sqlmap" << EOF
#!/bin/sh
cd /opt/${pkgname}
python sqlmap.py "\$@"
EOF
  cat > "${pkgdir}/usr/bin/sqlmapapi" << EOF
#!/bin/sh
cd /opt/${pkgname}
python sqlmapapi.py "\$@"
EOF

  chmod 755 "${pkgdir}/usr/bin/sqlmap"
  chmod 755 "${pkgdir}/usr/bin/sqlmapapi"
}

# vim: ts=2 sw=2 et:
