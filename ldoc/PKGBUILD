# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bartłomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: Jaroslav Lichtblau <svetlemodry@archlinux.org>
# Contributor: Sébastien Luttringer

pkgname=ldoc
pkgver=1.4.6
pkgrel=3
pkgdesc='LuaDoc-compatible documentation generation system'
url='https://stevedonovan.github.com/ldoc/'
arch=('any')
license=('custom')
depends=('lua' 'lua-penlight')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/stevedonovan/ldoc/archive/${pkgver}.tar.gz)
sha512sums=('bcaddba4e462a4fb0aa0d8fcff390de3f195fa5cc569d8f05b84ad0fff900f5ac945be7a2eb15cf5ca60cc462f02c874579e4a19e33776178a04104ef9d944af')

package() {
  cd LDoc-${pkgver}

  # install library
  install -dm 755 "${pkgdir}/usr/share/lua/5.4"
  cp -a ldoc "${pkgdir}/usr/share/lua/5.4"

  # install binaries
  install -Dm 755 ldoc.lua "${pkgdir}/usr/bin/ldoc.lua"
  ln -s ldoc.lua "${pkgdir}/usr/bin/ldoc"

  # copy docs
  install -Dm 644 readme.md doc/doc.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYRIGHT "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}

# vim: ts=2 sw=2 et:
