# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=ailment
pkgname=python-${_pyname}
pkgver=9.2.14
pkgrel=1
pkgdesc='The angr intermediate language'
url='https://github.com/angr/ailment'
license=('custom:BSD2')
arch=('any')
depends=('python' 'python-claripy' 'python-pyvex' 'python-pypcode')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest' 'python-archinfo')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('b9483a5407aaa3f24f89b421621e761667697d90d25b5a7a9a7f542adaa2c0f9b355271061741c3eee334bdf4b3d71e3e91c1a34119239ff182b2a63d687bb73')
b2sums=('f6f0f6bcd821f63cb3d7a94c3b67de0d906172cca6e76e994418f7c5475b70fddd95644ade941bdfa62bec4b5a98570bd52905370aaf3643e22d5f780dfd60c8')

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  # TODO: needed for angr (hard dep)
  # PYTHONPATH=build/lib pytest
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
