# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Ã‰tienne Deparis <etienne@depar.is>

pkgname=libnitrokey
pkgver=3.7
pkgrel=1
pkgdesc='Communicate with Nitrokey stick devices in a clean and easy manner'
url='https://www.nitrokey.com'
arch=('x86_64')
license=('LGPL3')
depends=('glibc' 'gcc-libs' 'hidapi' 'libusb')
makedepends=('cmake' 'qt5-tools' 'catch2')
provides=('libnitrokey.so')
options=(debug)
source=(https://github.com/Nitrokey/libnitrokey/releases/download/v${pkgver}/libnitrokey-v${pkgver}.tar.gz{,.sig})
sha512sums=('1f9c689a64b99aef8b683c0bca0e0c8b07b51008c18a884571f0c38d1b73deac0d1067f6b93fc7181226655005828299662e75ec0885eaeeca1a5023de2348eb'
            'SKIP')
b2sums=('42afdd903491d24fad80bbaf60efff8b22177aadf1b1b3d9c1c65ba981fe3313c9f5a3140bb4fef59629681537860c899a6bbfca330304afaaf16961f93ba619'
        'SKIP')
validpgpkeys=('868184069239FF65DE0BCD7DD9BAE35991DE5B22') # Szczepan Zalega <szczepan.zalega@gmail.com>

build() {
  cmake -B build \
    -DBUILD_SHARED_LIBS=ON \
    -DCOMPILE_OFFLINE_TESTS=ON \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -S $pkgname-v$pkgver
  make -C build VERBOSE=1
}

check() {
  ctest -VV --test-dir build
}

package() {
  make -C build DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
