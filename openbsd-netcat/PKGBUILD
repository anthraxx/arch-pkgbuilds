# Maintainer: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Andrej Gelenberg <andrej.gelenberg@udo.edu>

pkgname="openbsd-netcat"
pkgver=1.218_2
pkgrel=1
pkgdesc="TCP/IP swiss army knife. OpenBSD variant."
arch=('x86_64')
url="https://salsa.debian.org/debian/netcat-openbsd"
license=('BSD')
depends=('libbsd')
makedepends=('gcc' 'make')
provides=('netcat')
conflicts=('gnu-netcat')
source=("https://salsa.debian.org/debian/netcat-openbsd/-/archive/debian/${pkgver/_/-}/netcat-openbsd-debian-${pkgver/_/-}.tar.gz")
sha512sums=('d10be99f7ebb03c0b0e3ad5d9ba04c6c1aac3d10556b324201cbf4f07736f5101db6e622720b5803dd32a31c6b5d4f12148331ba581789f14072a7fca55cb1eb')
b2sums=('f0e74a7562223ecf73888adfb6936a2ee0a0851dd047cc98e4a2b77d4aca432ac56f050d6b5c1e675ee26b30eba7ba8e0fa0d7f253b1700a06fea89768bd5776')

build() {
  cd netcat-openbsd-debian-${pkgver/_/-}
  for i in `cat debian/patches/series`; do
	echo "** patch $i" 1>&2
	cat "debian/patches/$i"
  done | patch -p1
  make \
	CFLAGS="$CFLAGS -DDEBIAN_VERSION=\"\\\"${pkgrel}\\\"\" -I/usr/include/libbsd" \
	LDFLAGS="$LDFLAGS -lbsd"
}

package() {
  cd netcat-openbsd-debian-${pkgver/_/-}
  install -Dm0755 nc "$pkgdir"/usr/bin/nc
  ln -s nc "$pkgdir"/usr/bin/netcat
  install -Dm0644 nc.1 "$pkgdir"/usr/share/man/man1/nc.1
  install -dm0755 "$pkgdir"/usr/share/licenses/$pkgname/
  head -n28 netcat.c | tail -n+2 >"$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
