# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Moritz Kiefer <moritz.kiefer@purelyfunctional.org>

pkgname=libcbor
pkgver=0.10.1
pkgrel=1
pkgdesc='C library for parsing and generating CBOR, the general-purpose schema-less binary data format'
url='https://github.com/PJK/libcbor'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('cmake' 'cmocka')
provides=('libcbor.so')
source=(https://github.com/PJK/libcbor/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('fd662d59127cd86a7e13eeb87ba0b4a9280f367b77fe85f579e5c230503ad39a323a8b9dfb8c6a49f8a05d5b81460ca4526dc130c7e1d403fd0839c0f37f5548')
b2sums=('49b224493b4743fab0352e139ed9e85873238beb25e23555cd31f2ef720f6eb9ac136f26a92be56778f703f240dab2b6e0beddbb88c5cbf7edf084db7e96df76')

build() {
  cd ${pkgname}-${pkgver}
  cmake . \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS}" \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_TESTS=1
  make -C build VERBOSE=1
  make -C build/test VERBOSE=1
}

check() {
  cd ${pkgname}-${pkgver}
  make -C build/test test
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
