# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Moritz Kiefer <moritz.kiefer@purelyfunctional.org>

pkgname=libcbor
pkgver=0.9.0
pkgrel=1
pkgdesc='C library for parsing and generating CBOR, the general-purpose schema-less binary data format'
url='https://github.com/PJK/libcbor'
arch=('x86_64')
license=('MIT')
depends=('glibc')
makedepends=('cmake' 'cmocka')
provides=('libcbor.so')
source=(https://github.com/PJK/libcbor/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('710239f69d770212a82e933e59df1aba0fb3ec516ef6666a366f30a950565a52981b0d46ca7e0eea739f5785d79cc21fc19acd857a4a0b135f4f6aa3ef5fd3b0')
b2sums=('7e78722650c702552dda4844615c454989281abe8c45ba3c9168af16e9d440a1b7e4e0c9afd89faa48e74ba85dcbe0c7733eec3e44fdafc0ca340e97574fe274')

build() {
  cd ${pkgname}-${pkgver}
  cmake . \
    -Bbuild \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_C_FLAGS="${CFLAGS} ${CPPFLAGS}" \
    -DBUILD_SHARED_LIBS=ON \
    -DWITH_TESTS=1
  make -C build VERBOSE=1
  make -C build/test VERBOSE=1
}

check() {
  cd ${pkgname}-${pkgver}
  make -C build/test test
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
