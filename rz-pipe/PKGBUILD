# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=rz-pipe
pkgname=(python-rzpipe)
pkgver=0.1.0
pkgrel=2
pkgdesc='Access rizin via pipe from any programming language'
url='https://github.com/rizinorg/rz-pipe'
arch=('any')
license=('MIT')
makedepends=('python' 'python-setuptools' 'rizin')
source=(${url}/archive/python-v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('7cd6498ee1bb8af9a4771cf5aa622c06105a1bb999b57cb8257ea1ee26a27b3ccde901023f2205572d0673807ff0559b13a497a1a35ee5e4796e2d023a6ee29a')
b2sums=('0e02fa053ab62dfbd2876ad98521f0fc7e5b98a577dd08e31ab0383b6ca25941cfd6f6a5e57fbfbb1ac995df0fe4ec5f7fdf293f4b8e54444881dd249f35ce8b')

build() {
  cd rz-pipe-python-v${pkgver}/python
  python setup.py build
}

package_python-rzpipe() {
  depends=('python' 'rizin')
  cd rz-pipe-python-v${pkgver}/python
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
