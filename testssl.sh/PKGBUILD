# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Johannes Schleifenbaum <johannes [at] js-webcoding [dot] de>

pkgname=testssl.sh
_pkgver=3.0.6
pkgver=${_pkgver/-/.}
pkgrel=1
pkgdesc='Testing TLS/SSL encryption'
url='https://github.com/drwetter/testssl.sh'
arch=('any')
license=('GPL2')
depends=('bash' 'coreutils' 'util-linux' 'openssl' 'inetutils' 'bind-tools' 'procps-ng')
makedepends=('perl' 'perl-data-dump' 'perl-json' 'perl-test-base')
source=(https://github.com/drwetter/testssl.sh/archive/v${_pkgver}/${pkgname}-${pkgver}.tar.gz
        set-install-dir.patch)
sha512sums=('a118cd3e6d8e560f6c739356551091e6186e87bdaec24c8bc987c3503b5c22276f69a5fafc1f73cfd65528844ca7c37f3ee473cfb1de0646067ee0a48089eb26'
            '82bb83a5553bdc8d02cb655c76163ac9c86b0fc1cff9c5627cb035eae016c5a8b76a3a9e278de4578779bf63a35548815a53911288872530d65c1100c065f621')
b2sums=('840eb0027f6b3e9ae919c9d8765c138a10ef9789e14dadc7d1e6e27fef2e906e8182309b2ef504312fc7577fc84b25d4efa59121c4c641a6bbaf91ab14259f18'
        '26f90e385085e0c50bd877b37e85f489177bcef0be90baad096c6f3e95732542e2adcaf2aa03ba75d264607d869ea479b768834ba476e258eea291c452f4f3b9')

prepare() {
  cd ${pkgname}-${_pkgver}
  patch -p0 < "${srcdir}/set-install-dir.patch"
  rm bin/openssl.Linux*
}

check() {
  cd ${pkgname}-${_pkgver}
  # the bundled openssl segfaults in the tests
  # TESTSSL_INSTALL_DIR="$(pwd)" prove -v
}

package() {
  cd ${pkgname}-${_pkgver}
  install -Dm 755 testssl.sh "${pkgdir}/usr/bin/testssl"
  install -Dm 644 etc/* -t "${pkgdir}/usr/share/testssl.sh/etc"
  install -Dm 644 Readme.md doc/testssl.1.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 doc/testssl.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
