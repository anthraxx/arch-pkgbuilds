# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rack-protection
pkgname=ruby-${_gemname}
pkgver=2.2.0
pkgrel=1
pkgdesc='Protect against typical web attacks, works with all Rack apps, including Rails'
url='http://sinatrarb.com/protection/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rack')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-bundler' 'ruby-minitest')
checkdepends=('ruby-rspec' 'ruby-rack-test')
options=('!emptydirs')
source=(https://github.com/sinatra/sinatra/archive/v${pkgver}/sinatra-${pkgver}.tar.gz)
sha512sums=('be9432a8efa93578accc836cd0fecca128e56989d8596f1b3d33d2d956e90acd80244b98473fff2adae13ed4a1e933c41e73c80755a6f3418a65cdf33c011145')
b2sums=('e407b9241cab27e326c0a2d4d83593e292de902b6ed19a12f17b0cd114230b0d10f1ffaa8f8b8237d17b547c7e012c57456af357019b228f3d7a88847220cf33')

prepare() {
  cd sinatra-${pkgver}/${_gemname}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec Rakefile
}

build() {
  cd sinatra-${pkgver}/${_gemname}
  rake build
}

check() {
  cd sinatra-${pkgver}/${_gemname}
  rake test
}

package() {
  cd sinatra-${pkgver}/${_gemname}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 License -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -r "${pkgdir}/${_gemdir}/cache"
}

# vim: ts=2 sw=2 et:
