# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=tasklib
pkgname=python-${_pyname}
pkgver=2.4.3.r10.g16d6e54
# Current trunk, includes zoneinfo support from:
# https://github.com/GothenburgBitFactory/tasklib/commit/0307266a6f1c53110f819064f8b79d00eb73c2d4
_commit=16d6e54f7a41f9bfc1d2d7d6fdcd8e90cbc5e9a3
pkgrel=1
pkgdesc='Python library for interacting with taskwarrior databases'
url='http://tasklib.readthedocs.org/en/latest/'
arch=('any')
license=('BSD')
depends=('python')
makedepends=('python-setuptools' 'python-sphinx')
source=(https://github.com/GothenburgBitFactory/tasklib/archive/${_commit}.tar.gz)
sha512sums=('42b08d1e1c60ec3a489efe5e26c8cf2f50a57b35f0d8d2790c3e7423fe6107c9699c8bcdb39c635b41f098ef6f824bc6c315eaddf457746f80ed85621eb6e2a5')
b2sums=('547b32f3936c16759ac04d46328835afa0d0387b6820aa3afcf4aca79ec10cadef0605a8396a866ed3ed0da5f832694340ce0d81f94955752320a9a3ec0951f4')

build() {
  cd ${_pyname}-${_commit}
  python setup.py build
  export PYTHONPATH=..
  make -C docs text man
}

package() {
  cd ${_pyname}-${_commit}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 docs/_build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
