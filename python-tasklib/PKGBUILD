# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=tasklib
pkgname=python-${_pyname}
pkgver=2.4.0
pkgrel=1
pkgdesc='Python library for interacting with taskwarrior databases'
url='http://tasklib.readthedocs.org/en/latest/'
arch=('any')
license=('BSD')
depends=('python' 'python-pytz' 'python-tzlocal')
makedepends=('python-setuptools' 'python-sphinx')
source=(https://github.com/robgolding/tasklib/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('0d55801f315f4f5f7b32c103e672b161e739f57dd1ef1803eb1a0950e5177767d8f5c9edc80e917b87513b8a3de7dafc2570d969447bb4d1fae8737a24980b24')
b2sums=('1c64892e64ecef0c6f24b3b3d8ccfc50f8fe9f1f63347b0091268a7cdb9a5a82ee4ff766c96848e60cd94e6367cd82c015967f8c9c21a4228b26a1b82d923efb')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
  export PYTHONPATH=..
  make -C docs text man
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 docs/_build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
