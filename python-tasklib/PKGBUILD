# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=tasklib
pkgname=python-${_pyname}
pkgver=2.3.0
pkgrel=1
pkgdesc='Python library for interacting with taskwarrior databases'
url='http://tasklib.readthedocs.org/en/latest/'
arch=('any')
license=('BSD')
depends=('python' 'python-pytz' 'python-tzlocal')
makedepends=('python-setuptools' 'python-sphinx')
source=(https://github.com/robgolding/tasklib/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('8c4e8753bb6637a78d7ac4cf3f9d3b2c78a2ffb28e87cacb8a7ca9333265ce06793f62de7c1d157b2c3c87b0cb862382fbdf797054d73dd8720d2b7b23205014')
b2sums=('aecf028f91627cb7ca3f83c02404fdc3d7d9ed9fc68342e8732a149195d1c5f6e66845de0a6ff532df60be40faa82e16be041647dbd18be619329fafed5b7a88')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
  export PYTHONPATH=..
  make -C docs text man
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 docs/_build/man/${_pyname}.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
  install -Dm 644 docs/_build/text/* -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
