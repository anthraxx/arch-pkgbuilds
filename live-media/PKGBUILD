# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Gilles CHAUVIN <gcnweb@gmail.com>

pkgname=live-media
pkgver=2022.10.01
pkgrel=1
pkgdesc='Set of C++ libraries for multimedia streaming'
url='http://live555.com/liveMedia'
arch=('x86_64')
license=('LGPL')
depends=('gcc-libs' 'openssl')
options=('staticlibs')
source=(https://download.videolan.org/contrib/live555/live.${pkgver}.tar.gz)
sha256sums=('429de73061e3fc6901c4d2f0b7562ae3f6233060ca4b5e182fe555d065cbdd45')
sha512sums=('9f2622fa9fd290bf519ed25858041c216a28491e6ebcacdb11096df9c59284d22c33eebc2b5db3133dace437a555fb74e5f215b52ec5c73184067dcb685039f3')

build() {
  cd live
  export CFLAGS+=" -ffat-lto-objects"
  export CXXFLAGS+=" -ffat-lto-objects"
  sed -E 's|(-DSOCKLEN_T=socklen_t)|\1 -fPIC -DRTSPCLIENT_SYNCHRONOUS_INTERFACE=1|g' -i config.linux
  ./genMakefiles linux
  make
}

package() {
  cd live
  for dir in BasicUsageEnvironment groupsock liveMedia UsageEnvironment; do
    install -Dm 644 ${dir}/*.a -t "${pkgdir}/usr/lib"
    install -Dm 644 ${dir}/include/*.h* -t "${pkgdir}/usr/include/${dir}"
  done
  find testProgs -type f -perm 755 -print0|xargs -0 -I '{}' install -Dm 755 '{}' -t "${pkgdir}/usr/bin"
}

# vim: ts=2 sw=2 et:
