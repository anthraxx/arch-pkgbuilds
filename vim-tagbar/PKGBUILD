# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rhinoceros <https://aur.archlinux.org/account/rhinoceros>
# Contributor: Patrice Peterson <runiq at archlinux dot us>

pkgname=vim-tagbar
pkgver=2.7
pkgrel=3
pkgdesc='Plugin to browse the tags of the current file and get an overview of its structure'
url='https://majutsushi.github.io/tagbar/'
arch=('any')
license=('custom:vim')
depends=('ctags' 'vim')
groups=('vim-plugins')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/majutsushi/tagbar/archive/v${pkgver}.tar.gz)
sha512sums=('d964d3055f1679aad86b0756cc444a4857eb29ca22f2bd3567ad6c8d073e75d0c8823109038415f5bd1d269bbf8e36da6f34d70fedee54c10f90535ef79ff3c8')

package() {
  cd ${pkgname#vim-}-${pkgver}
  _installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -Dm 644 doc/tagbar.txt -t "${_installpath}/doc"
  install -Dm 644 autoload/tagbar.vim -t "${_installpath}/autoload"
  install -Dm 644 plugin/tagbar.vim -t "${_installpath}/plugin"
  install -Dm 644 syntax/tagbar.vim -t "${_installpath}/syntax"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
