# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rhinoceros <https://aur.archlinux.org/account/rhinoceros>
# Contributor: Patrice Peterson <runiq at archlinux dot us>

pkgname=vim-tagbar
pkgver=3.1.1
pkgrel=1
pkgdesc='Plugin to browse the tags of the current file and get an overview of its structure'
url='https://github.com/preservim/tagbar'
arch=('any')
license=('custom:vim')
depends=('ctags' 'vim-plugin-runtime')
groups=('vim-plugins')
source=(https://github.com/preservim/tagbar/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('bbe9215e742dcafa7e943fadd701cd009580301395fba20600a94ce07270e706014abea600fcab5c1d52fc372a9245b60074ab47274d99676edc436a25103445')
b2sums=('fe8347b739003d9c595e329650099a9e0fbec7c211269d6ce64df3ed7a97048b4df9ab77312c43ad8bf310d4e616d87a24bc8492e0bced2be43c0e626fc71710')

package() {
  cd ${pkgname#vim-}-${pkgver}
  _installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -d "${_installpath}"
  cp -r -t "${_installpath}" autoload doc plugin syntax
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
