# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Bart≈Çomiej Piotrowski <nospam@bpiotrowski.pl>
# Contributor: Patrick Leslie Polzer <leslie.polzer@gmx.net>
# Contributor: cdhotfire <cdhotfire@gmail.com>
# Contributor: Shinlun Hsieh <yngwiexx@yahoo.com.tw>
# Contributor: Elisamuel Resto <ryuji@simplysam.us>

pkgname=chrony
pkgver=4.2
pkgrel=1
pkgdesc='Lightweight NTP client and server'
url='https://chrony.tuxfamily.org/'
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'libcap' 'libcap.so' 'libedit' 'libedit.so' 'libseccomp' 'libseccomp.so'
         'nettle' 'libnettle.so' 'gnutls')
makedepends=('asciidoctor')
options=('!emptydirs')
backup=('etc/chrony.conf')
source=(https://download.tuxfamily.org/chrony/${pkgname}-${pkgver}.tar.gz
        ${pkgname}-${pkgver}.tar.gz.asc::https://download.tuxfamily.org/chrony/${pkgname}-${pkgver}-tar-gz-asc.txt
        chrony.sysusers
        chrony.tmpfiles)
sha256sums=('273f9fd15c328ed6f3a5f6ba6baec35a421a34a73bb725605329b1712048db9a'
            'SKIP'
            '56aa817638c8f959169d514f1e7fc727ef31e7d3beda4fca5165292c7602ee42'
            'c0b52787f68f3ef365eef64f53f22b4fb0046a91f0e38923f9d16f1ca19c3f3a')
b2sums=('6eac4f144f5811d0f83a9827e2b5113dead1ff96952f6e6d32dcea9858e04512f635d70237fe6faced095991204b0f62bcb0e9d1a4b34e8778f205058afdfb45'
        'SKIP'
        'e310a05f95d84741f7ddbbc496c52a9f603bb79afc47e57d291a92ae02efb630a357fda378ab5a11875c3997d073e05019d88dce7bbf4377a0228f328ba78fec'
        'e709df2f97b523ef1c7a9dae3232ccbd235ac8f025eba866165272f8ba0372a0aba965d3aae1402395e43331bd629b9d8f3fbffd75fc254f4a7e5ecd47f73130')
validpgpkeys=(
  '8F375C7E8D0EE125A3D3BD51537E2B76F7680DAC' # Miroslav Lichvar <mlichvar@redhat.com>
)

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i \
    -e 's|pool pool.ntp.org|pool 2.arch.pool.ntp.org|g' \
    -e 's|server foo.example.net|server 0.arch.pool.ntp.org|g' \
    -e 's|server bar.example.net|server 1.arch.pool.ntp.org|g' \
    -e 's|server baz.example.net|server 3.arch.pool.ntp.org|g' \
    examples/chrony.conf.example*
  sed 's|/usr/sbin|/usr/bin|g' -i examples/*.service
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --enable-scfilter \
    --enable-ntp-signd \
    --with-user=chrony \
    --with-sendmail=/usr/bin/sendmail \
    --with-hwclockfile=/etc/adjtime \
    --sbindir=/usr/bin \
    --with-pidfile=/run/chrony/chronyd.pid
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install install-docs

  install -Dm 644 "${srcdir}/chrony.sysusers" "${pkgdir}/usr/lib/sysusers.d/chrony.conf"
  install -Dm 644 "${srcdir}/chrony.tmpfiles" "${pkgdir}/usr/lib/tmpfiles.d/chrony.conf"
  install -Dm 644 examples/{chronyd,chrony-wait}.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 examples/chrony.logrotate "${pkgdir}/etc/logrotate.d/chrony"
  install -Dm 644 README NEWS FAQ -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/* -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
  echo "chronyd.service" | install -Dm 644 /dev/stdin "${pkgdir}/usr/lib/systemd/ntp-units.d/chrony.list"

  install -Dm 644 examples/chrony.conf.example3 "${pkgdir}/etc/chrony.conf"
  sed -i \
    -e '/^! pool /s/^! //' \
    -e '/^! leapsectz right\/UTC/s/^! //' \
    -e '/^! makestep 1.0 3/s/^! //' \
    -e '/^! rtcsync/s/^! //' \
    "${pkgdir}/etc/chrony.conf"

  rmdir "${pkgdir}"{/var/lib/chrony,/var/lib}
}

# vim: ts=2 sw=2 et:
