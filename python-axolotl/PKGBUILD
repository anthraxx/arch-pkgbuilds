# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexandre Bouvier <contact AT amb DOT tf>
# Contributor: Ivan Shapovalov <intelfx@intelfx.name>
# Contributor: Tommaso Sardelli <lacapannadelloziotom AT gmail DOT com>
# Contributor: Philipp Joram <phijor AT t-online DOT de>

pkgbase=python-axolotl
pkgname=('python-axolotl' 'python2-axolotl')
pkgver=0.1.42
pkgrel=2
pkgdesc='Python port of libaxolotl'
url='https://github.com/tgalal/python-axolotl'
arch=('any')
license=('GPL3')
makedepends=('python-setuptools' 'python-protobuf' 'python-cryptography' 'python-axolotl-curve25519'
             'python2-setuptools' 'python2-protobuf' 'python2-cryptography' 'python2-axolotl-curve25519')
checkdepends=('python-pytest' 'python2-pytest')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/tgalal/python-axolotl/archive/${pkgver}.tar.gz)
sha256sums=('26a3c43769affe9bfdf82b783684ee62fef1a2c080bdb94ed186a360bdbb50b6')
sha512sums=('db9e1cfab87d690619a672b1782942a18b12b17af555959c17bcc3e2581e9c689c57becc0ea884a1129df9dace17684ba03de38b81f8c8c65cab27962ebdb6c5')

prepare() {
  cp -a ${pkgbase}-${pkgver}{,-py2}
}

build() {
  msg2 'Building python...'
  (cd ${pkgbase}-${pkgver}
    python setup.py build
  )
  msg2 'Building python2...'
  (cd ${pkgbase}-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  msg2 'Testing python...'
  (cd ${pkgbase}-${pkgver}
    py.test
  )
  msg2 'Testing python2...'
  (cd ${pkgbase}-${pkgver}-py2
    py.test2
  )
}

package_python-axolotl() {
  depends=('python-protobuf' 'python-cryptography' 'python-axolotl-curve25519')
  cd ${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

package_python2-axolotl() {
  depends=('python2-protobuf' 'python2-cryptography' 'python2-axolotl-curve25519')
  cd ${pkgbase}-${pkgver}-py2
  python2 setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
