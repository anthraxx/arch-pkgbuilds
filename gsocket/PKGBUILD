# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=gsocket
pkgver=1.4.38
pkgrel=2
pkgdesc='Global Socket moving data from here to there securely, fast and trough NAT/Firewalls'
url='https://www.gsocket.io/'
arch=('x86_64')
license=('custom')
depends=('glibc' 'bash' 'openssl' 'rsync' 'sshfs' 'openssh' 'grep' 'binutils' 'coreutils')
source=("https://github.com/hackerschoice/gsocket/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('439863dbaafbcb585d13f0830aee310eb17893c9e26d55db3c6576f7e868b2c1af7eba8d0dcc16ecbb51a444be0e4deb8e527511899787f5cfec2c52ec43495e')
b2sums=('080c54b35ed7677e1fef60d2db33c8b59d514103a4f6dd2dd4485c43a48ea8a297bb05c62ea3889574895145a40ec9cd168542a8aa0795753616697aff979b0a')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
