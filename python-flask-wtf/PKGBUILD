# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Serge Victor <arch@random.re>

_pyname=flask-wtf
pkgname=python-flask-wtf
pkgver=1.0.0
pkgrel=1
pkgdesc='Simple integration of Flask and WTForms'
url='https://flask-wtf.readthedocs.io/'
arch=('any')
license=('BSD')
depends=('python-flask' 'python-wtforms' 'python-werkzeug')
makedepends=('python-setuptools' 'python-sphinx' 'python-pallets-sphinx-themes'
             'python-sphinxcontrib-log-cabinet' 'python-sphinx-issues')
checkdepends=('python-flask-babel' 'python-tox')
options=('!makeflags')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/wtforms/flask-wtf/archive/v${pkgver}.tar.gz)
sha512sums=('eb9e2268954cef004faef32c98cf162daf3538bfa926ba181291eba655338755d449f2a6d436b13474e0fcb08d5736f18b1e326b1ce0b8ca502120c834c85867')

prepare() {
  cd ${_pyname}-${pkgver}
  sed -i "s/^release, version =/& '${pkgver}', '${pkgver%.*}.x' #/" docs/conf.py
}

build() {
  cd ${_pyname}-${pkgver}
  make -C docs man text SPHINXBUILD=sphinx-build
}

check() {
  cd ${_pyname}-${pkgver}
  tox -e py310
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst  -t "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r docs/_build/text "${pkgdir}/usr/share/doc/${pkgname}/text"
  install -Dm 644 docs/_build/man/flask-wtf.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
