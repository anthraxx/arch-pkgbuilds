# Maintainer:
# Contributor: travisghansen <travisghansen@yahoo.com>
# Contributor: aleiphoenix <aleiphoenix@gmail.com>

pkgname=librabbitmq-c
pkgver=0.10.0
pkgrel=1
pkgdesc="A RabbitMQ(amqp) library written in C-language"
arch=('x86_64')
license=('MIT')
depends=('popt' 'openssl')
makedepends=('cmake' 'docbook-xsl' 'doxygen' 'xmlto')
url="https://github.com/alanxz/rabbitmq-c"
source=("$pkgname-$pkgver.zip::https://github.com/alanxz/rabbitmq-c/archive/v$pkgver.zip")
sha512sums=('8fd08564accb92524f4e5e87eec4fb9db94902b7854fc1cbf2413bedf85c6b8e5d8a7104a79dd34dd501f438f22de6fbef3c763a76c27ef734d9d6cb4f1d5d43')

build() {
  cd ${pkgname:3}-$pkgver
  cmake . \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DBUILD_EXAMPLES=OFF \
    -DBUILD_TOOLS=ON \
    -DBUILD_TOOLS_DOCS=ON \
    -DBUILD_TESTS=OFF \
    -DENABLE_SSL_SUPPORT=ON \
    -DBUILD_API_DOCS=ON
  make
}

package() {
  cd ${pkgname:3}-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE-MIT "$pkgdir"/usr/share/licenses/$pkgname/LICENSE
}
