# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Juergen Hoetzel <juergen@archlinux.org>
# Contributor: John Proctor <jproctor@prium.net>

pkgname=socat
pkgver=1.7.4.3
pkgrel=1
pkgdesc='Multipurpose relay'
url='http://www.dest-unreach.org/socat/'
license=('GPL2')
arch=('x86_64')
depends=('glibc' 'readline' 'libreadline.so' 'openssl')
checkdepends=('bash' 'net-tools' 'procps-ng')
source=("http://www.dest-unreach.org/socat/download/${pkgname}-${pkgver}.tar.gz")
sha256sums=('d697245144731423ddbbceacabbd29447089ea223e9a439b28f9ff90d0dd216e')
b2sums=('d91de7ef55332001e6439f64130555b9558338fb9b6c15c91ab5efc8f86a2e7e0f5fe0b292754731a198d83be5f511c3388c65c7c7f559c55691f42703f1849b')

build() {
  cd "${pkgname}-${pkgver}"
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man \

  make
}

check() {
  cd "${pkgname}-${pkgver}"
  # TODO: one of the tests always fails, but lets keep
  # it for manual sanity check
  make test||true
}

package() {
  cd "${pkgname}-${pkgver}"
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
