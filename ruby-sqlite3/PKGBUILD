# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>

_gemname=sqlite3
pkgname=ruby-${_gemname}
pkgver=1.4.2
pkgrel=5
pkgdesc='Ruby bindings for the SQLite3 embedded database'
url='https://github.com/luislavena/sqlite3-ruby'
arch=('x86_64')
license=('BSD')
depends=('ruby' 'sqlite')
options=('!emptydirs')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-rake-compiler' 'ruby-hoe' 'ruby-mini_portile' 'ruby-minitest')
source=(https://github.com/sparklemotion/sqlite3-ruby/archive/v${pkgver}/${pkgname}-v${pkgver}.tar.gz
        # https://github.com/sparklemotion/sqlite3-ruby/pull/309
        ruby-sqlite3-fix-tests.patch::https://github.com/sparklemotion/sqlite3-ruby/commit/f687023b58e2bd7ab793ecf4dcf1452d439a944f.patch)
sha256sums=('7e876104c0feb6e0e69befc3f6055fb7f20f37b13a667c8f9782d22272f37479'
            '3bcc0b1848189c7fafd5b2bcb5a2ea51a8eba4d1b06788d497b306585d17b49e')
sha512sums=('c30938f0ad835e23f9e857323ea654d111d1ef5d6f055471ca5f8f080bcdcbb56ac773be9aab34b6432cdb53d69a842c99dcebbd3ea9808967f04907863abc89'
            '57f3c3cbfb89fbe208dd2b85e7ddd68781cfb1094b8e43753a7cf8e907b6d31b597355ea268a529c30c985f7ff9d8367a1bb2090d33652f5abe2a2413ecb65f4')

prepare() {
  cd ${_gemname}-ruby-${pkgver}

  patch --verbose --strip=1 --input=../ruby-sqlite3-fix-tests.patch

  sed 's|~>|>=|g' -i Gemfile
}

build() {
  cd ${_gemname}-ruby-${pkgver}
  rake compile
  rake gem
}

check() {
  cd ${_gemname}-ruby-${pkgver}
  rake test
}

package() {
  cd ${_gemname}-ruby-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}/${_gemdir}" \
    -n "${pkgdir}/usr/bin" pkg/${_gemname}-${pkgver}.gem

  install -Dm 644 README.rdoc CHANGELOG.rdoc API_CHANGES.rdoc -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"

  cd "${pkgdir}/${_gemdir}"
  rm -r cache gems/${_gemname}-${pkgver}/ext
  rm -rf "${pkgdir}/${_gemdir}/doc/${_gemname}-${pkgver}/ri/ext/"
  rm extensions/*/*/${_gemname}-${pkgver}/{mkmf.log,gem_make.out}
}

# vim: ts=2 sw=2 et:
