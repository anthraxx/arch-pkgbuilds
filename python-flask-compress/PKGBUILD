# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Cedric Girard <girard.cedric@gmail.com>

_pyname=Flask-Compress
pkgname=python-flask-compress
pkgver=1.12
pkgrel=1
pkgdesc='Compress responses in your Flask app'
url='http://pypi.python.org/pypi/Flask-Compress/'
arch=('any')
license=('BSD')
depends=('python' 'python-flask' 'python-brotli')
makedepends=('python-setuptools' 'python-setuptools-scm' 'python-build' 'python-installer' 'python-wheel')
checkdepends=('python-pytest')
source=(https://pypi.io/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz)
sha256sums=('e2159499f39d618a4d56ba0484e7b58b57956b9a2c6d3510f095f5bb14b7afc5')
sha512sums=('5712aff44aaaa379656e03f7b63fdef727721245cb9329e698a2e25aefca21a77202f5eb0c5f5828395647c694a5b417778a7b5db9eb592753a979d3994603a5')

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH="build/lib" pytest
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
