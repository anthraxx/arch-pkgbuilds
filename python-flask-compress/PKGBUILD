# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Cedric Girard <girard.cedric@gmail.com>

_pyname=Flask-Compress
pkgname=python-flask-compress
pkgver=1.10.1
pkgrel=1
pkgdesc='Compress responses in your Flask app'
url='http://pypi.python.org/pypi/Flask-Compress/'
arch=('any')
license=('BSD')
depends=('python' 'python-flask' 'python-brotli')
makedepends=('python-setuptools')
source=(https://pypi.io/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz)
sha256sums=('28352387efbbe772cfb307570019f81957a13ff718d994a9125fa705efb73680')
sha512sums=('fb94aef71f59e38a6058f5ef799c8973d258cf624b1d8bf0a991cac2e4eabb73e2836ce351faa73bc7640720396183212a6dc8d72428b53ceb3473f7b61f0d27')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pyname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
