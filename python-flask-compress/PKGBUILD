# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Cedric Girard <girard.cedric@gmail.com>

_pyname=Flask-Compress
pkgname=python-flask-compress
pkgver=1.5.0
pkgrel=1
pkgdesc='Compress responses in your Flask app'
url='http://pypi.python.org/pypi/Flask-Compress/'
arch=('any')
license=('BSD')
depends=('python-flask')
makedepends=('python-setuptools')
source=(https://pypi.io/packages/source/${_pyname:0:1}/${_pyname}/${_pyname}-${pkgver}.tar.gz)
sha256sums=('f367b2b46003dd62be34f7fb1379938032656dca56377a9bc90e7188e4289a7c')
sha512sums=('fe2d1dfef8be8d04b78a2938dd52a1f6068e6c35741f593fbb0d3d050b789b8d70188d683f7028b92a180653afda0566bda29b82475b20444f6563c8620ff1ec')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pyname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
