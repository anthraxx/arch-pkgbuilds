# Maintainer: Jelle van der Waa <jelle@archlinux.org
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Husam Bilal <husam212 AT gmail DOT com>

_name=sqlalchemy-utils
pkgname=python-sqlalchemy-utils
pkgver=0.38.2
pkgrel=1
pkgdesc='Various utility functions, new data types and helpers for SQLAlchemy'
url='https://github.com/kvesteri/sqlalchemy-utils'
depends=('python' 'python-sqlalchemy' 'python-anyjson' 'python-babel'
         'python-arrow' 'python-intervals' 'python-phonenumbers' 'python-passlib'
         'python-colour' 'python-dateutil' 'python-furl' 'python-cryptography'
         'python-pendulum')
checkdepends=('python-pytest' 'python-mock' 'python-flexmock' 'python-jinja')
makedepends=('python' 'python-setuptools')
license=('BSD')
arch=('any')
source=(https://github.com/kvesteri/sqlalchemy-utils/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('611099e53995aadf226a4e53b3a7e61d6bbb7b33574b65c25f9880daa9b35bbd261929a804b2f14de774b253d64a57dbba9fa7af0a931648b9e0b53477421e39')
b2sums=('0a81b3f53e4e2207820f408e6f932afc28b7be1848753a5734ace0c295ea4269df406b87953a1036e21038b5c2f2cf96cedda0a10fbc6756b266e24cb373ce47')

build() {
  cd ${_name}-${pkgver}
  python setup.py build
}

check() {
  cd ${_name}-${pkgver}
  # Tests require a postgres and MySQL db
  pytest --disable-pytest-warnings tests/test_models.py
}

package() {
  cd ${_name}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
