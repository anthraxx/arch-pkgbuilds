# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=gufw
pkgver=21.04.0
pkgrel=2
pkgdesc='Uncomplicated way to manage your Linux firewall'
url='https://gufw.org/'
arch=('any')
license=('GPL3')
depends=('gtk3' 'polkit' 'python' 'python-gobject' 'ufw' 'webkit2gtk'
         'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('intltool' 'python-distutils-extra')
source=(https://github.com/costales/gufw/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('58769f3edc4faca9400c78907368209c3bdbc3bac3534b84f1484bce6c08c93778ad20e25ffd3857afaea73076b713a00cd3ff2f8e034ccb22864dfd082b1105')
b2sums=('33be802a5843c9cc166bd7de3fed32d66e72ec929e64c37c347ff7289030f9d19ce26655f0d079216590b77a071e4276effa28447894c983d7e49db8de1afb4c')

prepare () {
  cd ${pkgname}-${pkgver}
  local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  sed 's|/usr/share/gufw/gufw/gufw.py $1|/usr/lib/python'"${PYTHONVERSION}"'/site-packages/gufw/gufw.py "$@"|' -i bin/gufw-pkexec
}

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  # required so desktop, icon and translation files get installed
  export XDG_RUNTIME_DIR="$(mktemp -d --tmpdir gufw-build-XXXXXXXXX)"
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
}

# vim: ts=2 sw=2 et:
