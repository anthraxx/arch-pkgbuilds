# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor : Martin Wimpress <code@flexion.org>

pkgname=gufw
_pkgver=18.04
pkgver=${_pkgver}.0
pkgrel=2
pkgdesc='Uncomplicated way to manage your Linux firewall'
url='https://gufw.org/'
arch=('any')
license=('GPL3')
depends=('gtk3' 'polkit' 'python-gobject' 'ufw' 'webkit2gtk' 'desktop-file-utils' 'gtk-update-icon-cache')
makedepends=('intltool' 'python-distutils-extra')
source=(https://launchpad.net/gui-ufw/trunk/${_pkgver}/+download/gui-ufw-${pkgver}.tar.gz)
sha256sums=('b6b12524d3d2c6035288f15003c97aa23eef862fdaa78fb621e045cd793374fc')
sha512sums=('86154bd802e87ecd4bb6cfe74ae50d716d96bf33ca21847d439f602a4d318f7fcfa1f8a7222567ba194dba479f217842be7f1538ad725256458ebc428a33e831')

prepare() {
  cd gui-ufw-${pkgver}
  sed 's|python3.5|python3.7|g' -i bin/gufw-pkexec
}

package(){
  cd gui-ufw-${pkgver}
  export XDG_RUNTIME_DIR="/tmp/${pkgname}"
  python setup.py install --root="${pkgdir}" -O1
}

# vim: ts=2 sw=2 et:
