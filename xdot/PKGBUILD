# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Randy Heydon <randy.heydon at clockworklab dot net>

pkgname=xdot
_pkgname=${pkgname}.py
pkgver=0.9
pkgrel=3
pkgdesc="Interactive viewer for graphs written in Graphviz's dot language"
url='https://github.com/jrfonseca/xdot.py'
arch=('any')
license=('LGPL2.1')
depends=('graphviz' 'python' 'python-gobject' 'python-cairo' 'gtk3' 'python-setuptools')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/jrfonseca/${_pkgname}/archive/${pkgver}.tar.gz
        xdot.desktop)
sha512sums=('806f74030178528e3f28bb5da858307820dedf719d101a17bf0463d4924b3753af6f4a7251b6552cfe937316f1909051cd4dfaa4c6672b37823b0a617f5b1fab'
            '7bd7994f03c4d7634a9ab780423f1e9d647df0836fd6d8f825dc0bac6ad506ad44457ba473367d1a16b89d00f441c730cb2b66cb5c300542addcf622be82c867')

prepare() {
  cd ${_pkgname}-${pkgver}
  # Don't check for unneeded module (FS#57235)
  sed -i '/install_requires=/d' setup.py
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
  install -Dm 644 ../xdot.desktop -t "${pkgdir}/usr/share/applications"
}

# vim: ts=2 sw=2 et:
