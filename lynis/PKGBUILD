# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: SÃ©bastien Luttringer <seblu@aur.archlinux.org>

pkgname=lynis
pkgver=3.0.7
pkgrel=1
pkgdesc='Security and system auditing tool to harden Unix/Linux systems'
url='https://cisofy.com/lynis/'
arch=('any')
license=('GPL3')
backup=('etc/lynis/default.prf')
depends=('sh' 'awk')
optdepends=('net-tools: networking tests'
            'bind-tools: nameserver tests'
            'iptables: firewall tests')
source=("https://downloads.cisofy.com/lynis/lynis-$pkgver.tar.gz"{,.asc})
sha512sums=('c65d42071a48b15c2f16ceaf30404db0dac740c4cc6109442a0757e0576f794ebaf9430547eb1a0a452ce85993f00e8779ce2c4ffc47c206d674b8fd2cb02e3a'
            'SKIP')
b2sums=('d9cddcba9c83ddee08ea38c8e80bf6f0a78179ad102673a876a1fe3d4544612e945d8637a8d026ce7bff72b09d6274223031e1cb6fc74596b2c65f0380fecd67'
        'SKIP')
validpgpkeys=('84FAA9983B24AEF24D6C87F1FEBB7D1812576482') # CISOfy (Software Signing Key) <software@cisofy.com>

prepare() {
  cd "$pkgname"
  sed -e 's|/path/to/lynis|/usr/bin/lynis|g' -i extras/systemd/lynis.service
}

package() {
  cd "$pkgname"

  # application
  install -Dm 755 lynis -t "${pkgdir}/usr/bin"
  install -Dm 644 default.prf -t "${pkgdir}/etc/${pkgname}"
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -ra db include plugins "${pkgdir}/usr/share/${pkgname}"

  # doc files
  install -Dm 644 README INSTALL CHANGELOG.md FAQ \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 "${pkgname}.8" -t "${pkgdir}/usr/share/man/man8"

  # completion
  install -Dm 644 extras/bash_completion.d/${pkgname} \
    -t "${pkgdir}/usr/share/bash-completion/completions"

  # systemd
  install -Dm 644 extras/systemd/{lynis.service,lynis.timer} \
    -t "${pkgdir}/usr/lib/systemd/system"
}

# vim: ts=2 sw=2 et:
