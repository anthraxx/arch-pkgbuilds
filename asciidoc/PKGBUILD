# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: Florian Pritz <flo@xinu.at>
# Contributor: Chris Brannon <cmbrannon79@gmail.com>
# Contributor: Geoffroy Carrier <geoffroy@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>
# Contributor: Jaroslaw Rosiek <philosoph@interia.pl>
# Contributor: Darwin Bautista <djclue917@gmail.com>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>

pkgname=asciidoc
pkgver=10.1.1
pkgrel=1
pkgdesc='Text document format for short documents, articles, books and UNIX man pages'
arch=('any')
url='https://asciidoc.org/'
license=('GPL')
depends=('python' 'libxslt' 'docbook-xsl')
checkdepends=('dblatex' 'graphviz' 'lilypond' 'python-pytest'{,-mock} 'source-highlight')
makedepends=('python-pip')
optdepends=('graphviz: graphviz-filter'
            'lilypond: music-filter'
            'imagemagick: music-filter'
            'source-highlight: source-highlight-filter'
            'dblatex: pdf generation'
            'fop: alternative pdf generation'
            'w3m: text generation'
            'lynx: alternative text generation')
source=("https://github.com/asciidoc-py/asciidoc-py/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
# https://github.com/asciidoc-py/asciidoc-py/issues/234
# source=("https://github.com/asciidoc-py/asciidoc-py/releases/download/$pkgver/$pkgname-$pkgver.tar.gz")
sha256sums=('064e34241bd96e7239e8048596ceb03262d63145d04070d53906151f2042d007')
b2sums=('1a8d6909fc1c21c0ec3f4728a9aaa2fea34fdc34e118161dfe5270a8a1a0a4f3640f9978e7f8512a85d17fd90a8e996b202b54b54dd4dd86fb537c1819bdd06b')

prepare() {
  cd ${pkgname}-py-${pkgver}
  autoconf
  # https://github.com/asciidoc-py/asciidoc-py/issues/201
  sed -i -e '/pip install/s/\.$/--root $(DESTDIR) ./' Makefile.in
}

build() {
  cd ${pkgname}-py-${pkgver}
  ./configure \
    --prefix=/usr
  make build
  make manpages
}

check() {
  cd ${pkgname}-py-${pkgver}
  make test
}

package() {
  cd ${pkgname}-py-${pkgver}
  make install DESTDIR="${pkgdir}"
  make docs DESTDIR="${pkgdir}"
  install -Dm0644 -t "$pkgdir/usr/share/man/man1/" doc/*.1
  # ascidocapi is deprecated, but still included in 10.x on a "provisional" basis
  local _platlib="$(python -c 'import sysconfig; print(sysconfig.get_paths()["platlib"])')"
  install -Dm0644 -t "${pkgdir}/${_platlib}/${pkgname}/" asciidoc/api.py
}
