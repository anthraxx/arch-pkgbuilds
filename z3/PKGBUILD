# Maintainer: Felix Yan <felixonmars@archlinux.org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: rudy.matela

pkgbase=z3
pkgname=('z3' 'python-z3')
pkgver=4.8.15
pkgrel=1
pkgdesc='High-performance theorem prover'
url='https://github.com/Z3Prover/z3'
arch=('x86_64')
license=('MIT')
 # 'java-environment-common' 'mono' 'ocaml' 'ocaml-findlib'
makedepends=('cmake' 'ninja' 'python' 'python-setuptools' 'gcc-libs')
source=(https://github.com/Z3Prover/z3/archive/z3-$pkgver.tar.gz)
sha512sums=('7b08dec5b035a38edc90c4c491f508fd9ed227357de94400169db53d4c59382bd6a81ae6615771023a06534a3aa92668844f0ebfcc2a3b5ef4bba957426a0c6c')

prepare() {
  cd $pkgbase-z3-$pkgver
  mkdir -p build
}

build() {
  cd $pkgbase-z3-$pkgver/build
  cmake .. \
    -GNinja \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib \
    -DZ3_LINK_TIME_OPTIMIZATION=1 \
    -DZ3_BUILD_PYTHON_BINDINGS=1 \
    -DPYTHON_EXECUTABLE=/usr/bin/python
  ninja
}

package_z3() {
  depends=('gcc-libs')
  provides=('libz3' 'libz3.so')
  cd $pkgbase-z3-$pkgver/build
  DESTDIR="$pkgdir" ninja install
  install -Dm 644 ../LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
  rm -r "$pkgdir"/usr/lib/python*
}

package_python-z3() {
  depends=('python' 'z3')
  cd $pkgbase-z3-$pkgver/build
  DESTDIR="$pkgdir" ninja install
  install -Dm 644 ../LICENSE.txt -t "$pkgdir"/usr/share/licenses/$pkgname
  cd "$pkgdir"
  rm -r usr/include usr/bin usr/lib/{cmake,pkgconfig,lib*}
  python -m compileall .
  python -O -m compileall .
}

# vim: ts=2 sw=2 et:
