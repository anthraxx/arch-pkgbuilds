# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-eventlet
pkgname=('python-eventlet' 'python2-eventlet')
_pyname=eventlet
pkgver=0.24.1
pkgrel=1
pkgdesc='Highly concurrent networking library'
url='http://eventlet.net'
arch=('any')
license=('MIT')
makedepends=('python-greenlet' 'python-setuptools' 'python-sphinx' 'python-monotonic'
             'python2-greenlet' 'python2-setuptools' 'python2-sphinx' 'python2-monotonic')
checkdepends=('python-psycopg2' 'python-nose' 'python-pyopenssl' 'python-httplib2' 'python-mock' 'python-pyzmq' 'python-dnspython'
              'python2-psycopg2' 'python2-nose' 'python2-pyopenssl' 'python2-httplib2' 'python2-mock' 'python2-pyzmq' 'python2-dnspython' 'python2-subprocess32')
options=('!makeflags')
source=(${pkgbase}-${pkgver}.tar.gz::https://github.com/eventlet/eventlet/archive/v${pkgver}.tar.gz
        ${pkgbase}-python37-fix.patch::https://github.com/eventlet/eventlet/pull/506.patch)
sha512sums=('f5df2b64d025de136bb6c4615633d3865c460b4916a4070d65113198da42d692cea9f689a57b79a4b5539dc545cdab7d52023cfae7b3d9140c11909b4784a205'
            '7f13bfc830eee11275c70e538bd55dc1dfe71482b82e44908beef421182e9046873f6eb23722b3e76455db833cc3175d6ab486e89ac6ad434f28ca30ee299636')

prepare() {
  patch -d ${_pyname}-${pkgver} -Np1 < ${pkgbase}-python37-fix.patch
  sed -r 's|(check_idle_cpu_usage\(.*,) .*\)|\1 0.8\)|g' -i ${_pyname}-${pkgver}/tests/*_test.py
  cp -a ${_pyname}-${pkgver}{,-py2}
  sed 's|python|python2|' -i ${_pyname}-${pkgver}-py2/examples/*
}

build() {
  (cd ${_pyname}-${pkgver}
    python setup.py build
    make -C doc text
  )
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
    make -C doc text
  )
}

check() {
  (cd ${_pyname}-${pkgver}
    nosetests -sv tests
  )
  (cd ${_pyname}-${pkgver}-py2
    nosetests2 -sv tests
  )
}

package_python-eventlet() {
  depends=('python' 'python-greenlet' 'python-monotonic')
  optdepends=('python-psycopg2: non-blocking PostgreSQL support'
              'python-pyopenssl: non-blocking SSL support'
              'python-httplib2: non-blocking HTTP support'
              'python-pyzmq: non-blocking ZeroMQ support'
              'python-dnspython: non-blocking DNS support')

  cd ${_pyname}-${pkgver}
  python setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

package_python2-eventlet() {
  depends=('python2' 'python2-greenlet' 'python2-monotonic' 'python2-subprocess32')
  optdepends=('python2-psycopg2: non-blocking PostgreSQL support'
              'python2-pyopenssl: non-blocking SSL support'
              'python2-httplib2: non-blocking HTTP support'
              'python2-pyzmq: non-blocking ZeroMQ support'
              'python2-dnspython: non-blocking DNS support')

  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install --prefix=/usr --root="${pkgdir}" -O1
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r doc/_build/text "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
