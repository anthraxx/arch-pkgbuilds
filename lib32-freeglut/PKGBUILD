# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jan Alexander Steffens (heftig) <jan.steffens@gmail.com>
# Contributor: Eric Belanger <eric@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=lib32-freeglut
pkgver=3.2.2
pkgrel=2
pkgdesc='Provides functionality for small OpenGL programs (32-bit)'
url='http://freeglut.sourceforge.net/'
arch=('x86_64')
license=('MIT')
depends=('lib32-libxi' 'lib32-libxrandr' 'lib32-libgl' 'freeglut')
makedepends=('lib32-mesa' 'lib32-glu' 'lib32-libxxf86vm' 'cmake' 'ninja')
replaces=('lib32-glut')
provides=('lib32-glut')
conflicts=('lib32-glut')
options=('debug')
source=(https://downloads.sourceforge.net/freeglut/freeglut-${pkgver}.tar.gz)
sha512sums=('190231951d314f854c244bd27f7c20488403bd0eecffc342097a2e3d1621cec87f6c209a77b4c153aabcf44697b4070227930e295ab74fb88953cbbf94fc81d9')

prepare() {
  cd freeglut-${pkgver}
}

build() {
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='i686-pc-linux-gnu-pkg-config'

  cmake -S freeglut-${pkgver} -B build -G Ninja \
    -DFREEGLUT_BUILD_STATIC_LIBS=OFF \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DCMAKE_INSTALL_LIBDIR=lib32 \
    -DCMAKE_BUILD_TYPE=None
  cmake --build build
}

package() {
  DESTDIR="${pkgdir}" cmake --install build
  rm -rf "${pkgdir}/usr/include"
  ln -sr "${pkgdir}"/usr/lib32/pkgconfig/{glut,freeglut}.pc
  install -Dm 644 -t "${pkgdir}/usr/share/licenses/${pkgname}" freeglut-${pkgver}/COPYING
}

# vim: ts=2 sw=2 et:
