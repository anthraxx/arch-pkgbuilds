# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: sh0 <mee@sh0.org>
# Contributor: Maxwel <msg.maxwel@gmail.com>

pkgname=pax-utils
pkgver=1.3.4
pkgrel=1
pkgdesc='ELF utils that can check files for security relevant properties'
url='https://wiki.gentoo.org/wiki/Hardened/PaX_Utilities'
arch=('x86_64')
license=('GPL2')
depends=('bash' 'libcap' 'libcap.so' 'libseccomp' 'python' 'python-pyelftools')
makedepends=('linux-headers')
source=(https://distfiles.gentoo.org/distfiles//${pkgname}-${pkgver}.tar.xz)
sha512sums=('44a475860823e8b70b1d09d69e5fba3ed8298511d07e1e7b09ce62237cb8b1ecee8fc2fc550d6853d0b9f8db3c350bf78ced49d5f210997b294dc10e36627fcd')
b2sums=('c105ff568a8e4378cfef5f8b9acb415a850b1e209ea8413cdf518de82e5e925fdba60288557f042bf02d2e075ed5e538175b2078e4dafbe5c54117ed580fd87e')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -E 's|(  paths =) \[\]|\1 ["/usr/lib"]|g' -i lddtree.py
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --with-caps \
    --with-seccomp \
    --with-python
  make USE_SECCOMP=yes USE_CAP=yes USE_PYTHON=yes
}

check() {
  cd ${pkgname}-${pkgver}
  LD_LIBRARY_PATH=/usr/lib make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
