# Maintainer: Levente Polyak <anthraxx@archlinux.org>
# Contributor: Evangelos Foutras <evangelos@foutrelis.com>
# Contributor: Daurnimator <quae@daurnimator.com>

pkgname=lib32-libidn2
pkgver=2.3.2
pkgrel=1
pkgdesc='Free software implementation of IDNA2008, Punycode and TR46'
url='https://www.gnu.org/software/libidn/#libidn2'
arch=('x86_64')
license=('GPL2' 'LGPL3')
depends=('libidn2' 'lib32-glibc' 'lib32-libunistring')
makedepends=('gtk-doc' 'ruby-ronn')
source=(https://ftp.gnu.org/gnu/libidn/libidn2-${pkgver}.tar.gz{,.sig})
sha256sums=('76940cd4e778e8093579a9d195b25fff5e936e9dc6242068528b437a76764f91'
            'SKIP')
validpgpkeys=('9AA9BDB11BB1B99A21285A330664A76954265E8C'  # Simon Josefsson <simon@josefsson.org>
              '1CB27DBC98614B2D5841646D08302DB6A2670428') # Tim RÃ¼hsen <tim.ruehsen@gmx.de>

prepare() {
  cd libidn2-${pkgver}
  autoreconf -fvi
}

build() {
  cd libidn2-${pkgver}
  export CC='gcc -m32'
  export CXX='g++ -m32'
  export PKG_CONFIG_PATH='/usr/lib32/pkgconfig'
  ./configure \
    --prefix=/usr \
    --libdir=/usr/lib32
  make
}

check() {
  cd libidn2-${pkgver}
  make check
}
package() {
  cd libidn2-${pkgver}
  make DESTDIR="${pkgdir}" install
  rm -rf "${pkgdir}"/usr/{bin,include,share}
}

# vim: ts=2 sw=2 et:
