# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Caleb Maclennan <caleb@alerque.com>
# Contributor: Simon Hauser <Simon-Hauser@outlook.de>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=gitui
pkgver=0.17
pkgrel=1
pkgdesc='Blazing fast terminal-ui for git written in Rust'
url='https://github.com/extrawurst/gitui'
arch=('x86_64')
license=('MIT')
depends=('gcc-libs' 'libxcb' 'zlib') # 'libgit2' 'libgit2.so'
makedepends=('cargo' 'python') # xcb crate needs python
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('4f81b6bc29046ad4a92a5ee9040f68a02debe93529abad78d806e4eaa35a17ff')
b2sums=('6e69ec28cff83ef7bb1737679e4190ca6b8b2854a3d6ae3d22e0f2f0f878be74004593a88d10e06a7880ce3a948b688b04f711ffd5e6139ea8616b988b62bde0')

prepare() {
  cd "${pkgname}-${pkgver}"
  cargo fetch --locked --target "$CARCH-unknown-linux-gnu"
}

build() {
  cd "${pkgname}-${pkgver}"
  # Upstream Issue: https://github.com/rust-lang/git2-rs/issues/721
  # export LIBGIT2_SYS_USE_PKG_CONFIG=1
  cargo build --frozen --release
}

check() {
  cd "${pkgname}-${pkgver}"
  # export LIBGIT2_SYS_USE_PKG_CONFIG=1
  cargo test --frozen --release
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release/gitui -t "${pkgdir}/usr/bin/"
  install -Dm 644 {KEY_CONFIG,README,THEMES}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
