# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Simon Hauser <Simon-Hauser@outlook.de>
# Contributor: Wesley Moore <wes@wezm.net>

pkgname=gitui
pkgver=0.16.2
pkgrel=1
pkgdesc='Blazing fast terminal-ui for git written in Rust'
url='https://github.com/extrawurst/gitui'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'libgit2' 'libgit2.so' 'libxcb')
makedepends=('cargo' 'python') # xcb crate needs python
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('4e0e56fd897c5b0b42bd7fd645bd4cfae876d82e12422e70488527e8e60d5853')
b2sums=('e850b408951261ef7d6d5bf4a22f6e52495f55fe546c86f740927086b89b89b716ce2c30c716f5cc29f12b25f28e4c4668944dda18b04a4e2a6b03e1fb3e7155')

build() {
  cd "${pkgname}-${pkgver}"
  export LIBGIT2_SYS_USE_PKG_CONFIG=1
  cargo build --release --locked
}

check() {
  cd "${pkgname}-${pkgver}"
  export LIBGIT2_SYS_USE_PKG_CONFIG=1
  cargo test --release --locked
}

package() {
  cd "${pkgname}-${pkgver}"
  install -Dm 755 target/release/gitui -t "${pkgdir}/usr/bin/"
  install -Dm 644 {KEY_CONFIG,README,THEMES}.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.md -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
