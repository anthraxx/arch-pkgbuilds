# Maintainer: Christian Rebischke <chris.rebischke@archlinux.org>
# Maintainer: Daurnimator <daurnimator@archlinux.org>

pkgname=kustomize
pkgver=4.4.1
pkgrel=1
pkgdesc="Template-free customization of Kubernetes YAML manifests"
arch=('x86_64')
url="https://github.com/kubernetes-sigs/kustomize"
license=('Apache')
makedepends=('go')
depends=('glibc')
optdepends=('helm')
source=("https://github.com/kubernetes-sigs/kustomize/archive/kustomize/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('ca17d501f775ca04420c59a863e79c27c1b4e2003659bab602dd5a0c5ad6f7f0bec1ad78176419d32db67d5ab74c7653af7d139dc87cd163fbbda09545c7641a')
b2sums=('8dd9467dc1eb847e69a0ba48cb51467ba84c7a1c6148010d1a87aaf14bf196264ebc378ce2c7e3d340502cd7cd9b9a7f2665af7040058d837b3a043f21da9cae')

build(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  go build \
    -trimpath \
    -buildmode=pie \
    -mod=readonly \
    -modcacherw \
    -ldflags " \
      -X sigs.k8s.io/kustomize/api/provenance.version=${pkgver} \
      -X sigs.k8s.io/kustomize/api/provenance.buildDate=$(date -u +'%Y-%m-%dT%H:%M:%SZ' --date=@${SOURCE_DATE_EPOCH}) \
      -extldflags ${LDFLAGS}" \
    .
}

check(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  go test ./...
}

package(){
  cd "kustomize-kustomize-v${pkgver}/kustomize"

  # install completions
  mkdir -p "${pkgdir}/usr/share/bash-completion/completions"
  ./kustomize completion bash >  "${pkgdir}/usr/share/bash-completion/completions/kustomize"
  mkdir -p "${pkgdir}/usr/share/zsh/site-functions/"
  ./kustomize completion zsh >  "${pkgdir}/usr/share/zsh/site-functions/_kustomize"
  mkdir -p "${pkgdir}/usr/share/fish/vendor_completions.d"
  ./kustomize completion fish >  "${pkgdir}/usr/share/fish/vendor_completions.d/kustomize.fish"

  # install binary
  install -D kustomize "${pkgdir}/usr/bin/kustomize"
}

# vim: ts=2 sw=2 et:
