# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jason ryan <jasonwryan@gmail.com>

pkgbase=profanity
pkgname=('profanity' 'profanity-gtk')
pkgver=0.5.1
pkgrel=2
epoch=1
pkgdesc='Console based XMPP client'
url='http://www.profanity.im'
arch=('x86_64')
license=('GPL3')
makedepends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify' 'gpgme' 'python' 'gtk2')
checkdepends=('cmocka')
source=(${pkgname}-${pkgver}.tar.gz::http://www.profanity.im/${pkgname}-${pkgver}.tar.gz)
sha512sums=('fcafa7e1dcf0948d7fa30ba33665e1ec0e8bb9e0246df2d1cf3fe5d7f2bd25b07fa2cb0fd383ee23fc720850ff295159537567f175c21f1c93f50da0099e209d')

prepare() {
  cp -a ${pkgname}-${pkgver}{,-gtk}
}

build() {
  msg "Building non-gtk variant..."
  (cd ${pkgname}-${pkgver}
    ./configure \
      --prefix=/usr \
      --disable-icons \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-pgp
    make
  )
  msg "Building gtk variant..."
  (cd ${pkgname}-${pkgver}-gtk
    ./configure \
      --prefix=/usr \
      --enable-icons \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-pgp
    make
  )
}

package_profanity() {
  depends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify' 'gpgme' 'python')
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

package_profanity-gtk() {
  pkgdesc='Console based XMPP client (gtk support)'
  depends=('curl' 'ncurses' 'expat' 'glib2' 'libotr' 'libmesode' 'libnotify' 'gpgme' 'python' 'gtk2')
  provides=('profanity')
  conflicts=('profanity')
  cd ${pkgbase}-${pkgver}-gtk
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
