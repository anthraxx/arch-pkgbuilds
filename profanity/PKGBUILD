# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bart≈Çomiej Piotrowski <bpiotrowski@archlinux.org>
# Contributor: jason ryan <jasonwryan@gmail.com>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>

pkgbase=profanity
pkgname=('profanity' 'profanity-gtk')
pkgver=0.11.1
pkgrel=4
epoch=1
pkgdesc='Console based XMPP client'
url='http://www.profanity.im'
arch=('x86_64')
license=('GPL3')
_clidepends=(
  'libcurl.so' 'libncursesw.so' 'expat' 'glib2' 'libotr'
  'libmesode.so' 'libnotify.so' 'libcrypt.so'
  'libgpgme.so' 'python' 'libgcrypt' 'libsignal-protocol-c'
  'glibc' 'libreadline.so' 'libassuan' 'libgpg-error'
  )
_gtkdepends=(
  'libgtk-3.so' 'libxss' 'libx11' 'libpango-1.0.so'
  'gdk-pixbuf2' 'libatk-1.0.so' 'cairo'
  )
makedepends=(
  ${_clidepends[@]} ${_gtkdepends[@]} 'autoconf-archive'
  )
checkdepends=('cmocka')
source=(https://github.com/profanity-im/profanity/archive/${pkgver}/${pkgbase}-${pkgver}.tar.gz
        https://github.com/profanity-im/profanity/commit/fd9ccec8dc604902bbb1d444dba4223ccee0a092.patch
        https://github.com/profanity-im/profanity/commit/e5b6258c997d4faf36e2ffb8a47b386c5629b4eb.patch
        https://github.com/profanity-im/profanity/commit/242696f09a49c8446ba6aef8bdad65fb58a77715.patch)
sha256sums=('6a8b0b5a478c3a2b17b8c2eb572a3173a70b4c3740c61dd638a2d04a45147f9f'
            '6c59ae12318cd358302310ff340d9150c4ba5b36f64e0d0ed244668dfada9572'
            '07a0dd0f77d3e6f955343c42fe3667af46b76448522dc8239acff64853b268c9'
            'fc07aceacbc6ef344b9d3a4eeb6596dcbd1d1fb85af469e4e3607add1c5b2727')
sha512sums=('560116b85516f883711f54f2229619b49179666c6a5459df0dec311cc5ab6919bf648f14888b09f5c4f13a2d834f3c598967fe84ecf1169bc197274e8b5aaa93'
            '59db1462af00287adb9a6cb1014ecb13e1d34b6bf5fd891a3251f1d038d55466b3f272e7d40edb4578e102353e31c4c1c2869d365479b624401c042dd97ecdf5'
            'cb89582a31eff93f765c579d6eb68229c4fdca8bd6e4779cfbdc2be14917fad7e165ada16fbc6459546bdb6e4720e198755a0ddfe4e5298230d1f58d562c3ecd'
            '9f57d8fc24a0d6e4b03776029c075ce8155314e0e152e3dad2ecd7643bac581eb3fefe88566114b336d8e4e6673fcf78ddef98ad6856bedf125e8eff4e6e460c')
b2sums=('07aac723f080b482295a2f1f12c7ab0dfa2112d7690bf4c05412497d14dc09dafbb0933650c5240aa465fadbc5a13d48384a7679809925cc47e8b01685cc7a81'
        '904f1af29a8a5ec80b130c26859ca9c601037d5e80131f967c30b1004e87d7c104ac5b3183c85b05e8a3f630b3492ce4c839e1d6c1500b8f0aa6c2e6e336fa39'
        '410a04a4a44dcd0709e9a20e4213d12d170b7c0a26a0415fc6e11da1b4119229701c9f5e4cb9e43d982f0108ac116349ff34c5696bc4a7f9c1b077923c9d155f'
        'b406af51064c382b659ed33ba41eae96897e6845badaa21d26867b6475987a8f0663258428fe03d85d9ecfccf56a595228eee3f6ae03ec399d94e1ff45a6b5d6')

prepare() {
  cd ${pkgname}-${pkgver}

  # fix -Werror=format-security issues
  # can be removed with first release >0.11.1
  patch -Np1 -i "${srcdir}"/fd9ccec8dc604902bbb1d444dba4223ccee0a092.patch
  patch -Np1 -i "${srcdir}"/e5b6258c997d4faf36e2ffb8a47b386c5629b4eb.patch
  patch -Np1 -i "${srcdir}"/242696f09a49c8446ba6aef8bdad65fb58a77715.patch

  mkdir -p m4
  autoreconf -fiv
  cp -a "${srcdir}"/${pkgname}-${pkgver}{,-gtk}
}

build() {
  echo "Building non-gtk variant..."
  (cd ${pkgname}-${pkgver}
    ./configure \
      --prefix=/usr \
      --disable-icons-and-clipboard \
      --without-xscreensaver \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    make
  )
  echo "Building gtk variant..."
  (cd ${pkgname}-${pkgver}-gtk
    ./configure \
      --prefix=/usr \
      --enable-icons-and-clipboard \
      --with-xscreensaver \
      --enable-notifications \
      --enable-python-plugins \
      --enable-c-plugins \
      --enable-plugins \
      --enable-otr \
      --enable-omemo \
      --enable-pgp
    make
  )
}

package_profanity() {
  depends=(
    ${_clidepends[@]}
  )
  cd ${pkgbase}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

package_profanity-gtk() {
  pkgdesc+=' (gtk support)'
  depends=(
    ${_clidepends[@]} ${_gtkdepends[@]}
  )
  provides=('profanity')
  conflicts=('profanity')
  cd ${pkgbase}-${pkgver}-gtk
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
