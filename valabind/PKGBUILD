# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=valabind
pkgver=1.8.0
pkgrel=3
pkgdesc='Tool to parse vala or vapi files to transform them into swig interface files, C++, NodeJS-ffi or GIR'
url='https://github.com/radare/valabind'
arch=('x86_64')
license=('GPL3')
depends=('sh' 'glibc' 'glib2' 'vala' 'swig' 'libvala-0.56.so')
makedepends=('meson')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/${pkgname}/archive/${pkgver}.tar.gz
        fix-build.diff)
sha256sums=('3eba8c36c923eda932a95b8d0c16b7b30e8cdda442252431990436519cf87cdd'
            '760cbcc4cdd3da1d64973bcbd78c8b72479c89fcdf83f8748cacce6976cb2acf')
sha512sums=('50a32b080e1f55b88ce65facd8df948e77938a74b800658cadd9874ff4f78fa3d11cee97b5fe19d69c0fc664af8ab1750bbc2a4163c89546ee2cc4e82034f7e4'
            '5673af12844b4ab81a6dbb4c921e0e16593bc50280202b04f8fa9d71c5a2ecda93b617e50a48643a3054fb8bc72104254326a93ee839319d2c2f731edd7a12d9')

prepare() {
  cd ${pkgname}-${pkgver}

  # Fix build outside source dir
  patch -Np1 -i ../fix-build.diff
}

build() {
  arch-meson ${pkgname}-${pkgver} build
  meson compile -C build
}

package() {
  meson install -C build --destdir "${pkgdir}"
}

# vim:set ts=2 sw=2 et:
