# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=valabind
pkgver=1.8.0
pkgrel=2
pkgdesc='Tool to parse vala or vapi files to transform them into swig interface files, C++, NodeJS-ffi or GIR'
url='https://github.com/radare/valabind'
arch=('x86_64')
license=('GPL3')
depends=('sh' 'glibc' 'glib2' 'vala' 'swig' 'libvala-0.54.so')
makedepends=('meson')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/radare/${pkgname}/archive/${pkgver}.tar.gz)
sha256sums=('3eba8c36c923eda932a95b8d0c16b7b30e8cdda442252431990436519cf87cdd')
sha512sums=('50a32b080e1f55b88ce65facd8df948e77938a74b800658cadd9874ff4f78fa3d11cee97b5fe19d69c0fc664af8ab1750bbc2a4163c89546ee2cc4e82034f7e4')

build() {
  cd ${pkgname}-${pkgver}
  arch-meson . build
  meson compile -C build
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" meson install -C build
}

# vim: ts=2 sw=2 et:
