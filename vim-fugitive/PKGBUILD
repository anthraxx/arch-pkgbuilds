# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: M Rawash <mrawash@gmail.com>
# Contributor: János Illés <ijanos@gmail.com>

pkgname=vim-fugitive
pkgver=3.6
pkgrel=1
pkgdesc='Git wrapper so awesome, it should be illegal'
url='https://github.com/tpope/vim-fugitive'
arch=('any')
license=('custom:vim')
depends=('vim' 'git')
groups=('vim-plugins')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        license.txt)
sha512sums=('800888cf75141d2e524682014a19e1698d2e289a53d9247792991115538947197c3e272ba355328c7ac2c1566b1391674580aa244003c566ac050d2b7262446e'
            'a50e91b1896b0d952008ba2f641a87af2d1a01e4f280f6c914edcd51ae5d1586d5ade71c3609866b501569007bcb7f2494f08280afec170884b90fab36332fac')
b2sums=('51429ee113b330317f70859f98fb41b675feb85ad7dfd02049634ff9c1aeb41170e7cf45db6928b9b8b9dc410879e39f57e003438e95347c5ca6b0602979de69'
        '63a85fc6710dc62cf9d982eaf8fa048ccc81754e9c67c6a071ae9608c7ba90f07d744733f377e08078612dcc9a0e33590c96f4a4a1f81cdba72f86bddf34e324')

package() {
  cd ${pkgname}-${pkgver}
  local _installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -d "${_installpath}"
  cp -r -t "${_installpath}" autoload doc plugin ftdetect ftplugin syntax
  install -Dm 644 ../license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
