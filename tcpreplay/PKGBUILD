# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: SÃ©bastien Duquette <ekse.0x@gmail.com>
# Contributor: Dawid Wrobel <cromo@klej.net>

pkgname=tcpreplay
pkgver=4.4.1
pkgrel=1
pkgdesc='Gives the ability to replay previously captured traffic in a libpcap format'
url='https://tcpreplay.appneta.com'
arch=('x86_64')
license=('GPL3')
depends=('libpcap' 'libdnet' 'autogen')
makedepends=('tcpdump')
optdepends=('tcpdump: decoding of packets support')
options=('!emptydirs')
source=(https://github.com/appneta/tcpreplay/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha512sums=('e73bf9e7f72b72c91d22a26558b2eab3cb0d5afb8f89bb91cc8165fa5dcb536987ca8bce20cba601dbfd545ad236d47febb5b372aa2dd6b289069cb12afad393'
            'SKIP')
b2sums=('e2421d3187cd65b585586a7f229d412f430b63a2418b4ede8790d6144462aa13eb41e299eef1ccdf8d08d462ff4c4854fed82ddc8302cb713067a5a59fe54369'
        'SKIP')
validpgpkeys=('84E4FA215C934A7D97DC76D5E9E2149793BDE17E') # Fred Klassen <tcpreplay@appneta.com>

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-local-libopts \
    --enable-dynamic-link \
    --enable-shared \
    --with-libdnet \
    --with-tcpdump=/usr/bin/tcpdump \
    ac_cv_header_net_bpf_h=n
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make -j1 -C test tcpprep
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
