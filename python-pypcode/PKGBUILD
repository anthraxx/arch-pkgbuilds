# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Letu Ren <fantasquex at gmail dot com>

_pyname=pypcode
pkgname=python-${_pyname}
pkgver=1.0.7
pkgrel=1
pkgdesc="Python bindings to Ghidra's SLEIGH library for disassembly and lifting to p-code IR"
url='https://github.com/angr/pypcode'
license=('custom')
arch=('x86_64')
depends=('python' 'python-cffi')
makedepends=('cmake' 'python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        pypcode-memory-unique-ptr.patch::https://patch-diff.githubusercontent.com/raw/angr/pypcode/pull/31.patch)
sha512sums=('ccb7312d42faa2450fbdb961146507396409c274d2894506acfa14dcae5b97869fad230f1931f1f342e8dccf138b9edc38802bfec1c8b09b959441f28fe4f73f'
            '9848631798b7c2a0a911ed11e67cad1a7134cc91f5faa95cc05355d10fa5135300c6b9b035760c1ee9d2c89a6053a4cdc96519e129c17f8079f7b1f190d07462')
b2sums=('fc51208d3cef60afbbf2d5f8d86f02a42c2de59f8e4a55cc9a1dd0e2204c054fe8bc5ee97826399def871a8520bac3b64073c359afa3912165515fe5e50a0bcc'
        '8d56e67e5899c0294a96138ee13a72c6a280b7b449910f62d29ee2904cfd45daa2093f1990a0cda643cc0ba2dd516e7dde0af22d4633364d60387de6c10637cb')

prepare() {
  cd ${_pyname}-${pkgver}
  patch -Np1 < ../pypcode-memory-unique-ptr.patch
}

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
