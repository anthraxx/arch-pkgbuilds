# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: henning mueller <mail@nning.io>
# Contributor: Ariel Popper <a@arielp.com>
# Contributor: mortzu <me@mortzu.de>

pkgname=thc-ipv6
pkgver=3.6
pkgrel=2
pkgdesc='Complete tool set to attack the inherent protocol weaknesses of IPv6 and ICMP6'
url='https://github.com/vanhauser-thc/thc-ipv6'
arch=('x86_64')
license=('AGPL3')
depends=('libpcap' 'openssl' 'libnetfilter_queue')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/vanhauser-thc/thc-ipv6/archive/v${pkgver}.tar.gz)
sha512sums=('3ca25b85a6c31450490dc46ec999fff86303b7dfd28edefb8fd2319b794e43d36ed3c289d5ca775ba031055e83174e9927c25f74eed2b4dcfc63d8e04446d0ab')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|CFLAGS=|CFLAGS+=|g' -i Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 README CHANGES HOWTO-INJECT -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
