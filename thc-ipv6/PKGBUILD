# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: henning mueller <mail@nning.io>
# Contributor: Ariel Popper <a@arielp.com>
# Contributor: mortzu <me@mortzu.de>

pkgname=thc-ipv6
pkgver=3.8
pkgrel=2
pkgdesc='Complete tool set to attack the inherent protocol weaknesses of IPv6 and ICMP6'
url='https://github.com/vanhauser-thc/thc-ipv6'
arch=('x86_64')
license=('AGPL3')
depends=('glibc' 'libpcap' 'libpcap.so' 'openssl' 'libnetfilter_queue' 'perl' 'perl-socket6')
source=(https://github.com/vanhauser-thc/thc-ipv6/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('e3d8c80e1383e4825a3db717192446754d01e8e3098deb3075df0d60f79f014cefbd62a16c04379539e42c74513aa0f8bb97fc59221e100563aa7c6398a8b893')
b2sums=('a2b57e13a47aacf1205b87c6f30cbea2468f47aad7232b92190995bf7ffbb64904d4d0cd187657c12584cd41e66cb0e4db3b6d826d41287aa020398de8ca7f91')

prepare() {
  cd ${pkgname}-${pkgver}
  sed 's|CFLAGS=|CFLAGS+=|g' -i Makefile
}

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 README CHANGES HOWTO-INJECT -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
