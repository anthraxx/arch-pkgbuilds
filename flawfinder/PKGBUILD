# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Python Shell <pythonshell@yeah.net>
# Contributor: paraxor <paraxor5344@gmail.com>
# Contributor: Pranay Kanwar <pranay.kanwar@gmail.com>

pkgname=flawfinder
pkgver=2.0.18
pkgrel=1
pkgdesc='Searches through source code for potential security flaws'
url='https://dwheeler.com/flawfinder/'
arch=('any')
license=('GPL2')
depends=('python')
makedepends=('python-setuptools')
source=(https://dwheeler.com/flawfinder/flawfinder-${pkgver}.tar.gz)
sha512sums=('630be03190f1a8900c001143a9cbb98d2bbd4cc1134097070b39b34a51ff8ee98ff060f082dd7fb5c6773ce1e751926a1b95faac6c307facdba445d919547940')
b2sums=('8ddc04d7cef5946d574db499c9d3bd61f9bf8c556d58d649b50544d8c70e76c7dab15391464f5dac3dc229602a6988ddbfaa194dab579cc1b56112af519d6102')

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" prefix=/usr install
  install -Dm 644 README.md ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
