# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=gajim
pkgver=1.3.3
pkgrel=2
pkgdesc='Full featured and easy to use XMPP (Jabber) client'
url='https://gajim.org/'
arch=('any')
license=('GPL3')
depends=('gtk3' 'python-cairo' 'python-gobject' 'python-keyring' 'python-nbxmpp'
         'python-pyasn1' 'python-pyopenssl' 'python-precis_i18n' 'python-css-parser'
         'python-distro' 'hicolor-icon-theme')
optdepends=('python-dbus: for gajim-remote and zeroconf support'
            'avahi: serverless chatting with autodetected clients in a local network'
            'farstream: start audio and video chat'
            'gst-plugins-good: for video/voice support'
            'gst-plugins-bad: for video/voice support'
            'gst-plugins-ugly: for video/voice support'
            'gst-libav: for video/voice support'
            'gst-python: for video/voice support'
            'gspell: for spell checking support'
            'geoclue: share current location'
            'python-keyring: store passwords encrypted in a keyring'
            'gupnp-igd: request your router to forward port for file transfer'
            'libxss: measure idle time, in order to set auto status'
            'python-gnupg: encrypting chat messages with OpenPGP'
            'python-pillow: support of WebP avatars'
            'python-axolotl: OMEMO support'
            'python-qrcode: generate QR codes for OMEMO keys'
            'gsound: Notification Sounds')
source=(https://www.gajim.org/downloads/${pkgver%.*}/gajim-${pkgver}.tar.gz)
sha512sums=('b1b6ed1eeb9ce01c26240a0a0c1ff3991ac305787e2ed49ecf7ed9cc75789da2d0ec1c3a1fda6dc67fe440fa7aa143af9be1a837acfc94255d7027f63f55ced2')
b2sums=('27c4163a5762e8fa72524f91d846439b5bd54aff8ec0c4f8ffe4061295540d986b71fc333a2d65110e87821efc5a96ce0dc8a519236c5d71df9a722c65851296')

build() {
  cd ${pkgname}-${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
}

# vim: ts=2 sw=2 et:
