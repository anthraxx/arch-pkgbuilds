# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Benjamin Denhartog <ben@sudoforge.com>
# Contributor: Andreas 'Segaja' Schleifer <archlinux at segaja dot de>

pkgname=terragrunt
pkgver=0.36.2
pkgrel=1
pkgdesc='Thin wrapper for Terraform that provides extra tools for working with multiple Terraform modules'
url='https://github.com/gruntwork-io/terragrunt'
arch=('x86_64')
makedepends=('git' 'go')
depends=('glibc' 'terraform')
license=('MIT')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('3ee1942cc6fd1408885ac0ff3d0106207a8cc846062b0d09e981ef6a293c2c5d')
b2sums=('51e96ac5fe58593df6e37479570be25cf1c93e76464e5a874e09816dda2c3ef77bf9bb2747672e22b607a91fd5c4d3c2da5ca293477331c8e0c9378f184b1b72')
options=('!lto')

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  go build -v \
    -ldflags "-linkmode=external -extldflags '${LDFLAGS}' -X main.VERSION=${pkgver}" \
    .
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 ${pkgname} -t "${pkgdir}/usr/bin"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim:set ts=2 sw=2 et:
