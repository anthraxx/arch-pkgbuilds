# Maintainer: Eric BÃ©langer <eric@archlinux.org>

pkgname=traceroute
pkgver=2.1.0
pkgrel=6
pkgdesc="Tracks the route taken by packets over an IP network"
arch=('x86_64')
url="http://traceroute.sourceforge.net/"
license=('GPL2')
depends=('glibc')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz
        reproducible-build.patch)
sha256sums=('3669d22a34d3f38ed50caba18cd525ba55c5c00d5465f2d20d7472e5d81603b6'
            '7da5bb24044f6f4b7289ba7cab6bebed01f1ddbf664101405761ca160b62c9db')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../reproducible-build.patch
}

build() {
  cd ${pkgname}-${pkgver}
  make CFLAGS="$CFLAGS" CPPFLAGS="$CPPFLAGS" LDFLAGS="$LDFLAGS"
}

package() {
  cd ${pkgname}-${pkgver}
  make prefix=/usr DESTDIR="${pkgdir}" install
}
