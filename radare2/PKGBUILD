# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=5.4.0
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'libcapstone.so' 'openssl' 'libzip' 'zlib'
         'lz4' 'xxhash' 'libxxhash.so' 'file' 'libuv' 'tree-sitter'
         'libtree-sitter.so' 'libzip.so' 'libmagic.so')
makedepends=('meson')
optdepends=('r2ghidra: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/radare/radare2/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('b77fef7eda7951b2a880e3131768280dc662338f25a44d4aad5990d01fa852250ed0395fa0639bf1cdb0a6d876ad74da3da2f59362231c41f56a552709ce7e09')
b2sums=('ab8bb934bd13a3e46da51b96539b82156a68cec305e9cf9c942fbf111776c3198d397ff5876286f9fa5ebf37ad3769a71e45dcbe94af55531f16456a8abc161b')

build() {
  cd ${pkgname}-${pkgver}
  touch libr/config.mk
  arch-meson build \
    -D use_sys_capstone=true \
    -D use_capstone_version=v4 \
    -D use_sys_magic=true \
    -D use_sys_zip=true \
    -D use_sys_zlib=true \
    -D use_sys_lz4=true \
    -D use_sys_xxhash=true \
    -D use_sys_openssl=true \
    -D use_sys_tree_sitter=true \
    -D use_libuv=true \
    -D use_webui=true \
    -D tree-sitter-sync=true
  ninja -C build
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  cp -r doc/* "${pkgdir}/usr/share/doc/radare2"
  ln -s /usr/share/man/man1/radare2.1.gz "${pkgdir}/usr/share/man/man1/r2.1.gz"
}

# vim: ts=2 sw=2 et:
