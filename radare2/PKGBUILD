# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Guillermo Ramos <0xwille at gmail dot com>

pkgname=radare2
pkgver=5.6.6
pkgrel=3
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://radare.org'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=('sh' 'capstone' 'libcapstone.so' 'openssl' 'libzip' 'zlib'
         'lz4' 'xxhash' 'libxxhash.so' 'file' 'libuv' 'tree-sitter'
         'libtree-sitter.so' 'libzip.so' 'libmagic.so')
makedepends=('meson')
optdepends=('r2ghidra: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/radare/radare2/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        radare2-5.6.6-meson-token-fix.patch::https://github.com/radareorg/radare2/commit/b914887fcd382805759ed927ffb879d7b1cad7dc.patch)
sha512sums=('0969d923b0c65c937dadb3cef655a3931b3daf1fc48bf1f6e3dd1afd1df54f65da3baf4bd401eb1677f88566774ed115c271cdc923dc4ef800ef867f4849efb0'
            '1ec97c3875916f8638533b80db3d555208a5ccb8910b2f472b9269880eabc96eb387c052cb9e30d489b6151b839a2ac4a837cbbe72f5ee6273c5228766687dfb')
b2sums=('a0a15e9eaec4466d0855cd6973e2b166fadb1aeb1a9fe91556fe69ca8e4893dbc2313033ce1a4dfde123e40b44e20524e006a67f7b0a5581ceb035603e587268'
        'cbef51aa39221d7f996a2548d40ecbe4f2cbecf76074e3a37567d84314c987944f35dd579564e8c031b7098f9ca405b9c9a4ef5a5c1ab29f13e90d2aa5360487')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -Np1 < ../radare2-${pkgver}-meson-token-fix.patch
}

build() {

# XXX: tree-sitter was removed in this release
# https://github.com/radareorg/radare2/commit/cbc949c039d0aea8566657f7a177d7b2f95ee9b4
#   -D use_sys_tree_sitter=true \
#   -D tree-sitter-sync=true

  cd ${pkgname}-${pkgver}
  touch libr/config.mk
  arch-meson build \
    -D use_sys_capstone=true \
    -D use_capstone_version=v4 \
    -D use_sys_magic=true \
    -D use_sys_zip=true \
    -D use_sys_zlib=true \
    -D use_sys_lz4=true \
    -D use_sys_xxhash=true \
    -D use_sys_openssl=true \
    -D use_libuv=true \
    -D use_webui=true \
    -D want_threads=false

  ninja -C build
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  cp -r doc/* "${pkgdir}/usr/share/doc/radare2"
  ln -s /usr/share/man/man1/radare2.1.gz "${pkgdir}/usr/share/man/man1/r2.1.gz"
}

# vim: ts=2 sw=2 et:
