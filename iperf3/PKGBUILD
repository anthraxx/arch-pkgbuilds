# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Timothy Redaelli <timothy.redaelli@gmail.com>
# Contributor: Jim Ferguson <ferguson@ncsa.uiuc.edu>
# Contributor: Xi0N <jonandermonleon@gmail.com>
# Contributor: goll <adrian.goll+aur[at]gmail>

pkgname=iperf3
pkgver=3.11
pkgrel=1
pkgdesc='TCP, UDP, and SCTP network bandwidth measurement tool'
url='https://github.com/esnet/iperf'
arch=('x86_64')
license=('BSD')
depends=('glibc' 'openssl')
provides=('libiperf.so')
source=(https://downloads.es.net/pub/iperf/iperf-${pkgver}.tar.gz)
sha512sums=('99dc0473a6b00f30190c7e82a259836db666c2ba95dad17af4450c49b0d09b005f3dcd9bd7b6703b62f92f67cb602f206da9d3e815eeba06b3a4751164d91c8f')
b2sums=('82a7edf0a40a7addd10ae847a685a5d9d39c75bd428ddeb35a1e36d6b270954f2250638a854fa1f7c470884ebc843c20e833c804e9bd20e0f909421301dd97fc')

build() {
  cd iperf-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd iperf-${pkgver}
  make check
}

package() {
  cd iperf-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 examples/*{.am,.in,.c} -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim: ts=2 sw=2 et:
