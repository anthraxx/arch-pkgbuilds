# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Santiago Torres-Arias <santiago[at]archlinux[dot]org>
# Contributor: spider-mario <spidermario@free.fr>

pkgname=afl
pkgver=2.57b
pkgrel=2
pkgdesc='Security-oriented fuzzer using compile-time instrumentation and genetic algorithms'
url='http://lcamtuf.coredump.cx/afl/'
arch=('x86_64')
license=('Apache')
depends=('glibc' 'bash')
optdepends=('gcc: gcc instrumentation support'
            'clang: clang instrumentation support'
            'llvm: experimental clang-fast instrumentation support'
            'gnuplot: graph plotting support')
makedepends=('clang' 'llvm' 'gcc' 'wget' 'python' 'lib32-glibc')
provides=('american-fuzzy-lop')
replaces=('american-fuzzy-lop')
options=('!emptydirs' '!strip')
source=(https://github.com/google/AFL/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha256sums=('6f05a6515c07abe49f6f292bd13c96004cc1e016bda0c3cc9c2769dd43f163ee')
sha512sums=('5d12b96229e966aeb11deb47cd8eb36cde2fa32656eb187db02c7240909d6acda36be59b2baf7e42cddd228d25ec94c8bffc3fa27f7f4b5e1e462b62929ce75d')
b2sums=('69029d59451e468528386154e8938a1a7303765e039f98aa05e36f07314398f60ff2578f765142e1f1ff653e5ab93ea53a2efb98abe275046ebacdd3ca8363b6')

build() {
  cd AFL-${pkgver}
  make PREFIX=/usr
  make -C llvm_mode PREFIX=/usr
}

package() {
  cd AFL-${pkgver}
  make PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 llvm_mode/README.llvm -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
