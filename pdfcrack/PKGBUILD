# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Lukas Jirkovsky <l.jirkovsky@gmail.com>

pkgname=pdfcrack
pkgver=0.20
pkgrel=1
pkgdesc='Password recovery tool for PDF-files'
url='https://pdfcrack.sourceforge.net/'
arch=('x86_64')
license=('GPL2')
depends=('glibc')
source=(https://downloads.sourceforge.net/pdfcrack/pdfcrack-${pkgver}.tar.gz{,.asc})
sha512sums=('50298aeec8a50b929d1440ea644bacb6afad638552948112b3fb2744124188e0cb52f67a804d76b09f747040aea56eadbab88e90481be8fce8e8147ed4c00188'
            'SKIP')
b2sums=('31e0d1324254d660e5cb6cc57e401bf4fe7574630dcd902d73621a5e62c32e13f9cc6a9812c3ab904160727b27c08869f8f9f5b93657db5941a05fa10b49a4ce'
        'SKIP')
validpgpkeys=(
  '987AEFC56E70D68440C0D45149241DE2BA57F699' # Henning Norén <henning.noren@gmail.com>
  'AC49CA5B95335D6DAF85BAC563EFAA4606037EB3' # Henning Norén <henning.noren@gmail.com>
  '4A524F4C6BD20FF789116E0443001F2E87BE0FD0' # Henning Norén <henning.noren@gmail.com>
)

build() {
  cd ${pkgname}-${pkgver}
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm755 pdfcrack -t "${pkgdir}/usr/bin"
  install -Dm644 README changelog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm644 pdfcrack.1 -t "${pkgdir}/usr/share/man/man1"
}

# vim: ts=2 sw=2 et:
