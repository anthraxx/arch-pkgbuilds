# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jens Adam <jra@byte.cx>
# Contributor: Arch3y <arch3y[at]archstrike[dot]org>

pkgname=zmap
pkgver=2.1.1
pkgrel=2
pkgdesc='Fast network scanner designed for Internet-wide network surveys'
url='https://zmap.io/'
arch=('i686' 'x86_64')
license=('Apache')
depends=('gmp' 'hiredis' 'json-c' 'libpcap')
makedepends=('byacc' 'cmake' 'gengetopt')
backup=('etc/zmap/blacklist.conf' 'etc/zmap/zmap.conf')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/zmap/zmap/archive/v${pkgver}.tar.gz)
sha512sums=('9d62dceff77fa20ff5945516b0766340366175947130bb844f19c11dac7676e658bcdedd6be29ffc945706aa3cdc0e4ac207bd127b618592769a4d3e75892857')

build() {
  cd ${pkgname}-${pkgver}
  cmake -DWITH_JSON=ON -DWITH_REDIS=ON
  make
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 src/zmap "${pkgdir}/usr/bin/zmap"
  install -Dm 755 src/ztee "${pkgdir}/usr/bin/ztee"
  install -Dm 755 src/zblacklist "${pkgdir}/usr/bin/zblacklist"
  install -Dm 644 src/zmap.1 "${pkgdir}/usr/share/man/man1/zmap.1"
  install -Dm 644 src/ztee.1 "${pkgdir}/usr/share/man/man1/ztee.1"
  install -Dm 644 src/zblacklist.1 "${pkgdir}/usr/share/man/man1/zblacklist.1"
  install -Dm 644 conf/* -t "${pkgdir}/etc/zmap"
  for F in AUTHORS CHANGELOG.md INSTALL.md README.md; do
    install -Dm 644 "${F}" "${pkgdir}/usr/share/doc/zmap/${F}"
  done
  cp -a examples "${pkgdir}/usr/share/doc/zmap"
}

# vim: ts=2 sw=2 et:
