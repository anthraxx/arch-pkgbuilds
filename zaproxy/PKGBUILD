# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Joris Steyn <jorissteyn@gmail.com>
# Contributor: fnord0 <fnord0 AT riseup DOT net>

pkgname=zaproxy
pkgver=2.11.0
pkgrel=1
pkgdesc='Integrated penetration testing tool for finding vulnerabilities in web applications'
url='https://www.owasp.org/index.php/ZAP'
arch=('any')
license=('Apache')
depends=('bash' 'java-runtime' 'ttf-font')
source=(https://github.com/zaproxy/zaproxy/releases/download/v${pkgver}/ZAP_${pkgver}_Linux.tar.gz)
sha512sums=('263ba41dd3afa3dfa1b057d5f310f177beea11493186947dd1eeb03b52f6f05ab78f2668e71e26aac06ce6f3f85450805fc583da509555f2fac668ab516ea58c')
b2sums=('0f490d14bf3aed3bfa801aecbc9e9bf54726566cbcb847e1dbdd8759b3cad0d21167071d6ec2626d44f9c4c26b5dcee6c3e65f5383d4f241da0bd42c1fcc6bbb')

prepare() {
  cd ZAP_${pkgver}
  cat > ${pkgname} <<EOF
#!/bin/sh
cd /usr/share/zaproxy
./zap.sh
cd -
EOF
}

package() {
  cd ZAP_${pkgver}
  install -d "${pkgdir}/usr/share/${pkgname}"
  cp -pR . "${pkgdir}/usr/share/${pkgname}"
  install -Dm 755 ${pkgname} "${pkgdir}/usr/bin/${pkgname}"
}

# vim: ts=2 sw=2 et:
