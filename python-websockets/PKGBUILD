# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sherlock Holo <sherlockya@gmail.com>
# Contributor: user6553591 <Message on Reddit>

pkgname=python-websockets
pkgver=10.1
pkgrel=2
pkgdesc='Python implementation of the WebSocket Protocol (RFC 6455)'
url='https://github.com/aaugustin/websockets'
arch=('x86_64')
license=('BSD')
depends=('python')
makedepends=('python-setuptools' 'python-sphinx' 'python-sphinx-copybutton'
             'python-sphinx-furo' 'python-sphinx-inline-tabs'
             'python-sphinxcontrib-spelling' 'python-sphinxcontrib-trio'
             'python-sphinxext-opengraph')
source=(https://github.com/aaugustin/websockets/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('c284ec92bc133c7083a72d4f364d9ace13d3eb6d7355ae46084d8097c137cdec3933cae9d5ab72d8a517af38328cea6c39877f747f5386df54a7362271cc7e7b')
b2sums=('425e7c8597f081874127ac75003aed11ebb2cf647ce9fed9e17c955e485a7a59c95d1112524d3f0d747b796b0cc41972258661c9fc9c7972f68aaebe3223b596')

build() {
  cd websockets-${pkgver}
  python setup.py build
  sphinx-build -b dirhtml docs docs/_build/html
  sphinx-build -b man docs docs/_build/man
}

check() {
  cd websockets-${pkgver}
  python setup.py test
}

package() {
  cd websockets-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  cp -rT docs/_build/html "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/websockets.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
