# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Chris Severance aur.severach AatT spamgourmet.com

_pyname=pcapy-ng
pkgname=python-pcapy
pkgver=1.0.4
pkgrel=2
pkgdesc='Extension module that interfaces with the libpcap packet capture library'
url='https://github.com/stamparm/pcapy-ng'
arch=('x86_64')
license=('Apache')
depends=('python' 'libpcap')
makedepends=('python' 'python-setuptools')
source=(https://github.com/stamparm/pcapy-ng/archive/${pkgver}/${_pyname}-${pkgver}.tar.gz
        replace-one-more-unsigned-int-len-with-Py_ssize_t.patch)
sha256sums=('2fd66fbf5915199a9b92fd23051a0404fc95eca5ad0e9b20777539b314132951'
            'cef155f680372ba4a9bbd7dcc8fb1279b60147fefe960047fd84c95e3f0e16e7')
sha512sums=('713238a91037c2b420df81cce68bc26e5993d40ba4797914798ea071b6a7fdc66530b6ab17cbd32aee4b77436e9d1bd8f54f9bda1f98f7d4363435c7e05ac190'
            '3fdba64da2ace23d3fb31938c213aeb4e0858d93bf21093f27f44fa7f9bd43a54e5e8293683f703b9a3cb4fdaa3e4ccc4d5f22fa629bf70bf2a0e183df049f80')

prepare() {
  cd ${_pyname}-${pkgver}
  # https://github.com/stamparm/pcapy-ng/pull/2
  patch -Np1 -i ../replace-one-more-unsigned-int-len-with-Py_ssize_t.patch
  sed -i '/self.assertEqual(refNone, sys.getrefcount(None))/d' tests/pcapytests.py
}

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pyname}-${pkgver}/tests
  local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  PYTHONPATH="../build/lib.linux-${CARCH}-${PYTHONVERSION}" \
    python pcapytests.py
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 pcapy.html ChangeLog -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm -r "${pkgdir}/usr/share/doc/${_pyname}"
}

# vim: ts=2 sw=2 et:
