# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=claripy
pkgname=python-${_pyname}
pkgver=9.2.14
pkgrel=1
pkgdesc='Abstraction layer for constraint solvers'
url='https://github.com/angr/claripy'
license=('custom:BSD2')
arch=('any')
depends=('python' 'python-z3-solver' 'python-cachetools' 'python-decorator'
         'python-pysmt' 'python-six')
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('0ff7f750b8f32e40e581c0975991276fbd6322dc3b32dc0002f21b90e5615e3ebc3f6a2f028c32b422494aae84b6cd12a455056c0f1014f8c48951059d774de6')
b2sums=('06a19f894ccbc414d1ade5d9fdca7b3c2e19aa15dd2110e7eec7fb9cae629d085f1b6c5a7c6edbda827171ef08a0a6a2de6b5abefc599a5ac5d226d85ee37e3b')

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=build/lib pytest
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
