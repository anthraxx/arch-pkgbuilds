# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=xwallpaper
pkgver=0.7.1
pkgrel=1
pkgdesc='Wallpaper setting utility for X'
url='https://github.com/stoeckmann/xwallpaper'
arch=('x86_64')
license=('ISC')
depends=('pixman' 'libpng' 'libxpm' 'xcb-util' 'xcb-util-image' 'libjpeg-turbo'
         'libseccomp' libseccomp.so libpixman-1.so libjpeg.so)
source=(https://github.com/stoeckmann/xwallpaper/releases/download/v${pkgver}/xwallpaper-${pkgver}.tar.xz{,.sig})
sha512sums=('706f814ee892f9d136ef20bb3c9bb4b8537e3ed7029b3b20fbebb1504426529d2fa6a45117a391419253543a48230f5786ffa3f871f85477699c08dfa3ba5c92'
            'SKIP')
b2sums=('b8327b15bc421275660cc81d14e4952b37a1310fd50eea61b772115e2c345c01cae9c4f908d9ca55427e642b10bea7277af02e1d3525db6c1d6e80a356db1b2b'
        'SKIP')
validpgpkeys=('CDB24BD3BDDCBCBBAE5CB6207DB470F35B0B8B18') # Tobias Stoeckmann <tobias@stoeckmann.org>

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --with-zshcompletiondir=/usr/share/zsh/site-functions
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
