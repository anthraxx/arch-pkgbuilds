# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=xwallpaper
pkgver=0.7.3
pkgrel=2
pkgdesc='Wallpaper setting utility for X'
url='https://github.com/stoeckmann/xwallpaper'
arch=('x86_64')
license=('ISC')
depends=('pixman' 'libpng' 'libxpm' 'xcb-util' 'xcb-util-image' 'libjpeg-turbo'
         'libseccomp' libseccomp.so libpixman-1.so libjpeg.so)
options=('debug')
source=(https://github.com/stoeckmann/xwallpaper/releases/download/v${pkgver}/xwallpaper-${pkgver}.tar.xz{,.sig})
sha512sums=('7fa9cc9ca2445afdfcb647724a6dc8bb3b52b9a6c91d237f6b443cf1fe6624c67d16d611bf4e435c5f3dcdae6cbf73bbde257a4e194bc127dc13dca1f8067c19'
            'SKIP')
b2sums=('45942dbe7e3627fbbf99ac5ab94c88ecea919e48131f268f0be10a0fba15a4feddf207c0787ac47f75bc6a5cc94f2b78d599e97950609196930a73073ae21ec1'
        'SKIP')
validpgpkeys=('CDB24BD3BDDCBCBBAE5CB6207DB470F35B0B8B18') # Tobias Stoeckmann <tobias@stoeckmann.org>

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --with-zshcompletiondir=/usr/share/zsh/site-functions
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
