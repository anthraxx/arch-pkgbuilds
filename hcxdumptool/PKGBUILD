# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=hcxdumptool
pkgver=6.2.7
pkgrel=2
pkgdesc='Small tool to capture packets from wlan devices'
url='https://github.com/ZerBea/hcxdumptool'
arch=('x86_64')
license=('MIT')
depends=('glibc' 'openssl')
source=(https://github.com/ZerBea/hcxdumptool/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz
        https://github.com/ZerBea/hcxdumptool/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.gz.asc)
sha512sums=('52aa6031f66499089afc8b72984086eab6d52e20d3cd20bdea1cb6d5cdb44996a3d091279eb38965fc4e16bdcf2d3b0b648ec130081f3603559059229bc6a039'
            'SKIP')
b2sums=('2b14a0b221de63db218226ceea31ebc34b4ebcb072b287e99801ca0bfe0aeddeba661888dd4b6202f01645a1664a6c51a9b4d7f5fac2295605b28d850ca940f5'
        'SKIP')
validpgpkeys=('5920CE1C567948AFD2C0A9B7375516A45DB88630') # ZeroBeat <ZeroBeat@gmx.de>

build() {
  cd ${pkgname}-${pkgver}
  make PRODUCTION=1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" PREFIX=/usr PRODUCTION=1 install
  install -Dm 644 README.md changelog -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
