# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Sam Stuewe <halosghost at archlinux dot info>

pkgname=hashcat
pkgver=6.1.1
pkgrel=1
epoch=1
pkgdesc='Multithreaded advanced password recovery utility'
url='https://hashcat.net/hashcat'
arch=('x86_64')
depends=('ocl-icd' 'xxhash')
makedepends=('opencl-headers')
provides=('libhashcat.so')
license=('MIT')
source=(https://hashcat.net/files/${pkgname}-${pkgver}.tar.gz{,.asc})
sha512sums=('788539b488ba55684d6d93ce18f3b05fa822a8cbfcdbcde9c2b54001d8927580dca253e0e9ff9a19f4278045046d732e104054fc743c39f47952ca24d2e93724'
            'SKIP')
validpgpkeys=('A70833229D040B4199CC00523C17DA8B8A16544F') # Hashcat signing key <signing@hashcat.net>

build() {
  cd ${pkgname}-${pkgver}
  rm -rf deps/{OpenCL-Headers,xxHash}
  make \
    PREFIX=/usr \
    SHARED=1 \
    USE_SYSTEM_XXHASH=1 \
    USE_SYSTEM_OPENCL=1
}

package() {
  cd ${pkgname}-${pkgver}
  make \
    DESTDIR="${pkgdir}" \
    PREFIX=/usr \
    SHARED=1 \
    USE_SYSTEM_XXHASH=1 \
    USE_SYSTEM_OPENCL=1 \
    install
  install -Dm 644 docs/license.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
