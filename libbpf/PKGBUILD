# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor:  Adrian Bacircea <adrian.bacircea@gmail.com>

pkgname=libbpf
pkgver=0.6.0
pkgrel=1
pkgdesc='Library for loading eBPF programs and reading and manipulating eBPF objects from user-space'
url='https://github.com/libbpf/libbpf'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc' 'libelf' 'linux-api-headers')
makedepends=('rsync')
provides=('libbpf.so')
source=(https://github.com/libbpf/libbpf/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('923eac64dca015939cbbd51cb0a7daf609d9873d249d6e621d73776b51c52f6087f2a957976163474d359a471250b27b44ad93179948335e949a82ed9613ed43')
b2sums=('a6076ae7465eb9c99ff1b2c63a388ef2d15c71882d7de0b5279604ad7b91ef3fce583c88710126a9384d96a810775409daadb0adaea41dd9dfc12af183fa50d7')

build() {
  cd ${pkgname}-${pkgver}
  make -C src
}

package() {
  cd ${pkgname}-${pkgver}
  make -C src DESTDIR="${pkgdir}" LIBSUBDIR=lib install install_headers
  install -Dm 644 LICENSE* -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
