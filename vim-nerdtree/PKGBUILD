# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Alexander F RÃ¸dseth <xyproto@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Kwpolska <kwpolska@gmail.com>
# Contributor: Thomas Dziedzic < gostrc at gmail >
# Contributor: Laszlo Papp <djszapi @ gmail at com>

pkgname=vim-nerdtree
pkgver=6.10.15
pkgrel=1
pkgdesc='Tree explorer plugin for navigating the filesystem'
url='https://github.com/scrooloose/nerdtree'
arch=('any')
license=('custom:WTFPL')
depends=('vim-plugin-runtime')
groups=('vim-plugins')
source=(https://github.com/scrooloose/nerdtree/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('204031072dc2d990f760f585ac222660d848c6d733a2ae60b304352db3291a09bb9f1fe70509d49fdd521ff18b57a1e3d724aae9a03434c6d0b973ad9f9e0b8a')
b2sums=('2b4bfea9b200f8f4dfcbdb12d5f7c86b186c96d002b485596f9baf2f97d585240ee502f81062d1d737fa4ec68e04b335422d0a2035497952208ae515f0bd1a67')

prepare() {
  cd nerdtree-${pkgver}
  sed 's|\[s:rootNERDTreePath,|["/usr/share/doc/vim-nerdtree",|' -i autoload/nerdtree.vim
}

package() {
  cd nerdtree-${pkgver}
  local _installpath="${pkgdir}/usr/share/vim/vimfiles"
  install -d "${_installpath}"
  cp -r -t "${_installpath}" autoload doc plugin syntax nerdtree_plugin lib
  install -Dm 644 CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENCE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.markdown CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
