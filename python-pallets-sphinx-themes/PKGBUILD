# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-pallets-sphinx-themes
_pyname=${pkgname/python-/}
pkgver=2.0.1
pkgrel=4
pkgdesc='Sphinx themes for Pallets and related projects'
url='https://github.com/pallets/pallets-sphinx-themes'
arch=('any')
license=('BSD')
depends=('python' 'python-sphinx' 'python-importlib-metadata')
makedepends=('python-setuptools')
source=(https://github.com/pallets/pallets-sphinx-themes/archive/${pkgver}/${_pyname}-${pkgver}.tar.gz
        jinja2-deprecations.patch)
sha512sums=('6e16ead8bb489336f8b8fe7e50d1e65acf950ab64c90ba1508ceb07bd598d8ddd5b960d9b31697867ddb7bffa970a08d7d53b778b5eec5e01b7c07babf2d5de6'
            '55009d31e2a0d41b8e2a3e9f69024f283935913589059069ab999ca706ff8fc914e1f7156db9c832091fb771b2c98eb54373d4c3393d439fbbcfd382fa521bec')
b2sums=('6fc745ffc5d344cd542f1b299ef5151144a13d1f75923398d42a6882d3c80aca4827123a3bd1f751dd771e882170c76f5bad99ad42d09dec2a9baf474d846365'
        '3b5550c6b0703158abe99f61a1363f233b95265c247e55360d53c83023f473f22c11b832d0fbd4164d7906b6bc50ad90b3b398f8adeed5ab92f651910f0c6b35')

prepare() {
  cd ${_pyname}-${pkgver}
  patch -Np1 -i ../jinja2-deprecations.patch
}

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
