# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-pallets-sphinx-themes
_pyname=${pkgname/python-/}
pkgver=2.0.1
pkgrel=3
pkgdesc='Sphinx themes for Pallets and related projects'
url='https://github.com/pallets/pallets-sphinx-themes'
arch=('any')
license=('BSD')
depends=('python' 'python-sphinx' 'python-importlib-metadata')
makedepends=('python-setuptools')
source=(https://github.com/pallets/pallets-sphinx-themes/archive/${pkgver}/${_pyname}-${pkgver}.tar.gz)
sha512sums=('6e16ead8bb489336f8b8fe7e50d1e65acf950ab64c90ba1508ceb07bd598d8ddd5b960d9b31697867ddb7bffa970a08d7d53b778b5eec5e01b7c07babf2d5de6')
b2sums=('6fc745ffc5d344cd542f1b299ef5151144a13d1f75923398d42a6882d3c80aca4827123a3bd1f751dd771e882170c76f5bad99ad42d09dec2a9baf474d846365')

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE.rst -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
