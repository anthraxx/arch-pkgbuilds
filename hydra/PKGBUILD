# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rudy Matela <rudy@matela.com.br>
# Contributor: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: pootzko <pootzko@gmail.com>
# Contributor: Jinx <jinxware@gmail.com>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=hydra
pkgver=9.4
pkgrel=2
pkgdesc='Very fast network logon cracker which support many different services'
url='https://github.com/vanhauser-thc/thc-hydra'
arch=('x86_64')
license=('GPL3')
depends=('postgresql-libs' 'subversion' 'libssh' 'mariadb-libs' 'libidn'
         'libfbclient' 'afpfs-ng' 'zlib' 'openssl' 'ncurses' 'pcre2'
         'freerdp' 'libmemcached' 'mongo-c-driver'
         libncursesw.so libmariadb.so libpq.so libssh.so libfreerdp2.so
         libwinpr2.so)
source=(https://github.com/vanhauser-thc/thc-hydra/archive/v${pkgver}/thc-hydra-v${pkgver}.tar.gz)
sha512sums=('de5a1e73673fdc2f193cfb1aeec337c9d8faad52b06524850f352a797a4a5ef08cba8fc51da96fed1db01d141127e58c2aa874027f51c40a94a474de778d5241')
b2sums=('2ba7f7e4a70c4f6c9dc4ec94d652ab7534cb0fb17b88412dfcf1e651d9f31af573ab9acd2d9cb698da6462f4744f53dfb8f87a437c32bebfa4f9a04f2f9136c0')

build() {
  cd thc-hydra-${pkgver}
  ./configure --fhs
  make
}

package() {
  cd thc-hydra-${pkgver}
  make DESTDIR="${pkgdir}" MANDIR=/share/man/man1 DATADIR=/share/hydra install
  install -Dm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
  rm "${pkgdir}"/usr/share/man/man1/xhydra.1
}

# vim: ts=2 sw=2 et:
