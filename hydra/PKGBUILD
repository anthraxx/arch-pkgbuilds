# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Rudy Matela <rudy@matela.com.br>
# Contributor: Bartek Piotrowski <barthalion@gmail.com>
# Contributor: Thomas S Hatch <thatch45 at gmail dot com>
# Contributor: pootzko <pootzko@gmail.com>
# Contributor: Jinx <jinxware@gmail.com>
# Contributor: Timoth√©e Ravier <tim@siosm.fr>

pkgname=hydra
pkgver=9.0
pkgrel=3
pkgdesc='Very fast network logon cracker which support many different services'
url='https://github.com/vanhauser-thc/thc-hydra'
arch=('x86_64')
license=('GPL3')
depends=('postgresql-libs' 'subversion' 'libssh' 'mariadb-libs' 'libidn'
         'libfbclient' 'afpfs-ng' 'zlib' 'openssl' 'ncurses' 'pcre'
         'freerdp' 'libmemcached' 'mongo-c-driver')
optdepends=('gtk2: graphical front-end (xhydra)')
makedepends=('gtk2')
source=(https://github.com/vanhauser-thc/thc-hydra/archive/v${pkgver}/thc-hydra-v${pkgver}.tar.gz
        hydra-9.0-apr-path-max.patch::https://github.com/vanhauser-thc/thc-hydra/commit/db9025bf86e79f1af1e1d70bdc6ea133e486c781.patch)
sha512sums=('58067bd2696e82645bb959fe5537f2876e1d8c170a15fe6a9ac65bbdc33cfb75b47fd1a969c314a6c6d91b2b6c0ce4ba087680bbd67a5a38925a2a8b5c5ad338'
            '83b1c31fb87fb3eb8d331afad88d8363336de0a80f864b4ed7f69fac033a67a01e1a0d7c0df62058082a77a40555d52f2ef62de354275b1c0403b48586062f70')
b2sums=('b4834c66fb1bebcd19151784b75d4c9220cbf7045b289a063a1cbc48a793abfa2edcb466d43234bd4c8e14074a2e2a39ccd697698392f24f39c84da54ee07165'
        'a261b08a469c2540fb67dd6c9d085ab38eb08fa22f050afbe407bc3f0d2d7c46739533b626c6e1d8ab5aa5a0bea22095658893d83918ef07c84359163de43598')


prepare() {
  cd thc-hydra-${pkgver}
  patch -Np1 < ../hydra-9.0-apr-path-max.patch
}

build() {
  cd thc-hydra-${pkgver}
  CFLAGS+=' -fcommon' # https://wiki.gentoo.org/wiki/Gcc_10_porting_notes/fno_common
  ./configure --fhs
  make
}

package() {
  cd thc-hydra-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
