# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Limao Luo <luolimao+AUR@gmail.com>
# Contributor: Wes Brown <wesbrown18@gmail.com>

pkgname=volatility3
pkgver=1.0.1
pkgrel=3
pkgdesc='Advanced memory forensics framework'
url='https://github.com/volatilityfoundation/volatility3/wiki'
arch=('any')
license=('custom')
depends=(
  'python' 'python-yara' 'python-capstone' 'python-jsonschema'
  'python-pycryptodome' 'python-pefile')
makedepends=('python-setuptools')
provides=('volatility')
replaces=('volatility')
source=(https://github.com/volatilityfoundation/volatility3/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('df8d595decc9a4f6f1d3474fa03b94f571c05f429e191fe3bfa420fab1a5b6953bc53f396370b7ea76ff2f1ff79125be860561fc1eefd7d00b4e9f60cf25814d')
b2sums=('09c68d329b25b8bd467156bf526e04a56e17a17d67e40ce4017e4307dbcbaa987dd78fe9dc5f3dc690d6e0c58542b728a96b58d73412e5776e653b2f38228739')

build() {
  cd ${pkgname}-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}-${pkgver}
  python setup.py install -O1 --prefix=/usr --root="${pkgdir}" --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
