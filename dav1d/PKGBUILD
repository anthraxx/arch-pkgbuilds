# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=dav1d
pkgver=0.3.0
pkgrel=1
pkgdesc='AV1 cross-platform decoder focused on speed and correctness'
url='https://code.videolan.org/videolan/dav1d/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('meson' 'ninja' 'nasm' 'doxygen')
provides=('libdav1d.so')
source=("https://downloads.videolan.org/videolan/${pkgname}/${pkgver}/${pkgname}-${pkgver}.tar.xz"{,.asc})
sha256sums=('7f229261595ed0940b5cb360d5f0045032ad5c9cbe0f7e71562ae6bee9bacf25'
            'SKIP')
validpgpkeys=(
              '65F7C6B4206BD057A7EB73787180713BE58D1ADC' # VideoLAN Release Signing Key 2013-2017
             )

build() {
  cd ${pkgbase}-${pkgver}
  arch-meson build
  ninja -C build
}

check() {
  cd ${pkgbase}-${pkgver}/build
  meson test
}

package() {
  cd ${pkgbase}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
