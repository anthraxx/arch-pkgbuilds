# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Massimiliano Torromeo <massimiliano.torromeo@gmail.com>

pkgname=python-ujson
_pkgname=ultrajson
pkgver=3.1.0
pkgrel=1
pkgdesc="Ultra fast JSON encoder and decoder for Python"
url="https://github.com/ultrajson/ultrajson"
arch=('x86_64')
license=('BSD')
depends=('python')
makedepends=('python-setuptools' 'python-pip')
checkdepends=('python-six' 'python-pytest')
source=(https://github.com/ultrajson/ultrajson/archive/$pkgver/$pkgname-$pkgver.tar.gz)
sha512sums=('403430cbcd09968a47c1e85169d62ed3dbb6a014b9e7f3e74c53554a37f008878bc115f4ee84c70c2ae7b4d0534a2f74bc3d6bc8104282181db498e6172a0dc2')
b2sums=('bbf1a44bffd8adb2b9237d8067686dd69355cf96cf1ebfb1fd3b7ff217db77be03690ed04b98c670619ba9408b6df00f6c1da890b471b622438604a7ccecb848')

prepare() {
  sed '/setup_requires=\["setuptools_scm"\],/d' -i ${_pkgname}-${pkgver}/setup.py
  sed "s/{version}/$pkgver/" ${_pkgname}-${pkgver}/python/version_template.h > ${_pkgname}-${pkgver}/python/version.h
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  local PYTHONVERSION="$(python -c 'import sys; print("{}.{}".format(sys.version_info.major, sys.version_info.minor))')"
  PYTHONPATH="build/lib.linux-${CARCH}-${PYTHONVERSION}" pytest
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --skip-build -O1 --root="${pkgdir}"
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
