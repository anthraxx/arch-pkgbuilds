# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Eric BÃ©langer <eric@archlinux.org>

pkgname=nasm
pkgver=2.15.02
pkgrel=1
pkgdesc='80x86 assembler designed for portability and modularity'
url='https://www.nasm.us'
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('perl-font-ttf' 'perl-sort-versions' 'fontconfig' 'adobe-source-sans-pro-fonts'
             'ttf-liberation' 'ghostscript' 'xmlto' 'asciidoc')
source=(https://www.nasm.us/pub/nasm/releasebuilds/${pkgver}/${pkgname}-${pkgver}.tar.xz)
sha512sums=('7375f970ca0c6c31fe2cb5c8c2b5a0f854eaff374b1e7bc9e09546246a84fe73a2ba6073e63d9287444cf17c9e2d342c3ff73ca961c2c0e3e2a9c3aba77114c7')
b2sums=('e325ef3b59582b1e3bd6503ec8c5b8a7abf92c98a0234451a3695ecd3102674a34d7cd260a50278b8d98f4663651de058e858e58e6dfda8ae33d1cdb8bb2fe3f')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
  make -C doc
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install install_rdf
  install -Dm 644 LICENSE -t "${pkgdir}"/usr/share/licenses/nasm
}

# vim: ts=2 sw=2 et:
