# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=precis_i18n
pkgname=python-precis_i18n
pkgver=1.0.3
pkgrel=4
pkgdesc='PRECIS framework (RFC 8264, RFC 8265, RFC 8266)'
url='https://github.com/byllyfish/precis_i18n'
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
source=(${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('2b7fe3d06969ae1eb4c8005ad2b2cdc3f8eb81d586317ca428767c9df2906d19c7a228e26ed8bb1ba70e76e36810d2e5e86cf7aebc1103f31de616a33800e67c')
b2sums=('33b737940fa09fb14190a6ce386f590acb8b89831fd4ddc6562b0b34204ed08e4cb677dc7e65c63aaf827637ed3ba4cade700769df1983e17840ae03d0e9d7f1')

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 LICENSE.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGELOG.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
