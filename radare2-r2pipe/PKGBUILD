# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=radare2-r2pipe
pkgname=(python-r2pipe)
pkgver=5.3.0
pkgrel=1
pkgdesc='Access radare2 via pipe from any programming language'
url='https://github.com/radareorg/radare2-r2pipe'
arch=('any')
license=('MIT')
makedepends=('python' 'radare2')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('c3d2f6789a32ec1e5baaf34263cad6ae4a7b501f0bbbeabd0612e559dc57875fdda46d965fd85c70f618a4621bd7f87052b607869b42c3b97210a833c6696e6b')
b2sums=('45e03b391ad68dbb2290ff25c178ee3a2c23ef384a6b80ea535523df4a2efca4bc416e681243961f9b5e0677d9e0233594e018a6f9fa1c875bc8b2ec8187388f')

build() {
  cd radare2-r2pipe-${pkgver}/python
  python setup.py build
}

package_python-r2pipe() {
  depends=('python' 'radare2')
  cd radare2-r2pipe-${pkgver}/python
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
