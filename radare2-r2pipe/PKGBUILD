# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=radare2-r2pipe
pkgname=(python-r2pipe)
pkgver=5.4.0
pkgrel=2
pkgdesc='Access radare2 via pipe from any programming language'
url='https://github.com/radareorg/radare2-r2pipe'
arch=('any')
license=('MIT')
makedepends=('python' 'radare2')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('b2c598d80f1612fa80077556ffd62982666f9a8474f148029047c08d76262c918fa41e414a08f669dc060489e970872788f0a7fbedadeaf1b18f8a1af9845e58')
b2sums=('acefd5dcb12e6d01c243ee37455b1897638be1217658a8738b10fb506d9b294c1ad63c8d55d6d6ba26ed7b7e1739d83a6da41a2d830f5439f821fb817cd8a0b3')

build() {
  cd radare2-r2pipe-${pkgver}/python
  python setup.py build
}

package_python-r2pipe() {
  depends=('python' 'radare2')
  cd radare2-r2pipe-${pkgver}/python
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
