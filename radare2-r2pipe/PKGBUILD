# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=radare2-r2pipe
pkgname=(python-r2pipe)
pkgver=5.7.0
pkgrel=1
pkgdesc='Access radare2 via pipe from any programming language'
url='https://github.com/radareorg/radare2-r2pipe'
arch=('any')
license=('MIT')
makedepends=('python' 'radare2')
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('69d83fbc746d0986aabe7ae5847a2eba46e93964c687c27a143f9c42d02f3f4b13b13ba425cc6c09968edb957d02b101770bdd74aed9c430c291a3637ae5dac9')
b2sums=('da2e5ff034969a3e10e974caf1fc123e61a687e22eed5b3b0e828d2def1d6a141fc78f6c67ac201b0db292d6462445ac6919acffdd9d9961280872c09b1c4f7c')

build() {
  cd radare2-r2pipe-${pkgver}/python
  python setup.py build
}

package_python-r2pipe() {
  depends=('python' 'radare2')
  cd radare2-r2pipe-${pkgver}/python
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
