# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Alex <mail.avatar@gmail.com>

pkgname=exfat-utils
pkgver=1.3.0
pkgrel=2
pkgdesc='Utilities for exFAT file system'
url='https://github.com/relan/exfat'
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'fuse2')
conflicts=('fuse-exfat')
provides=('fuse-exfat')
replaces=('fuse-exfat')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/relan/exfat/archive/v${pkgver}.tar.gz)
sha512sums=('fa3951e16889db65685e1fb71b0c75fc7014a8025c3442bf6164b94ddd51d282a8ae5e891c46195be53d2a10d62444e2fa1bb3fa3de59c2c3411c6dac363b488')

prepare() {
  cd exfat-${pkgver}
  autoreconf -fiv
}

build() {
  cd exfat-${pkgver}
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin
  make CCFLAGS="${CFLAGS} ${CPPFLAGS} -std=c99" LINKFLAGS="${LDFLAGS}"
}

package() {
  cd exfat-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 */*.8 -t "${pkgdir}/usr/share/man/man8"
}

# vim: ts=2 sw=2 et:
