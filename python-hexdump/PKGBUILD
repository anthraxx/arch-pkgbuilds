# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: knucker <www.knucker[at]gmail.com>

pkgbase=python-hexdump
pkgname=('python-hexdump' 'python2-hexdump')
_pyname=${pkgbase/python-/}
_hghash=396fbd1d0c1e
pkgver=3.3
pkgrel=3
pkgdesc='Library and tool to work with hex and binary data'
url='https://bitbucket.org/techtonik/hexdump'
arch=('any')
license=('Public Domain')
makedepends=('python' 'python2')
checkdepends=('python-tox')
source=(${pkgbase}-${pkgver}.tar.bz2::https://bitbucket.org/techtonik/${_pyname}/get/${pkgver}.tar.bz2)
sha512sums=('b8f9c2c7ef69cf52572434c1edb529b7bd61e66cc62f1fb8ff32bcf5899502607552a9a1c50deed2e02f3b638744d509dedf588fa1176d79d4c9ec198f650e4d')

prepare() {
  (cd techtonik-${_pyname}-${_hghash}
    sed -r 's|(py3)[0-9]+|\1|g' -i tox.ini
    sed -r 's|(py2)[0-9]+|\1|g' -i tox.ini
  )
  cp -ra techtonik-${_pyname}-${_hghash}{,-py2}
  (cd techtonik-${_pyname}-${_hghash}-py2
    sed -r 's|(/usr/bin/env python)|\12|' -i hexdump.py
  )
}

check() {
  cd techtonik-${_pyname}-${_hghash}
  tox
}

package_python-hexdump() {
  depends=('python')
  cd techtonik-${_pyname}-${_hghash}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr
}

package_python2-hexdump() {
  depends=('python2')
  cd techtonik-${_pyname}-${_hghash}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --prefix=/usr
}

# vim: ts=2 sw=2 et:
