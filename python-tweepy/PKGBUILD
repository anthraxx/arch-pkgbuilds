# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-tweepy
_pypiname=tweepy
pkgver=4.5.0
pkgrel=1
pkgdesc='Python library for accessing the entire Twitter API'
url='https://www.tweepy.org/'
arch=('any')
license=('MIT')
depends=('python' 'python-requests' 'python-requests-oauthlib' 'python-six' 'python-pysocks')
makedepends=('python-setuptools' 'python-pip' 'python-requests' 'python-requests-oauthlib'
             'python-six' 'python-pysocks')
source=(https://github.com/tweepy/${_pypiname}/archive/v${pkgver}/${_pypiname}-${pkgver}.tar.gz)
sha256sums=('d17e2619017b704f13a3d8e3cd836533e61bdf7d943b67f135ac31cf621e1b4a')
sha512sums=('a246c7129b3c23ea14eaf439ce1595000961587a57bdc9dcc4852617b8531efcb764059b0448843fc06bc792f44d8cf24bd7a8e3bfaf5674078155d1f2059515')

build() {
  cd ${_pypiname}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pypiname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
