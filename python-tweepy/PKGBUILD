# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=python-tweepy
_pypiname=tweepy
pkgver=4.8.0
pkgrel=1
pkgdesc='Python library for accessing the entire Twitter API'
url='https://www.tweepy.org/'
arch=('any')
license=('MIT')
depends=('python' 'python-requests' 'python-requests-oauthlib' 'python-six' 'python-pysocks')
makedepends=('python-setuptools' 'python-pip' 'python-requests' 'python-requests-oauthlib'
             'python-six' 'python-pysocks')
source=(https://github.com/tweepy/${_pypiname}/archive/v${pkgver}/${_pypiname}-${pkgver}.tar.gz)
sha256sums=('6ca49718d9cd0e73080f780062e050dd8ca777201365fb50b935d727f7c1fc18')
sha512sums=('3342b377918957679d8d4a90f7f91c2a661cb9b313875fdfd90f5424cd44880f38c6e72955a54b4fd38ebe33273f9c2a2d2006bee861ba5869e8ee3fc05a6856')

build() {
  cd ${_pypiname}-${pkgver}
  python setup.py build
}

package() {
  cd ${_pypiname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --prefix=/usr --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -d "${pkgdir}/usr/share/doc/${pkgname}"
  cp -r examples "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
