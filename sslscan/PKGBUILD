# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne-arch at coredump dot fr>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=sslscan
pkgver=2.0.11
pkgrel=1
pkgdesc="A fast tool to scan SSL services such as HTTPS to determine supported ciphers"
url="https://github.com/rbsec/sslscan"
arch=('x86_64')
license=('GPL3')
depends=('openssl')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
b2sums=('4d4db6fa7b8f6f611e752d0f97ff1219fcbd064280bc4133dd38d1268cb268847801d25403eeb6bc475579a931e944c9a21d90e40fed6bed2a6a6bc535540f7d')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e 's/CFLAGS ?=/CFLAGS +=/' \
    -e 's/LDFLAGS ?=/LDFLAGS +=/' \
    -e "s/^GIT_VERSION =.*/GIT_VERSION = ${pkgver}/" \
    -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
