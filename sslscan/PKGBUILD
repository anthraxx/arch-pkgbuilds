# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne-arch at coredump dot fr>
# Contributor: George Rawlinson <grawlinson@archlinux.org>

pkgname=sslscan
pkgver=2.0.12
pkgrel=1
pkgdesc="A fast tool to scan SSL services such as HTTPS to determine supported ciphers"
url="https://github.com/rbsec/sslscan"
arch=('x86_64')
license=('GPL3')
depends=('openssl')
source=("$pkgname-$pkgver.tar.gz::$url/archive/$pkgver.tar.gz")
sha512sums=('3758b5e697a1b53e4c574deabfe366d0796d6cb2481a644ec6139cd11e9b4cc2878b094ab9bf4e700ecc73cce6a374a1cf9f487967de3a038dacb985aebee948')
b2sums=('4c7ec58b7e454bb661f05da58eff55dad940ae1a8fb28daf8056114402949e6712e59e13e7128dec0eb45e3e21c2b349e0ac5b2fc7a3a334807b6fc4e2aeb595')

prepare() {
  cd "$pkgname-$pkgver"
  sed -e 's/CFLAGS ?=/CFLAGS +=/' \
    -e 's/LDFLAGS ?=/LDFLAGS +=/' \
    -e "s/^GIT_VERSION =.*/GIT_VERSION = ${pkgver}/" \
    -i Makefile
}

build() {
  cd "$pkgname-$pkgver"
  make
}

package() {
  cd "$pkgname-$pkgver"
  make DESTDIR="$pkgdir" install
}

# vim:set ts=2 sw=2 et:
