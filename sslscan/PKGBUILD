# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Remi Gacogne <rgacogne-arch at coredump dot fr>

pkgname=sslscan
pkgver=1.11.12
pkgrel=1
pkgdesc="A fast tools to scan SSL services, such as HTTPS to determine the ciphers that are supported"
url="https://github.com/rbsec/sslscan"
arch=('i686' 'x86_64')
license=('GPL3')
makedepends=('git')
depends=('zlib')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/rbsec/${pkgname}/archive/${pkgver}-rbsec.tar.gz)
sha512sums=('e667cc50f0ec71343b73ca11871a1a53aa9f255c671f16b7c5f33093125a1e60d6e7945e7bd7296f4a71aa6837c823dc939e18982f3e3f831acc061ead9e7ebb')

prepare() {
  cd ${pkgname}-${pkgver}-rbsec
  sed -e 's/CFLAGS ?=/CFLAGS +=/' -e 's/LDFLAGS ?=/LDFLAGS +=/' -i Makefile
}

build() {
  cd ${pkgname}-${pkgver}-rbsec
  make static
}

package() {
  cd ${pkgname}-${pkgver}-rbsec
  make DESTDIR=${pkgdir} install
}

# vim:set ts=2 sw=2 et:
