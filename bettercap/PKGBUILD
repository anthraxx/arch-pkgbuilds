# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>

pkgname=bettercap
pkgver=2.28
pkgrel=1
pkgdesc='Swiss army knife for network attacks and monitoring'
url='https://github.com/bettercap/bettercap'
arch=('x86_64')
license=('GPL3')
depends=('libpcap' 'libnetfilter_queue' 'iproute2' 'glibc' 'libnfnetlink')
makedepends=('go' 'git')
optdepends=('bettercap-caplets: scripts (caplets) and proxy modules')
options=('!emptydirs')
source=(https://github.com/bettercap/bettercap/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('914b5171e5bea60c008b73e1de528c5d631da459f9670c7dc4eca068e28ee22d0a9b8e9a8b6655aa8ffec795fa5960c7d4335231841839a1abf8583d855c6753')
b2sums=('b46f4ec60ce1a65a6e7aaa9b05fd1c6280393c9ca4b4578b45ea880005bc7b5d9cd8e1aab7e908067d95aee8851bdaa5e2b5e0e27ccea7b6556035e2424b58a6')

prepare() {
  cd ${pkgname}-${pkgver}
  find . -type f -exec sed 's|/usr/local|/usr|g' -i {} +
}

build() {
  cd ${pkgname}-${pkgver}
  export GO11MODULE=on
  export CGO_LDFLAGS="${LDFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export GOFLAGS="-buildmode=pie -trimpath"
  go build
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 bettercap -t "${pkgdir}/usr/bin"
  install -Dm 644 bettercap.service -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
