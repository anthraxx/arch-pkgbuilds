# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Andreas 'Segaja' Schleifer <segaja at archlinux dot org>
# Contributor: Eli Schwartz <eschwartz@archlinux.org>
# Contributor: lp76 <l.peduto@gmail.com>
# Contributor: Daenyth <Daenyth+Arch AT gmail DOT com>
# Contributor: Gaetan Bisson <bisson@archlinux.org>

pkgname=ncdu
pkgver=2.1
pkgrel=2
pkgdesc='Disk usage analyzer with an ncurses interface'
url='https://dev.yorhel.nl/ncdu'
license=('MIT')
depends=('ncurses')
makedepends=('zig')
arch=('x86_64')
source=("https://dev.yorhel.nl/download/${pkgname}-${pkgver}.tar.gz"{,.asc}
        "hardening.patch")
sha256sums=('4ba8d20a64a55cc00af2d2c6138afcc9dc25e40a5ed6a8c544cb9e2285a195fe'
            'SKIP'
            '833142036e56cd7f8ee111f2e0855f12d305d2ec03925c2f3848a14f7305abbf')
b2sums=('9dd90b63ad7edbd402b0cdab7f5aa5263c6df562afb15229b5c2b6143cf7efc7af94fabcd710593b2fd94fca02742e08b401fc0103b1154c6cd278f5709eeef8'
        'SKIP'
        'bf38e61d425d2d264320fcbf27ad8e3af769aaa2b3df5d51cdaf7176b8aeb84504abb157560d558094afaf1fac406a6b35cc93e769459fda2e34eacf803b210f')
validpgpkeys=('74460D32B80810EBA9AFA2E962394C698C2739FA') # Yoran Heling <projects@yorhel.nl>

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    patch --verbose --strip=1 --input=../hardening.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    zig build -Drelease-safe -Dcpu=baseline

    make doc
}

check() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    zig build test
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    install -D --mode=755 "./zig-out/bin/${pkgname}" "${pkgdir}/usr/bin/${pkgname}"

    make install-doc PREFIX="${pkgdir}/usr"

    install -D --mode=644 LICENSES/MIT.txt "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
