# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: kastor <kastor@fobos.org.ar>

_name=pupnp
pkgname=libupnp
pkgver=1.14.8
pkgrel=1
pkgdesc='Portable Open Source UPnP Development Kit'
url="https://pupnp.sourceforge.io/"
arch=('x86_64')
license=('BSD')
depends=('glibc')
makedepends=('cmake')
provides=('libixml.so' 'libupnp.so')
source=("${pkgname}-${pkgver}.tar.gz::https://github.com/${_name}/${_name}/archive/release-${pkgver}.tar.gz")
sha512sums=('fa9d958d9f743db9544fd21f2423b3d01be49b12468f133cef8a00f4299c3e633749d0020516d6eda0d5efa7b43ecedec9d9fec60d2545ce87e992cd376514f3')
b2sums=('a931f672588347c2ae9f6af020dd20280e16a25c329992ea8ea7face2294d872bb1b35fbfb7db05337a8e19433ccef4079cc84706ef317250f27d299fc753a09')

prepare() {
  mv -v ${_name}-release-${pkgver} ${pkgname}-${pkgver}
}

build() {
  cd ${pkgname}-${pkgver}
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE='None' \
        -Wno-dev \
        -B build \
        -S .
  make VERBOSE=1 -C build
}

check() {
  cd ${pkgname}-${pkgver}
  make VERBOSE=1 -C build test
}

package() {
  cd ${pkgname}-${pkgver}
  make -C build DESTDIR="${pkgdir}" install
  install -vDm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -vDm 644 {ChangeLog,README.md} \
    -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
