# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: David Runge <dvzrv@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: kastor <kastor@fobos.org.ar>

_name=pupnp
pkgname=libupnp
pkgver=1.14.15
pkgrel=1
pkgdesc='Portable Open Source UPnP Development Kit'
arch=(x86_64)
url="https://pupnp.sourceforge.io/"
license=(BSD)
depends=(glibc)
makedepends=(cmake)
provides=(libixml.so libupnp.so)
source=($pkgname-$pkgver.tar.gz::https://github.com/$_name/$_name/archive/release-$pkgver.tar.gz)
sha512sums=('dde2563eaacdd288066012f001b819ec25ab51bc36aef23db71d2eb67eef778b6b2b10b35a2f6b3041ecef20167e873e1c54e17fc4a26e8d1d9f408e2050279d')
b2sums=('5918f41b64fe5e4a0209058f4706d724d0fcad74c8e3bfc2a54d6d5a1af253d4461ec43b0c896f1da14dcb3fb0925cbae3bc6eb015380e1cd23a3d88e6ed656f')

build() {
  cmake -DCMAKE_INSTALL_PREFIX=/usr \
        -DCMAKE_BUILD_TYPE=None \
        -DUPNP_BUILD_STATIC=OFF \
        -Dblocking_tcp_connections=OFF \
        -Dreuseaddr=ON \
        -Wno-dev \
        -B build \
        -S $_name-release-$pkgver
  make VERBOSE=1 -C build
}

check() {
  make VERBOSE=1 test -C build
}

package() {
  make VERBOSE=1 DESTDIR="$pkgdir" install -C build
  install -vDm 644 $_name-release-$pkgver/COPYING -t "$pkgdir/usr/share/licenses/$pkgname"
  install -vDm 644 $_name-release-$pkgver/{ChangeLog,README.md} -t "$pkgdir/usr/share/doc/$pkgname"
}

# vim: ts=2 sw=2 et:
