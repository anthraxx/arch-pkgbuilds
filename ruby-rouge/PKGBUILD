# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_gemname=rouge
pkgname=ruby-rouge
pkgver=3.28.0
pkgrel=1
pkgdesc='Pure-ruby code highlighter that is compatible with pygments'
url='http://rouge.jneen.net/'
arch=('any')
license=('MIT')
depends=('ruby' 'ruby-rdoc')
options=('!emptydirs')
source=(https://github.com/jneen/rouge/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('a6cb87ef7ec1eae94f127ef615b77830581d1e3a2f690e852effafd146e4456e6c931f843e9df068a750b69af8a6d8d958277d94e16550e016a58412caeba7d7')
b2sums=('38b360bc282887913e24e8ebd5463d792d03f55e70553f93b12ee94c7e1ed9d6d2ac3d084c21d21a59480a1669b552bc10b42ec2d0e63b167f142b241ce884db')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  gem build ${_gemname}.gemspec
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(gem env gemdir)"
  gem install --ignore-dependencies --no-user-install -i "${pkgdir}${_gemdir}" -n "${pkgdir}/usr/bin" ${_gemname}-${pkgver}.gem
  install -Dm 644 README.md CHANGELOG.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  rm "${pkgdir}/${_gemdir}/cache/${_gemname}-${pkgver}.gem"
}

# vim: ts=2 sw=2 et:
