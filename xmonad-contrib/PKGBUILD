# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Jelle van der Waa <jelle@archlinux.org>
# Contributor: Sergej Pupykin <pupykin.s+arch@gmail.com>
# Contributor: Vesa Kaihlavirta <vegai@iki.fi>
# Contributor: orbisvicis <gmail.com>

pkgname=xmonad-contrib
pkgver=0.16
pkgrel=131
pkgdesc='Add-ons for xmonad'
url='https://xmonad.org/'
arch=('x86_64')
license=('BSD')
depends=('ghc-libs' 'xmonad' 'sh' 'haskell-x11' 'haskell-x11-xft' 'haskell-utf8-string' 'haskell-random'
         'haskell-old-time')
makedepends=('ghc' 'uusi')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/xmonad/xmonad-contrib/archive/v${pkgver}.tar.gz
        ghc9.patch)
sha512sums=('68f93ef6bcc1f2b3f5422c35a9ddcf846c75c1e9d8e6177750f4fad8dec818642e0a7159d295a381f0656c85b1f52382af1e32d577a825c4fcbdfa4c131581cc'
            'aced6dbfa91edbbb46e6cf29593a12e73df0cd89ad66de5ab646c8e6b9a49b54c93478c3e589377a106b3bc6c5d68246aecd2d662870b33421f2c0ee70995c6c')

prepare() {
  uusi -d semigroups -u X11 ${pkgname}-${pkgver}/${pkgname}.cabal
  patch -d ${pkgname}-${pkgver} -p1 < ghc9.patch
}

build() {
  cd ${pkgname}-${pkgver}
  runhaskell Setup.lhs configure -O --enable-shared --enable-executable-dynamic --disable-library-vanilla \
             --prefix=/usr -fuse_xft --libsubdir=\$compiler/site-local/\$pkgid --ghc-option=-fllvm \
             --docdir=/usr/share/doc/${pkgname}
  runhaskell Setup build $MAKEFLAGS
  runhaskell Setup register --gen-script
  runhaskell Setup unregister --gen-script
  sed -i -r -e "s|ghc-pkg.*update[^ ]* |&'--force' |" register.sh
  sed -i -r -e "s|ghc-pkg.*unregister[^ ]* |&'--force' |" unregister.sh
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 744 register.sh   "${pkgdir}/usr/share/haskell/register/${pkgname}.sh"
  install -Dm 744 unregister.sh "${pkgdir}/usr/share/haskell/unregister/${pkgname}.sh"
  runhaskell Setup.lhs copy --destdir="${pkgdir}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
