# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Orhun ParmaksÄ±z <orhun@archlinux.org>
# Contributor: Daniel Wallace <danielwallace at gtmanfred dot com>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>

pkgname=scrot
pkgver=1.6
pkgrel=2
pkgdesc='Simple command-line screenshot utility for X'
url='https://github.com/resurrecting-open-source-projects/scrot'
arch=('x86_64')
license=('MIT')
depends=('imlib2' 'libxext' 'libxfixes' 'libxcomposite')
makedepends=('autoconf-archive')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha512sums=('6ff2404e0b0a657e6c3a43034b6964d47db3fb884a2e948b756424d626b6a9f1b59e7e7800e7557b56c5afe77537992b80203f564030ca1928d06a89f16c6740')
b2sums=('0ba3b6ea8031841da039a130512a74f8db516204355eb0041027a3cc6c348b08beafe158cb44fadaa3c7a307198548d29a37ba8e9cabf0ef616914030aae82bb')

prepare(){
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 README.md ChangeLog -t "${pkgdir}"/usr/share/doc/${pkgname}
  install -Dm 644 COPYING -t "${pkgdir}"/usr/share/licenses/${pkgname}
}

# vim: ts=2 sw=2 et:
