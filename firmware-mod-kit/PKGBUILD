# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: gardar <aur@gardar.net>
# Contributor: Antonio VÃ¡zquez <antoniovazquezblanco[at]gmail[dot]com>

pkgname=firmware-mod-kit-git
pkgver=0.99.r75.g104c821
pkgrel=1
pkgdesc='Allows easy deconstruction and reconsutrction of firmware images for various embedded devices'
url='http://code.google.com/p/firmware-mod-kit/'
arch=('i686' 'x86_64')
license=('GPL')
makedepends=('dos2unix')
depends=('python-magic' 'zlib' 'xz')
options=('!strip')
source=("git+git://github.com/rampageX/$pkgname.git"
        "https://raw.githubusercontent.com/st-ty1/Arch-Linux_firmware-mod-kit/main/gcc10.patch")
sha512sums=('SKIP'
            'b777fe4386dc3e44f72d1fb0f2c41b4db751e0034bab1a7e1a2dbd26be741c5fa4345b671f74ec1896946263b364bf5ecc3e2befd3dc56073e651b8603c7c109')

pkgver() {
  cd ${srcdir}/$pkgname
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  dos2unix ${srcdir}/$pkgname/src/others/squashfs-3.4-cisco/squashfs-tools/mksquashfs.c
  patch -p1 -d ${srcdir}/$pkgname < gcc10.patch
}

build() {
  cd ${srcdir}/$pkgname/src
  ./configure
  make
}

package() {
  rm -rf ${srcdir}/$pkgname/.git
  install -d "${pkgdir}/opt"
  find $srcdir/$pkgname -name '*.o' -delete
  mv $srcdir/$pkgname "${pkgdir}/opt/${pkgname}"
}
