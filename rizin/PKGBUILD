# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=rizin
pkgver=0.3.4
pkgrel=1
pkgdesc='Open-source tools to disasm, debug, analyze and manipulate binary files'
url='https://rizin.re'
arch=('x86_64')
license=('GPL3' 'LGPL3')
depends=(
  'sh' 'capstone' 'libcapstone.so' 'openssl' 'libzip' 'zlib' 'lz4' 'xxhash'
  'libxxhash.so' 'file' 'libuv' 'tree-sitter' 'libtree-sitter.so'
  'libzip.so' 'libmagic.so')
makedepends=('meson' 'ninja' 'python')
optdepends=('rz-ghidra: ghidra decompiler plugin')
options=('!emptydirs')
source=(https://github.com/rizinorg/rizin/releases/download/v${pkgver}/rizin-src-v${pkgver}.tar.xz)
sha512sums=('cda35afe8c031f3207c055ed31d7f76a46b7addd04cead0f5aadf0733b66f4dd953ca16f054b47da7921d4726d6ea1250436012954a9755c1cd524a8fd0d670c')
b2sums=('99745f069e6bd72aec6a6f335f3aa0ccc46ebf186b1dc0aaf32b4f95d2be4bb49e8d3d976a72978c22fce82d896e70bf8403568e100b73898731045ef06a2ad5')

build() {
  cd ${pkgname}-v${pkgver}
  arch-meson build \
    -D use_sys_capstone=enabled \
    -D use_capstone_version=v4 \
    -D use_sys_magic=enabled \
    -D use_sys_libzip=enabled \
    -D use_sys_zlib=enabled \
    -D use_sys_lz4=enabled \
    -D use_sys_xxhash=enabled \
    -D use_sys_openssl=enabled \
    -D use_sys_tree_sitter=enabled \
    -D use_libuv=true \
    -D use_webui=true \
    -D local=disabled
  ninja -C build
}

package() {
  cd ${pkgname}-v${pkgver}
  DESTDIR="${pkgdir}" ninja -C build install
  install -d "${pkgdir}/usr/share/doc/rizin"
  cp -r doc/* "${pkgdir}/usr/share/doc/rizin"
}

# vim: ts=2 sw=2 et:
