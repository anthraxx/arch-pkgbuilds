# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: David Birks <david@birks.dev>
# Contributor: Mike Williamson <mike at korora dot ca>

pkgname=eksctl
pkgver=0.79.0
pkgrel=1
pkgdesc='Command line tool for creating clusters on Amazon EKS'
url='https://github.com/weaveworks/eksctl'
arch=('x86_64')
license=('Apache')
depends=('kubectl' 'glibc')
makedepends=('go')
source=("${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz")
sha256sums=('7f7a3c67378737a6c10aa57c209f88e1504b7c46b3186ecf00dad88c666634ed')
b2sums=('4773d4b80526fb254960776d4e3e98e5e62afecfd59ab99bf3054b1aaae62db2d049d2fc8a74ac9a82c6d79f497d0fb12115a96f47f7bf79f4871a8b6c850e5d')

build() {
  cd ${pkgname}-${pkgver}
  export CGO_CFLAGS="${CFLAGS}"
  export CGO_CXXFLAGS="${CXXFLAGS}"
  export CGO_CPPFLAGS="${CPPFLAGS}"
  export CGO_LDFLAGS="${LDFLAGS}"
  export GOFLAGS="-buildmode=pie -ldflags=-linkmode=external -trimpath -mod=readonly -modcacherw"
  go build -v \
    -ldflags "-linkmode=external -extldflags '${LDFLAGS}' -X github.com/weaveworks/eksctl/pkg/version.gitTag=${pkgver}" \
    ./cmd/eksctl
}

package() {
  cd ${pkgname}-${pkgver}
  install -Dm 755 ${pkgname} -t "$pkgdir/usr/bin"

  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"

  # completions
  "${pkgdir}/usr/bin/${pkgname}" completion bash | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/bash-completion/completions/${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion zsh | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/zsh/site-functions/_${pkgname}"
  "${pkgdir}/usr/bin/${pkgname}" completion fish | install -Dm 644 /dev/stdin "${pkgdir}/usr/share/fish/completions/${pkgname}.fish"
}

# vim: ts=2 sw=2 et:
