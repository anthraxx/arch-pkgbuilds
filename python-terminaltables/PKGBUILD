# Maintainer: Christian Rebischke <chris.rebischke[at]archlinux[dot]org>
# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgbase=python-terminaltables
_pyname=terminaltables
pkgname=('python-terminaltables' 'python2-terminaltables')
pkgver=3.1.0
pkgrel=5
pkgdesc="Generate simple tables in terminals from a nested list of strings"
arch=('any')
url="https://github.com/Robpol86/terminaltables"
license=('MIT')
makedepends=('python' 'python-setuptools'
             'python2' 'python2-setuptools')
checkdepends=('python-pytest' 'python-colorama' 'python-termcolor' 'python-colorclass'
              'python2-pytest' 'python2-colorama' 'python2-termcolor' 'python2-colorclass')
source=("${_pyname}-${pkgver}.tar.gz::https://github.com/Robpol86/${_pyname}/archive/v${pkgver}.tar.gz")
sha512sums=('dc49458652fff8bc6094d316d84c9b8e9fca1a26e3230c0b668bc03ec8528793f4ef024e8032d4a56fbfabfdfd4a1142870f550f0b373ba6a42dd2e3ead3f501')

prepare() {
  cp -a ${_pyname}-${pkgver}{,-py2}
}

build() {
  msg2 "Building python..."
  (cd ${_pyname}-${pkgver}
    python setup.py build
  )
  msg2 "Building python2..."
  (cd ${_pyname}-${pkgver}-py2
    python2 setup.py build
  )
}

check() {
  msg2 "Checking python..."
  (cd ${_pyname}-${pkgver}
    py.test
  )
  msg2 "Checking python2..."
  (cd ${_pyname}-${pkgver}-py2
    py.test2
  )
}

package_python-terminaltables() {
  depends=('python')
  cd ${_pyname}-${pkgver}
  python setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 example*.py -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

package_python2-terminaltables() {
  depends=('python2')
  cd ${_pyname}-${pkgver}-py2
  python2 setup.py install -O1 --root="${pkgdir}" --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 example*.py -t "${pkgdir}/usr/share/doc/${pkgname}/examples"
}

# vim:set et sw=2 ts=2 tw=79:
