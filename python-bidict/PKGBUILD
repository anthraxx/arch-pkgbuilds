# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Omar Pakker <archlinux@opakker.nl>

pkgname=python-bidict
_name=${pkgname#python-}
pkgver=0.21.4
pkgrel=1
pkgdesc='Bidirectional mapping library for Python'
url='https://bidict.readthedocs.org/'
arch=('any')
license=('MPL2')
depends=('python')
makedepends=('python-setuptools' 'python-pip' 'python-wheel')
checkdepends=('python-pytest' 'python-sphinx' 'python-sphinx-autodoc-typehints' 'python-pytest-benchmark'
	          'python-hypothesis' 'python-py-cpuinfo' 'python-sortedcollections')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz)
sha256sums=('42c84ffbe6f8de898af6073b4be9ea7ccedcd78d3474aa844c54e49d5a079f6f')
b2sums=('36933b070a30c5f64c28dda2efccc1d22a8a56a5b13b25c14f240a3bf6ee17d1f2cfee0b6d4f43f86bb1a14dd626acc0b611c8b2e4d2047529d46b463344c7d3')

build() {
  cd "bidict-${pkgver}"
  python setup.py build
}

check() {
  cd "bidict-${pkgver}"
  PYTHONPATH="build/lib" pytest
}

package() {
  cd "bidict-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
