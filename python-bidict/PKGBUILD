# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Omar Pakker <archlinux@opakker.nl>

pkgname=python-bidict
_name=${pkgname#python-}
pkgver=0.21.3
pkgrel=3
pkgdesc='Bidirectional mapping library for Python'
url='https://bidict.readthedocs.org/'
arch=('any')
license=('MPL2')
depends=('python')
makedepends=('python-setuptools' 'python-pip' 'python-wheel')
checkdepends=('python-pytest' 'python-sphinx' 'python-sphinx-autodoc-typehints' 'python-pytest-benchmark'
	          'python-hypothesis' 'python-py-cpuinfo')
source=(https://files.pythonhosted.org/packages/source/${_name::1}/${_name}/${_name}-${pkgver}.tar.gz)
sha256sums=('d50bd81fae75e34198ffc94979a0eb0939ff9adb3ef32bcc93a913d8b3e3ed1d')
b2sums=('d6fe581d3114e03fda76a0dcc47a65a5d0a840f17e5eb492341a6498f79b0f57f1d342220016001da4342baecdc1e5b575268d73a21fc01a0520fc11f31ff5ab')

build() {
  cd "bidict-${pkgver}"
  python setup.py build
}

check() {
  cd "bidict-${pkgver}"
  python setup.py test
}

package() {
  cd "bidict-${pkgver}"
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
