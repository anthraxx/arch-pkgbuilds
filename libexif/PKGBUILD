# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libexif
pkgver=0.6.23
pkgrel=1
pkgdesc='Library to parse an EXIF file and read the data from those tags'
url='https://github.com/libexif/libexif'
arch=(x86_64)
license=('LGPL')
depends=('glibc')
provides=('libexif.so')
source=(${url}/archive/${pkgname}-${pkgver//./_}-release.tar.gz)
sha512sums=('9fa2e124d6a5787fd1c6101be9c345e699bc074026f7c0e1d0c8dc202b922e749a76d7a9d4dd7afe880a7e2ff8ac029590b9957743962dbc0e17320c21afd22d')
b2sums=('e2e9c8ea15a0fce684e9a23b9f7bd8aa12c235bdd2c2dc0ebaee738e6ca82cf28ffb72e0d3ceef778b0f44fd1c1ac72bc79a428e09e850ae4b57bbc4672f404e')

prepare() {
  cd ${pkgname}-${pkgname}-${pkgver//./_}-release
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgname}-${pkgver//./_}-release
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgname}-${pkgver//./_}-release
  make DESTDIR="${pkgdir}" install
}
