# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Jan de Groot <jgc@archlinux.org>
# Contributor: Tobias Kieslich <tobias@justdreams.de>

pkgname=libexif
pkgver=0.6.24
pkgrel=1
pkgdesc='Library to parse an EXIF file and read the data from those tags'
url='https://github.com/libexif/libexif'
arch=(x86_64)
license=('LGPL')
depends=('glibc')
provides=('libexif.so')
source=(${url}/archive/${pkgname}-${pkgver//./_}-release.tar.gz)
sha512sums=('0b15a157c1030490bf1c4239487dffda90daad467ac6281db2a1b34a8419fca32b4b5265452e75cbcd2c9dc9a829643231cd3749e88251ed1b596756d1c5a9f4')
b2sums=('7379605e09697b84c314315f81d9c76ecb001e306df056bb02290ac43b436147e109465c0f0932af04c51e18d1e4050084d60df52d076decbf596c0625742f58')

prepare() {
  cd ${pkgname}-${pkgname}-${pkgver//./_}-release
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgname}-${pkgver//./_}-release
  ./configure --prefix=/usr
  make
}

package() {
  cd ${pkgname}-${pkgname}-${pkgver//./_}-release
  make DESTDIR="${pkgdir}" install
}
