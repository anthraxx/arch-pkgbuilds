# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Aaron Griffin <aaron@archlinux.org>

pkgname=asio
pkgver=1.24.0
pkgrel=1
pkgdesc='Cross-platform C++ library for ASynchronous network I/O'
url='https://think-async.com/Asio/'
arch=('any')
license=('custom:boost')
makedepends=('boost')
source=(https://sourceforge.net/projects/asio/files/asio/${pkgver}%20%28Stable%29/asio-${pkgver}.tar.bz2)
sha512sums=('c877968229bc5c4c21156b2bbb9775c7b11c3878cd62e8df3afaa70b2f221c59c6e4fa425cfb028cd170f9453ac1c32c5229f89437b13ac39142e4b52304c820')
b2sums=('4c9b913452fd4288684020ca58b48e22745a39c6a0d682ba84ff561b94c695dc6f5203ed06817f4dd0b9098ea3012578f7c6b2f251b3857e660b0c222dbc003e')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make check
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING LICENSE_1_0.txt -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
