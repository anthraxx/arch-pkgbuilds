# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: George Rawlinson <grawlinson@archlinux.org>

_gemname=rb-fsevent
pkgname=ruby-${_gemname}
pkgver=0.11.0
pkgrel=1
pkgdesc='FSEvents API with signals handled (without RubyCocoa)'
url='https://github.com/thibaudgg/rb-fsevent'
arch=('any')
license=('MIT')
depends=('ruby')
makedepends=('ruby-rdoc' 'ruby-rake' 'ruby-rspec' 'ruby-bundler')
options=('!emptydirs')
source=(${url}/archive/v${pkgver}/${_gemname}-${pkgver}.tar.gz)
sha512sums=('e6a3997fdf9d15cbd26cef7e00850f13deb01bf7aa72fb2b526ea315b82b1e4fc945e946050d7ce803e208d60656e7e9dcb97ddd94b4a18dacf1c62c03bf715e')
b2sums=('a347594c36e01f268d9f0093d1f46e478b0757694e5f8dbb98d972de74571f4bd7a78b162c1973a93631300f477486c194ef448b4af08de46ccf8753ac4266f6')

prepare() {
  cd ${_gemname}-${pkgver}
  sed -r 's|~>|>=|g' -i ${_gemname}.gemspec Rakefile
  sed 's|git ls-files -z|find -type f -print0\|sed "s,\\\\./,,g"|' -i ${_gemname}.gemspec
}

build() {
  cd ${_gemname}-${pkgver}
  rake build
}

package() {
  cd ${_gemname}-${pkgver}
  local _gemdir="$(ruby -e'puts Gem.default_dir')"
  gem install \
    --verbose \
    --ignore-dependencies \
    --no-user-install \
    --install-dir "$pkgdir/$_gemdir" \
    --bindir "$pkgdir/usr/bin" \
    "pkg/$_gemname-$pkgver.gem"

  # delete cache
  cd "$pkgdir/$_gemdir"
  rm -vrf cache

  # delete unnecessary files & folders
  cd "gems/$_gemname-$pkgver"
  rm -vrf ext .gitignore "$_gemname.gemspec"

  # move documentation
  install -vd "$pkgdir/usr/share/doc/$pkgname"
  mv -vt "$pkgdir/usr/share/doc/$pkgname" README.md

  # move license
  install -vd "$pkgdir/usr/share/licenses/$pkgname"
  mv -vt "$pkgdir/usr/share/licenses/$pkgname" LICENSE.txt
}

# vim: ts=2 sw=2 et:
