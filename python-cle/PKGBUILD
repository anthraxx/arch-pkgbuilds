# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Xeonacid <h.dwwwwww at gmail dot com>

_pyname=cle
pkgname=python-${_pyname}
pkgver=9.2.14
_testbinaries_ver=9.2.14
pkgrel=1
pkgdesc='Loads binaries and their libraries, resolves imports and provides an abstraction of process memory'
url='https://github.com/angr/cle'
arch=('any')
license=('custom:BSD')
depends=('python' 'python-pyelftools' 'python-cffi' 'python-pyvex' 'python-pefile' 'python-sortedcontainers')
optdepends=(
  'python-claripy: symbolic constraints'
  'python-minidump: minidump'
  'python-pyxbe: xbe'
  'python-arpy: ar'
  'python-pyaxmlparser: apk'
)
makedepends=('python-build' 'python-installer' 'python-wheel' 'python-setuptools')
checkdepends=('python-pytest' 'python-minidump' 'python-pyxbe' 'python-arpy' 'python-claripy' 'python-pyaxmlparser')
source=("${url}/archive/v${pkgver}/${pkgname}-${pkgver}.tar.gz"
        "https://github.com/angr/binaries/archive/v${_testbinaries_ver}/angr-binaries-${_testbinaries_ver}.tar.gz")
sha512sums=('8ef7f562485f7bf8a56e89fdfbea776892a49c49651a8d8823177527d2d95d2a2975e3b914cbf43555263f38f70e9fe40e73d9da8a12863c5859f7855864ca94'
            '18de5a475e397f6c2016c28dce89699d346c0526be7c7982ae6a4b4b66dfc3504e714b73abe5384e8e34393c81aab2f3badec1cd9d5db2f79aa351c47f97d475')
b2sums=('a147ce8bf9bf3c38e2cc3d23782c17acb55cb7ff54a0c9d0d6a22911282f380637fb2efbb92a8b498bea49e86d22242a99abea41794d2be18e959ec4077ea3e1'
        '21d55d13b895a1498f67d3162f83e973d29dae8bff3f5c45e0de198b3afa7a5a77bdc9cd4282913a08273b5b0698c5adacf69e30942eb56b1bf82c610b58203d')

prepare() {
  ln -s binaries-${_testbinaries_ver} binaries
}

build() {
  cd ${_pyname}-${pkgver}
  python -m build --wheel --no-isolation
}

check() {
  cd ${_pyname}-${pkgver}
  PYTHONPATH=build/lib pytest
}

package() {
  cd ${_pyname}-${pkgver}
  python -m installer --destdir="$pkgdir" dist/*.whl
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
