# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

pkgname=scapy3k
_pkgname=scapy-python3
pkgver=0.25
pkgrel=1
pkgdesc='Powerful interactive packet manipulation program (based on scapy) written in Python'
url='https://github.com/phaethon/scapy'
depends=('tcpdump' 'python' )
optdepends=('python-cryptography: WEP decryption, IPsec and certificate support'
            'python-pyx: psdump() and pdfdump() functions'
            'python-matplotlib: plotting support'
            'python-netifaces: network interface information support'
            'graphviz: conversations() method support'
            'sox: for VOIP support')
checkdepends=('python-cryptography' 'python-pyx' 'python-matplotlib' 'python-netifaces' 'graphviz' 'sox')
conflicts=('scapy')
arch=('any')
license=('GPL2')
source=(https://pypi.org/packages/source/s/$_pkgname/$_pkgname-$pkgver.tar.gz)
sha256sums=('2ae1b3bd9759844e830a6cc3ba11c3f25b08433a8ee3e7eddc08224905e5ef2b')
sha512sums=('74cb8149e843962394425aec54f3aecc894c8d003efd6eb3d1a7ca0dea760cc30ed8fe7f08769c3dc2f959a32169adccf97006571c6f450cb678e624ac68da3d')

prepare() {
  cd ${_pkgname}-${pkgver}
  sed -r 's|((bin\|doc)/(UT)?scapy)|\13k|g' -i setup.py
  mv doc/scapy.1.gz doc/scapy3k.1.gz
  mv bin/scapy{,3k}
  mv bin/UTscapy{,3k}
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-${pkgver}/test
  # Run only main tests, without network access
  #./run_tests -K netaccess -F
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install -O1 --skip-build --root="${pkgdir}" --prefix=/usr
  install -Dm 644 doc/scapy/*.rst -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
