# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=filezilla
pkgver=3.61.0
pkgrel=2
pkgdesc='Fast and reliable FTP, FTPS and SFTP client'
url='https://filezilla-project.org/'
arch=('x86_64')
license=('GPL')
depends=(
  'dbus' 'xdg-utils' 'wxwidgets-gtk3' 'libidn' 'hicolor-icon-theme' 'sqlite'
  'gnutls' 'libfilezilla'
  libfilezilla.so libdbus-1.so libgtk-3.so libgdk-3.so libnettle.so
)
source=(https://download.filezilla-project.org/client/FileZilla_${pkgver}_src.tar.bz2
        wxwidgets-3.2.patch)
sha256sums=('0afef0e4da25202687b32ed6b1ffcd8442d5b35bdcd16df4f02bb2ea92e299f7'
            'e77564b7c21a443f52dd7cb4fa4b8d1519396b241414fe6ee95feca6e143d748')
sha512sums=('97469bc038eab32f526859ae5db58eeef6f1a8128f67bc653b6b3707b1aa7c9771ef5caa780daa4d11d20d4e7ed8e5df24ef8bd70ee2353f80d77d5410cfa722'
            'd31eb0da71cbc740e6210435f97920caac6c26be98717b656c785df6be248fa27925495fa90b118dcbc582ac32af957472cbbe16fdb4fef0bbc6257d15d06931')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../wxwidgets-3.2.patch # Allow building with wxWidgets 3.2
  autoreconf -vif
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --with-pugixml=builtin
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
