# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=filezilla
pkgver=3.62.0
pkgrel=1
pkgdesc='Fast and reliable FTP, FTPS and SFTP client'
url='https://filezilla-project.org/'
arch=('x86_64')
license=('GPL')
depends=(
  'dbus' 'xdg-utils' 'wxwidgets-gtk3' 'libidn' 'hicolor-icon-theme' 'sqlite'
  'gnutls' 'libfilezilla'
  libfilezilla.so libdbus-1.so libgtk-3.so libgdk-3.so libnettle.so
)
source=(https://download.filezilla-project.org/client/FileZilla_${pkgver}_src.tar.bz2
        wxwidgets-3.2.patch)
sha256sums=('ab16663f320e831c2b06ed839a9f56aa8412f1b323dc65e65efb93f6a4596f0d'
            'd1b55c763222b625b24e97e2982a0eed5fa16a18909a7a3f86f06b246c2bf3d8')
sha512sums=('4852f81cdaed5fc7bf3acf850ad22c34052099477de09e07115db5f1bd440fd4ec2c04c22c484048e5cd82e4ee3474350a81d91905d0459a98cbedddbe5b6a47'
            '0f92bd0180830fdb54cfb2eff2b6ebb3413aaace2b901300ca2f192b5abffea28f511eb26dea4eb8fb2ff8b951d15b4c39bef77cad59542b9f3632bbddd79183')

prepare() {
  cd ${pkgname}-${pkgver}
  patch -p1 < ../wxwidgets-3.2.patch # Allow building with wxWidgets 3.2
  autoreconf -vif
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --with-pugixml=builtin
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
