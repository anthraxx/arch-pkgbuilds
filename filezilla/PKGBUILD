# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel J Griffiths <ghost1227@archlinux.us>
# Contributor: Alexander Fehr <pizzapunk gmail com>

pkgname=filezilla
pkgver=3.56.2
pkgrel=1
pkgdesc='Fast and reliable FTP, FTPS and SFTP client'
url='https://filezilla-project.org/'
arch=('x86_64')
license=('GPL')
depends=(
  'dbus' 'xdg-utils' 'wxgtk3' 'libidn' 'hicolor-icon-theme' 'sqlite'
  'gnutls' 'libfilezilla'
  libfilezilla.so libdbus-1.so libgtk-3.so libgdk-3.so libnettle.so
)
source=(https://download.filezilla-project.org/client/FileZilla_${pkgver}_src.tar.bz2)
sha256sums=('095ae0e7ca38931d7c06d7df6b47bf04f2632545edc3501cbc228ad4bc05057a')
sha512sums=('6aab8a0a8b2205ac5428c6b5ef698010cb9a42759913254ac5981be85ffb8d725251724c1972c9b9ba6c88029573ef2c8a36f6814252b8080476f0b080a9d2f1')

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-manualupdatecheck \
    --disable-autoupdatecheck \
    --with-pugixml=builtin \
    --with-wx-config=/usr/bin/wx-config-gtk3
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
