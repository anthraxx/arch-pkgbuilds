# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Mike Sampson <mike at sambodata dot com>
# Contributor: Dmitry A. Ilyashevich <dmitry.ilyashevich@gmail.com>

pkgbase=distorm
pkgname=('distorm' 'python-distorm')
pkgver=3.5.2
pkgrel=3
pkgdesc='Powerful disassembler library for x86/AMD64'
url='https://github.com/gdabah/distorm'
arch=('x86_64')
license=('BSD')
makedepends=('python' 'python-setuptools' 'glibc')
options=('staticlibs')
source=(https://github.com/gdabah/distorm/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('9c9c5d5e27711afe6c0e4fd4dcb5f8975a0ac8d139b9997f086deaecc888117ca696eb88d05085c827828bac02d949b4eda6ebe38ff85efb8b19c9fdae35abb0')
b2sums=('973779b81e8128b38b666d70e164e233f2ae22d796269626021398c76eebcea42c8de75036221cfc53a6a2f027c8677b95a1107cec5b2d3bfcfa9b2a667f8016')

build() {
  cd ${pkgbase}-${pkgver}
  make -C make/linux CFLAGS="${CFLAGS} ${CPPFLAGS} -fPIC"
  python setup.py build
}

package_distorm() {
  depends=('glibc')
  provides=('libdistorm3.so')
  cd ${pkgbase}-${pkgver}
  make -C make/linux PREFIX=/usr DESTDIR="${pkgdir}" install
  install -Dm 644 include/*.h -t "${pkgdir}/usr/include"
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
  ln -sf "/usr/lib/libdistorm3.so.$(grep 'LIB_R_VERSION =' make/linux/Makefile|cut -d' ' -f3)" "${pkgdir}/usr/lib/libdistorm3.so"
}

package_python-distorm() {
  depends=('python')
  cd ${pkgbase}-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
