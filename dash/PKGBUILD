# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: Dan McGee <dan@archlinux.org>

pkgname=dash
pkgver=0.5.11.1
pkgrel=1
pkgdesc='POSIX compliant shell that aims to be as small as possible'
url='http://gondor.apana.org.au/~herbert/dash/'
arch=('x86_64')
license=('BSD')
depends=('glibc')
install=dash.install
source=(https://git.kernel.org/pub/scm/utils/dash/dash.git/snapshot/${pkgname}-${pkgver}.tar.gz)
sha512sums=('da5126ebd17b1114e7a7da3a982f2de5e1a403c0a65f26f428043317129213126bccb8c9c9a7c2994ac1de2a27dcdce04ba51d1bc1a779ffe689a9ae974c0c36')
b2sums=('a1a6a7dfc9797506759ca4ae8726cac913a66b55a6491a8b18ee15c4a2ba0044abc7ebb137fae39361495921e360176e7118772d80829fce55edfff0b8bdd3a0')

prepare() {
  cd ${pkgname}-${pkgver}
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --mandir=/usr/share/man \
    --exec-prefix="" \
    --with-libedit
  make V=1
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}
