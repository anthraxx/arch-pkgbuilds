# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Tom Newsom <Jeepster@gmx.co.uk>

pkgname=fping
pkgver=4.3
pkgrel=1
pkgdesc='Utility to ping multiple hosts at once'
url='https://www.fping.org/'
arch=('x86_64')
license=('custom')
depends=('glibc')
install=${pkgname}.install
source=(https://www.fping.org/dist/${pkgname}-${pkgver}.tar.gz{,.asc})
sha512sums=('9304a590a9a914227331d495aae9bce434dbef6768b2efbca15bc3dddbdd2c0b8406402c0fe40f8e83740f35704b7473287ac80f7c638549174c0ec055ea8bcb'
            'SKIP')
b2sums=('43d68f8b6884d562aa5563049b1de5e2cc53c02b2ffd2870200cb535c9d0480c5b29e2f2d4886616f8bc0f41f011c671faf35a97f3a9e47275d6a86c1b7bd0a8'
        'SKIP')
validpgpkeys=('8D1E89B8FBA920A5A609F2615A11A22BC5A1F734') # David Schweikert <david@schweikert.ch>

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --sbindir=/usr/bin \
    --mandir=/usr/share/man \
    --enable-ipv4 \
    --enable-ipv6
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYING -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
