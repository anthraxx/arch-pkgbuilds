# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: googol <googol@posteo.de>
# Contributor: Oliver Mangold <o.mangold at gmail dot com>

pkgname=python-alembic
pkgver=1.7.1
pkgrel=1
pkgdesc='Lightweight database migration tool for usage with SQLAlchemy'
url='https://github.com/sqlalchemy/alembic'
arch=('any')
license=('MIT')
depends=('python' 'python-mako' 'python-sqlalchemy' 'python-editor' 'python-dateutil')
makedepends=('python-setuptools' 'python-mako' 'python-sqlalchemy' 'python-editor' 'python-dateutil')
checkdepends=('python-pytest' 'python-mock')
source=(https://pypi.org/packages/source/a/alembic/alembic-${pkgver}.tar.gz{,.asc})
sha512sums=('222a643363f89077b7d30068e0abeb8d8dc373778dd6e4364b559d8a63428f0fe3617505207f431c30a1bd43745dd95fd679dfb1401222f20068327805c2309c'
            'SKIP')
b2sums=('f107c1d787bf9025dc7c092a650449cb911e327fb501f6e4717285f627c455ad43765ca229522a605ac8345f754bbd6808485d77b99576b691ac04185263db51'
        'SKIP')
validpgpkeys=('83AF7ACE251C13E6BB7DEFBD330239C1C4DAFEE1') # Michael Bayer <classic@zzzcomputing.com>

prepare() {
  cd alembic-${pkgver}
  # do not treat warnings as errors
  sed 's|-p no:warnings||g' -i setup.cfg
}

build() {
  cd alembic-${pkgver}
  python setup.py build
}

check() {
  cd alembic-${pkgver}
  py.test
}

package() {
  cd alembic-${pkgver}
  python setup.py install --root="${pkgdir}" --prefix=/usr -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst CHANGES -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
