# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Bruno Galeotti <bgaleotti at gmail dot com>

_pkgname=requests-aws4auth
pkgname=python-requests-aws4auth
pkgver=1.1.1
pkgrel=3
pkgdesc='Amazon Web Services version 4 authentication for the Python Requests module'
url='https://github.com/sam-washington/requests-aws4auth'
arch=('any')
license=('MIT')
depends=('python' 'python-requests' 'python-six')
makedepends=('python-setuptools')
checkdepends=('python-pytest')
source=(${url}/archive/v.${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('447f69a6645360c1c2bbe57fe535a1518b1121c9b4cd7f96ccfe22053c1126cd79e60ce6faae28a1e207356dd7c414cab00f313d5139dfe97b24424e0d12c0aa')
b2sums=('6e1e9def84a5d4d89c9f460e68271e2f7275f4c1eb809380d8953d734d959a15ddba78c21b2dc8ad00e2e9972d76ecb75dbc5c9bf457f70b1c2474e6189b9927')

prepare() {
  cd ${_pkgname}-v.${pkgver}
  sed '/./{H;$!d} ; x ; s/package_data=.*\n.*]},\n//' -i setup.py
}

build() {
  cd ${_pkgname}-v.${pkgver}
  python setup.py build
}

check() {
  cd ${_pkgname}-v.${pkgver}
  python -m pytest -k 'not test_headers_amz_example'
}

package() {
  cd ${_pkgname}-v.${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
