# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Thorsten TÃ¶pper <atsutane-tu@freethoughts.de>
# Contributor: kevin <kevin@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>

pkgname=etherape
pkgver=0.9.20
pkgrel=1
pkgdesc='Graphical network monitor for various OSI layers and protocols'
url='http://etherape.sourceforge.net/'
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'glib2' 'libpcap' 'popt' 'goocanvas' 'gtk3' libpcap.so)
makedepends=('yelp-tools')
source=(https://downloads.sourceforge.net/${pkgname}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('1dbfbb0efff7871a4b78ca90f81ac2155b57ace217b72e443cdc8b3d3807f7ab881c876bec12d93673500026a9dbf1111417a419891df62230e664af9b54b636')
b2sums=('22682d1b7b72206519d0ad32855f558a718bed83674753fcc923419845ac8b97ce970c1121f49eac9b910e10675b5397e5d3a16701af5bc9b78187baee455ccf')

prepare() {
  cd ${pkgname}-${pkgver}
  sed -i 's/goocanvas-2.0/goocanvas-3.0/' configure.ac
  autoreconf -fiv
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr --sysconfdir=/etc
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
