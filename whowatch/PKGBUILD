# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Nezmer <me at nezmer.info>

pkgname=whowatch
pkgver=1.8.6
pkgrel=2
pkgdesc='Displays information about the users currently logged on to the machine in real time'
url='https://wizard.uek.krakow.pl/~mike/#whowatch'
arch=('x86_64')
license=('GPL2')
depends=('glibc' 'ncurses')
source=(https://github.com/mtsuszycki/whowatch/releases/download/${pkgname}-${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha512sums=('49aa45b0a88a41c98fb7b548f0f098004c8bcca9ec078e977fcec8bc109c9beab25989003bfff4b3dc38a9d62d69378f64441753a1cca62729d2013d2ed73cb0'
            'SKIP')
validpgpkeys=(
  '610B28B55CFCFE45EA1B563B3116BA5E9FFA69A3' # Paul Wise
  '30639B823094E64015F7F40B3E6CB16A1D1AD8D5' # Mike Suszycki
)

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --bindir=/usr/bin \
    --mandir=/usr/share/man
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
