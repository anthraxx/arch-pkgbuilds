# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dan McGee <dan@archlinux.org>

pkgname=irssi
pkgver=1.4.2
pkgrel=1
pkgdesc='Modular text mode IRC client with Perl scripting'
url='https://irssi.org/'
arch=('x86_64')
license=('GPL')
depends=('glibc' 'glib2' 'openssl' 'libotr' 'perl' 'ncurses' 'libncursesw.so')
makedepends=('meson' 'ninja')
optdepends=('perl-libwww: for the scriptassist script')
replaces=('irssi-otr')
backup=('etc/irssi.conf')
source=(https://github.com/irssi/irssi/releases/download/${pkgver}/${pkgname}-${pkgver}.tar.xz{,.asc})
sha512sums=('428157b85226b5299f55679d9384d3ae8b1e61f50a528bf21ffdf2f4b56014e0a86bdcf9ce05cf4dedd59d53829323bb62029a570f90ebf3243a06c3ce220caf'
            'SKIP')
validpgpkeys=('7EE65E3082A5FB06AC7C368D00CCB587DDBEF0E1') # The Irssi project <staff@irssi.org>

build() {
  cd ${pkgname}-${pkgver}
  arch-meson Build \
    -Dwith-perl-lib=vendor \
    -Dwith-proxy=yes \
    -Dwith-otr=yes \
    -Dwith-perl=yes
  ninja -C Build
}

package() {
  cd ${pkgname}-${pkgver}
  DESTDIR="${pkgdir}" ninja -C Build install
  install -Dm 644 irssi.conf "${pkgdir}"/etc/irssi.conf
}

# vim: ts=2 sw=2 et:
