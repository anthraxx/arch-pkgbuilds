# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pkgname=flask-paranoid
pkgname=python-flask-paranoid
pkgver=0.2
pkgrel=8
pkgdesc='Simple user session protection for Flask applications'
url='https://github.com/miguelgrinberg/flask-paranoid/'
arch=('any')
license=('MIT')
depends=('python' 'python-flask')
makedepends=('python-sphinx' 'python-setuptools')
checkdepends=('python-pytest')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/miguelgrinberg/flask-paranoid/archive/v${pkgver}.tar.gz
        no-hyphens-in-cookie-dates.patch)
sha256sums=('cda72c43fd17e6b782e079942b79ec9b19ac40c06337c871cc1dfedbe49297d3'
            '2a2f746a795b442b2894af1754fc7db18fb90594099df0af169d190aafb0f7b1')
sha512sums=('0d642999fa4d5b795c3dcecc586fc1600cdfceec4adb38bb87ca8be9825d9ddafabb07d98df573a89e61a44a0abd27cbbd21524237b0e23b337a7d5e32ddfdae'
            '2cbabd825d962a79ce9f53694f4f9223156094d586754a64fd51216a8cf2486b1638f3668e85783ec46de184cf6cd9e2628fc860df3835295792d657bc2d2f19')

prepare() {
  cd ${_pkgname}-${pkgver}
  patch -Np1 -i ../no-hyphens-in-cookie-dates.patch
}

build() {
  cd ${_pkgname}-${pkgver}
  python setup.py build
  sphinx-build -b text docs docs/_build/text
  sphinx-build -b man docs docs/_build/man
}

check() {
  cd ${_pkgname}-${pkgver}
  python setup.py test
}

package() {
  cd ${_pkgname}-${pkgver}
  python setup.py install --root="${pkgdir}" --optimize=1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 docs/_build/text/*.txt -t "${pkgdir}/usr/share/doc/${pkgname}"
  install -Dm 644 docs/_build/man/flask-paranoid.1 "${pkgdir}/usr/share/man/man1/${pkgname}.1"
}

# vim: ts=2 sw=2 et:
