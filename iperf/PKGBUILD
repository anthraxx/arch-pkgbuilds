# Maintainer: SÃ©bastien Luttringer <seblu@archlinux.org>
# Contributor: Giovanni Scafora <giovanni@archlinux.org>
# Contributor: Dale Blount <dale@archlinux.org>

pkgname=iperf
pkgver=2.0.13
pkgrel=2
pkgdesc='A tool to measure maximum TCP bandwidth'
arch=('x86_64')
license=('custom')
url='https://sourceforge.net/projects/iperf2/'
depends=('gcc-libs')
install=iperf.install
source=("https://downloads.sourceforge.net/iperf2/iperf-$pkgver.tar.gz"
        'iperf-tcp.service'
        'iperf-udp.service')
md5sums=('31ea1c6d5cbf80b16ff3abe4288dad5e'
         '1d8660b540aa0db2e627d3d400cb8de4'
         'a27a8d27b1585bc999a04a2f082e2726')
prepare() {
  cd $pkgname-$pkgver
  # apply patch from the source array (should be a pacman feature)
  local src
  for src in "${source[@]}"; do
    src="${src%%::*}"
    src="${src##*/}"
    [[ $src = *.patch ]] || continue
    msg2 "Applying patch $src..."
    patch -Np1 < "../$src"
  done
  :
}

build() {
  cd $pkgname-$pkgver
  ./configure --prefix=/usr \
              --enable-ipv6 \
              --enable-multicast \
              --enable-threads \
              --enable-fastsampling
  make
}

package() {
  pushd $pkgname-$pkgver
  make DESTDIR="$pkgdir" install
  install -Dm644 COPYING "$pkgdir/usr/share/licenses/iperf/LICENSE"
  popd
  # systemd
  install -Dm644 iperf-tcp.service "$pkgdir/usr/lib/systemd/system/iperf-tcp.service"
  install -Dm644 iperf-udp.service "$pkgdir/usr/lib/systemd/system/iperf-udp.service"
}

# vim:set ts=2 sw=2 et:
