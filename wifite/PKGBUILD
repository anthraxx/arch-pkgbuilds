# Maintainer: Levente Polyak <levente[at]leventepolyak[dot]net>
# Contributor: Fabiano Furtado < fusca14 (at) gmail (dot) com >

pkgname=wifite
pkgver=2.6.0
pkgrel=1
epoch=2
pkgdesc='Tool to attack multiple WEP and WPA encrypted networks at the same time'
url='https://github.com/kimocoder/wifite2'
arch=('any')
license=('GPL2')
depends=('python' 'aircrack-ng' 'iproute2' 'iw')
makedepends=('python-setuptools')
optdepends=(
  'wireshark-cli: detecting WPS networks and inspecting handshake capture files'
  'reaver: WPS Pixie-Dust & brute-force attacks'
  'bully: WPS Pixie-Dust & brute-force attacks'
  'cowpatty: detect WPA handshakes'
  'pyrit: detect WPA handshakes'
  'macchanger: change MAC for attacks'
  'hashcat: cracking PMKID hashes'
  'hcxdumptool: capturing PMKID hashes'
  'hcxpcapngtool: converting PMKID packet captures into hashcat format'
)
provides=(wifite2)
source=(${url}/archive/${pkgver}/${pkgname}-${pkgver}.tar.gz)
sha512sums=('19fd6054287d9f6fd10dc76f04995e289ddb3c2a6344f108f5b07d895e9660942d4d72d6773a4446ee1e69e6f95ee645f13af0ee8822bfe158c9a3165b24c4ac')
b2sums=('867462d23dd48ac1b7141fc9307759f9e5dc4ba22b3c9605bbc440dcd99654c42da61c56637ef12afa4ff037d5db28650a701255adef88a1b6613c9508db40be')

prepare() {
  cd ${pkgname}2-${pkgver}
  sed 's|sbin|bin|g' -i setup.cfg
}

build() {
  cd ${pkgname}2-${pkgver}
  python setup.py build
}

package() {
  cd ${pkgname}2-${pkgver}
  python setup.py install --root="${pkgdir}" -O1 --skip-build
  install -Dm 644 README.md -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
