# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Maintainer: kpcyrd <kpcyrd[at]archlinux[dot]org>
# Contributor: Kevin MacMartin <prurigro@gmail.com>
# Contributor: openfbt
# Contributor: Werecat
# Contributor: Xyne

pkgname=cjdns
pkgver=20.7
pkgrel=1
pkgdesc='Routing engine designed for security, scalability, speed and ease of use'
url='https://github.com/cjdelisle/cjdns'
arch=('x86_64')
license=('GPL3')
depends=('glibc' 'sh')
optdepends=('nodejs: optional utilities support')
makedepends=('nodejs' 'python')
install=cjdns.install
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/cjdelisle/${pkgname}/archive/cjdns-v${pkgver}.tar.gz)
sha512sums=('db103e70e3d9f5e4958dcbeed51df358450f9489b6573f9b9eddc1839ee2051fe5180eede894b465e92915fabcb4cb64ce08a41c06450f14de75943879633f25')
b2sums=('e35e114b6e23735d316d2ab7385b81174a748c56b5da2326e4e956ed9b8151a487575e06b6576d556435908d87c6989a45b7a562a45acbbed4d835fdb4636c82')

build() {
  cd ${pkgname}-${pkgname}-v${pkgver}
  # Pretend to be cross compiling so -march=native is not set.
  CROSS="true" CJDNS_RELEASE_VERSION="${pkgver}" \
    node ./node_build/make.js
}

package() {
  cd ${pkgname}-${pkgname}-v${pkgver}
  install -Dm 755 cjdroute -t "${pkgdir}/usr/bin"
  install -Dm 644 contrib/systemd/{cjdns,cjdns-resume}.service \
    -t "${pkgdir}/usr/lib/systemd/system"
  install -Dm 644 doc/man/cjdroute.conf.5 -t "${pkgdir}/usr/share/man/man5"
  install -Dm 644 -t "${pkgdir}/usr/share/doc/${pkgname}" \
    README.md \
    doc/admin-api.md \
    doc/configure.md \
    doc/djc_layer_model.md \
    doc/nat-gateway.md \
    doc/network-services.md \
    doc/non-root-user.md \
    doc/security_specification.md \
    doc/shorewall_and_vpn_gateway_howto.md \
    doc/tunnel.md
  cp -a tools "${pkgdir}/usr/lib/${pkgname}"
  cp -a node_modules "${pkgdir}/usr/lib/${pkgname}/node_modules"
}

# vim: ts=2 sw=2 et:
