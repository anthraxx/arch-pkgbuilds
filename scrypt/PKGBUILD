# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Oliver Herold <oliver - at - akephalos - dot - de>

pkgname=scrypt
pkgver=1.3.1
pkgrel=2
pkgdesc='key derivation function and encryption utility'
arch=('x86_64')
depends=('glibc' 'openssl')
url='https://www.tarsnap.com/scrypt.html'
license=('BSD')
source=(https://www.tarsnap.com/scrypt/scrypt-${pkgver}.tgz)
sha256sums=('df2f23197c9589963267f85f9c5307ecf2b35a98b83a551bf1b1fb7a4d06d4c2')
b2sums=('91faa22dc317927b92abe26c5e15cd52c409722716076c7cc502a0c6e4e84ceaaf60222b3ef141c6a209f36084df2d7f45d8f19482aa035c3e2442513c4283d9')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
