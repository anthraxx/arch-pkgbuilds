# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Daniel Micay <danielmicay@gmail.com>
# Contributor: Oliver Herold <oliver - at - akephalos - dot - de>

pkgname=scrypt
pkgver=1.3.0
pkgrel=2
pkgdesc='key derivation function and encryption utility'
arch=('x86_64')
depends=('openssl')
url='https://www.tarsnap.com/scrypt/'
license=('BSD')
source=(https://www.tarsnap.com/scrypt/scrypt-${pkgver}.tgz)
sha512sums=('199f58adb5231f1ad585dbe8f64d6c54920cee201bd7703bcfc7e64917bf8647f9e35b3835b78c41210fd9af5223a79938f2495e4b48e6a62bfe8721d82492c2')

build() {
  cd ${pkgname}-${pkgver}
  ./configure --prefix=/usr
  make
}

check() {
  cd ${pkgname}-${pkgver}
  make test
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
  install -Dm 644 COPYRIGHT -t "${pkgdir}/usr/share/licenses/${pkgname}"
}

# vim: ts=2 sw=2 et:
