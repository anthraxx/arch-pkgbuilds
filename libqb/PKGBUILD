# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Marcin Wieczorek <marcin@marcin.co>
# Contributor: ovi chis <ovi@ovios.org>

pkgname=libqb
pkgver=2.0.6
pkgrel=1
pkgdesc='Library for providing high performance, reusable features for client-server architecture'
url='https://github.com/ClusterLabs/libqb'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc')
makedepends=('doxygen' 'splint')
checkdepends=('check')
provides=('libqb.so')
source=(https://github.com/ClusterLabs/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha512sums=('f1711b83bcc85220b4f435ef38643a8c183cc19705921cea15bcad9983948179bd7e31a800b8d880b802455b018d0bb8c7d800ebbc88b27721a59d9b1ffa33f3'
            'SKIP')
b2sums=('d218b09696318d6c1a26d567dec527fd246b4cc943302debc8a1f7c42ed5f273f577c7d81ea623b564456ab104fdc4c5b4c293c79d943333bda416bc25cc8b62'
        'SKIP')
validpgpkeys=('C5E29348A2B634E9F71B2014791890532CB5CDDE') # libqb Release Signing Key <users@clusterlabs.org>

prepare() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh
}

check() {
  cd ${pkgname}-${pkgver}
  make -C tests VERBOSE=1 check
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-fatal-warnings \
    --disable-static \
    --libdir=/usr/lib \
    --sbindir=/usr/bin \
    --localstatedir=/var
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
