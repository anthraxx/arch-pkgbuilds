# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>
# Contributor: Marcin Wieczorek <marcin@marcin.co>
# Contributor: ovi chis <ovi@ovios.org>

pkgname=libqb
pkgver=1.0.5
pkgrel=2
pkgdesc='Library for providing high performance, reusable features for client-server architecture'
url='https://github.com/ClusterLabs/libqb'
arch=('x86_64')
license=('LGPL2.1')
depends=('glibc')
makedepends=('doxygen' 'splint')
checkdepends=('check')
provides=('libqb.so')
source=(https://github.com/ClusterLabs/${pkgname}/releases/download/v${pkgver}/${pkgname}-${pkgver}.tar.gz{,.asc})
sha512sums=('78d3e5a33edaf2f2eb26e51055f1836180d22273b23e2a53ce77a5193e368a0b3beaa2739f99450ec4e6c004f6ce8dabbee07120fd9003a99e9178c0c379483e'
            'SKIP')
validpgpkeys=('EA78541A2D92451106C8A1F7B67157F3A70D4537') # Christine Caulfield (Chrissie at work) <ccaulfie@redhat.com>

prepare() {
  cd ${pkgname}-${pkgver}
  ./autogen.sh
}

check() {
  cd ${pkgname}-${pkgver}
  make -C tests VERBOSE=1 check
}

build() {
  cd ${pkgname}-${pkgver}
  ./configure \
    --prefix=/usr \
    --disable-fatal-warnings \
    --disable-static \
    --libdir=/usr/lib \
    --sbindir=/usr/bin
  make
}

package() {
  cd ${pkgname}-${pkgver}
  make DESTDIR="${pkgdir}" install
}

# vim: ts=2 sw=2 et:
