# Maintainer: Levente Polyak <anthraxx[at]archlinux[dot]org>

_pyname=colorclass
pkgname=python-colorclass
pkgver=2.2.0
pkgrel=10
pkgdesc='Yet another ANSI color text library for colorful worry-free console applications'
url='https://github.com/Robpol86/colorclass'
arch=('any')
license=('MIT')
depends=('python')
makedepends=('python-setuptools')
checkdepends=('python-pytest' 'python-docopt')
source=(${pkgname}-${pkgver}.tar.gz::https://github.com/Robpol86/colorclass/archive/v${pkgver}.tar.gz
        python310.patch)
sha512sums=('c627cdf6abdb5a60e9ecc1fb3b669898edc027fdc3f8cde31be2e10402ee308c2067fcd50d1228759e93fc58c521b7fc12e6b259233a17cf99f55cda26279fed'
            'b8da7b0cc2b8ca942b260323d3de78549b1ed041f38e66f68b615dea7412d01719a9aa467102a2ed67deeda15eae5adc2dac62232aab374b3a5113e00e1a96a0')

prepare() {
  cd ${_pyname}-${pkgver}
  patch -Np1 -i ../python310.patch
}

build() {
  cd ${_pyname}-${pkgver}
  python setup.py build
}

check() {
  cd ${_pyname}-${pkgver}
  py.test
}

package() {
  cd ${_pyname}-${pkgver}
  python setup.py install --root="${pkgdir}" --prefix=/usr -O1 --skip-build
  install -Dm 644 LICENSE -t "${pkgdir}/usr/share/licenses/${pkgname}"
  install -Dm 644 README.rst example.py -t "${pkgdir}/usr/share/doc/${pkgname}"
}

# vim: ts=2 sw=2 et:
