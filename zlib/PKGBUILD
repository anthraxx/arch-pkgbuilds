# Maintainer: Pierre Schmitz <pierre@archlinux.de>

pkgbase=zlib
pkgname=(zlib minizip)
epoch=1
pkgver=1.2.12
pkgrel=3
arch=('x86_64')
license=('custom')
url="https://www.zlib.net/"
depends=('glibc')
options=('staticlibs')  # needed by binutils testsuite
source=("https://zlib.net/zlib-${pkgver}.tar.gz"{,.asc}
        $pkgname-handle-incorrect-crc-inputs.patch::https://github.com/madler/zlib/commit/ec3df00224d4.patch
        $pkgname-CVE-2022-37434.patch::https://github.com/madler/zlib/commit/eff308af425b67093bab25f80f1ae950166bece1.patch
        $pkgname-CVE-2022-37434-fix.patch::https://github.com/madler/zlib/commit/1eb7682f845ac9e9bf9ae35bbfb3bad5dacbd91d.patch)
sha256sums=('91844808532e5ce316b3c010929493c0244f3d37593afd6de04f71821d5136d9'
            'SKIP'
            '00e023c3ccb7b895ebb3421970b1b77f8a527b40190f35050b79fd0e817a7b0c'
            '0bf4794975bd3be95f3f1d92cdf781a26c937d5c879b72939ae9cffbf6c430c7'
            'db41b76fd40bdc77b26e9a202177cee807da5e7cf751e72298d62742c349057d')
validpgpkeys=('5ED46A6721D365587791E2AA783FCD8E58BCAFBA')

prepare() {
	cd "${srcdir}/zlib-$pkgver"
	grep -A 24 '^  Copyright' zlib.h > LICENSE

	# https://github.com/madler/zlib/issues/613
	patch -Np1 -i ../$pkgname-handle-incorrect-crc-inputs.patch

	# CVE-2022-37434
	patch -Np1 -i ../$pkgname-CVE-2022-37434.patch
	patch -Np1 -i ../$pkgname-CVE-2022-37434-fix.patch
}

build() {
	cd "${srcdir}/zlib-$pkgver"

	CFLAGS+=" -ffat-lto-objects"
	./configure --prefix=/usr
	make

	cd contrib/minizip
	cp Makefile Makefile.orig
	cp ../README.contrib readme.txt
	autoreconf --install
	./configure --prefix=/usr --enable-static=no
	make
}

check() {
	cd "${srcdir}/zlib-$pkgver"
	make test

	cd contrib/minizip
	make -f Makefile.orig test
}

package_zlib() {
	pkgdesc='Compression library implementing the deflate compression method found in gzip and PKZIP'

	cd "${srcdir}/zlib-$pkgver"
	make install DESTDIR="${pkgdir}"
	install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/zlib/LICENSE"
}

package_minizip() {
	pkgdesc='Mini zip and unzip based on zlib'
	depends=('zlib')

	cd "${srcdir}/zlib-$pkgver/contrib/minizip"
	make install DESTDIR="${pkgdir}"
	install -D -m644 "${srcdir}/zlib-$pkgver/LICENSE" "${pkgdir}/usr/share/licenses/minizip/LICENSE"

	# https://github.com/madler/zlib/pull/229
	rm "${pkgdir}/usr/include/minizip/crypt.h"
}
